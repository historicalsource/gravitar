	.TITLE LOOSEU
	.ASECT
	.RADIX 16
	.INCLUDE LOONYC
	.NLIST
	.INCLUDE HLL65F
	.LIST
	.ENABL AMA
	.= 5000
	.SBTTL	START 2 PLAYER GAME
START2:	JSR CLRSCF
	DEC $$CRDT
	DEC $$CRDT
	INC N2PGAM
	IFEQ
	INC N2PGAM+1
	THEN
	LDX I,1
	BEGIN
	JSR SETLIV
	STA X,LIVES
	LDA I,2
	STA X,ASHSPD
	LDA I,1
	STA X,FUEL3
	LDA I,CNEWGA
	STA X,STATE
	LDA I,CDSETU
	STA X,DSTATE
	DEX
	MIEND
	STX PLADED
	STX PLADED+1
	JSR SETXR
	LDX I,1
	LDA I,0
	BEGIN
	STA X,WAVENO
	STA X,PLNACT
	STA X,GTIMER
	STA X,FIRPLA
	STA X,SSFLAG
	STA X,APDEAD
	DEX
	MIEND
	STA $BCCNT
	STA $BC
	STA PLAYUP
	LDX I,2

	STX ATRAFL
	RTS
	.PAGE
	.SBTTL	START 1 PLAYER GAME
START1:	JSR SETLIV
	STA LIVES
	LDA I,2
	STA ASHSPD
	DEC $$CRDT
	INC N1PGAM
	IFEQ
	INC N1PGAM+1
	THEN
	JSR CLRSCF
	LDA I,1
	STA ATRAFL
	STA FUEL3
	LDA I,0
	STA GTIMER
	STA GTIMER+1
	STA WAVENO
	STA PLNACT
	STA PLAYUP
	STA FIRPLA
	STA SSFLAG
	STA APDEAD
	STA $BCCNT
	STA $BC
	JSR SETXR
	LDA I,-1
	STA PLADED
	STA LIVES+1
	LDA I,CNEWGA
	STA STATE
	LDA I,CDSETU
	STA DSTATE
	RTS

CLRSCF: LDX I,5
	LDA I,0
	BEGIN
	STA X,FUEL1
	STA X,SCORE1
	DEX
	MIEND
	RTS

SETXR:  LDY I,1D
	LDX I,0E
	BEGIN
	LDA X,RNDINI
	STA Y,SIRNXR
	PHA
	ASL
	ASL
	ASL
	ASL
	STA Y,SAVBON
	DEY
	STA Y,SAVBON
	PLA
	STA Y,SIRNXR
	DEY
	DEX
	MIEND
	LDA I,-1
	STA DUCAXR
	STA DUCAXR+1	; NO ROOM LEFT FOR THIS GUY
	RTS

RNDINI:	.BYTE 2,4,6,8,9,9,9,9
	.BYTE 9,9,9,9,9,9,9
SETLIV: LDA OPTSW1
	AND I,0C
	LSR
	LSR
	TAY
	LDA Y,OPTLIV
	RTS

OPTLIV: .BYTE 2,3,4,5
	.PAGE
	.SBTTL PLAYER DEATH
DIED:	JSR ASINIT
	JSR CLRSHT
	JSR CLRVEL
	JSR INISOU
	LDX PLAYUP
	DEC X,LIVES
	IFMI
	LDA I,2
	STA PAUSTM
	LDA I,0
	STA MSGBT3
	LDA I,CEXPN2
	STA X,NXSTAT
	LDA I,CPAUSE
	STA X,STATE
	LDA I,CDPAUS
	STA X,DSTATE
	STX LASPLA
	ELSE
	LDA I,CEXPN2
	STA X,STATE
	LDA I,CDEXP2
	STA X,DSTATE
	LDA I,-1
	STA X,PLADED
	JSR SAVBAS
	JSR SAVFUE
	JSR SWAP
	THEN
	LDA I,1
	STA BSCALE
	LDA I,0
	LDX I,0C
	BEGIN
	STA X,LACPVF
	DEX
	MIEND
	STA TRSTAT
	STA THSTAT
	STA MSGBT3
	STA MSGBT4
	STA LANGLE
	STA SWCNT
	STA SPRKLE
	STX RACTVE
	STX LSCALE
	LDA I,38
	STA LACPVL
	STX LACPVH
	RTS
DIED2:	JSR ASINIT
	JSR CLRSHT
	JSR CLRVEL
	JSR INISOU
	JSR SAVEBO
	LDA I,-1	
	LDX PLAYUP
	STA X,PLADED
	LDA X,CRPLNT
	TAY
	LDA Y,PLANID
	CMP I,0A8
	IFEQ
	LDA I,0FF 
	STA APTIME
	LDY I,CDEXP2
	LDA I,CEXPN2
	ELSE
	LDA I,CNWVYG
	LDY I,CDSETU
	THEN
	PHA
	DEC X,LIVES
	IFMI
	LDA I,0
	STA MSGBT3
	LDA I,2
	STA PAUSTM
	LDA I,CPAUSE
	STA X,STATE
	PLA
	STA X,NXSTAT
	LDA I,CDPAUS
	STA X,DSTATE
	ELSE
	PLA
	STA X,STATE
	STY X,DSTATE
	STX LASPLA
	JSR SWAP
	THEN
	LDA I,38
	STA LACPVL
	LDA I,-1
	STA LSCALE
	STA RACTVE
	STA LACPVH
	LDA I,1
	STA BSCALE
	LDA I,0
	STA LACPHH
	STA LACPHL
	STA MSGBT3
	STA MSGBT4
	STA TRSTAT
	STA THSTAT
	STA SWCNT
	STA SPRKLE
	STA LANGLE
	RTS
SWAP:	LDX PLAYUP
	LDA X,LIVES
	IFMI
	AND LIVES
	AND LIVES+1
	IFMI
	LDA I,CENDGA
	STA STATE
	STA STATE+1
	RTS
	THEN
	THEN
	LDA PLAYUP
	EOR I,1
	TAX
	LDY X,LIVES
	IFMI
	EOR I,1
	THEN
	STA PLAYUP
	RTS

SAVEBO: LDX PLAYUP
	LDA X,CRPLNT
	ASL
	CLC
	ADC PLAYUP
	TAY
	LDA X,PLBONS
	STA Y,SAVBON
	RTS
	.PAGE
	.SBTTL LEAVE A PLANET
ENDVYG: JSR CLRSHT 
	LDX PLAYUP
	LDA X,CRPLNT
	TAX
	LDA X,PLCPVL
	ADC X,GBLTVL
	STA LACPVL
	LDA X,PLCPVH
	ADC X,GBLTVH
	STA LACPVH
	LDA X,PLCPHL
	ADC X,GBLTHL
	STA LACPHL
	LDA X,PLCPHH
	ADC X,GBLTHH
	STA LACPHH
	LDA X,ANGPTR
	STA LANGLE
	STA SWCNT
	LSR
	LSR
	LSR
	AND I,6
	STA QUADRANT
	LDA X,PLANID
	CMP I,0A8
	IFEQ
	LDA MKMDFL
	BMI MKMD2
	LDA I,-1
	STA APTIME
	LDA RACTVE
	IFPL
MKMD2:  TXA
	ASL
	ADC PLAYUP
	TAX
	LDA X,SIRNXR
	STA TEMP1
	LDA I,-1
	JSR LINK
	LDA I,9
	LDX PLAYUP
	STA X,WAVENO
	THEN
	ELSE
	LDX PLAYUP
	LDA X,CRPLNT
	ASL
	CLC
	ADC PLAYUP
	TAX
	LDA X,SIRNXR
	IFPL
	LDA MKMDFL
	BMI MKMD1
	LDA I,0
	LDX I,7
	BEGIN
	ORA X,BSTAT
	DEX
	MIEND
	TAX
	IFEQ
MKMD1:  JSR NXTWAV
	THEN
	THEN
	THEN
	JSR CLRVEL
	LDX PLAYUP
	LDA X,CRPLNT
	TAX
	LDA X,PLANID
	CMP I,0A8
	IFEQ
	LDA I,MESTIM?0FF
	AND MSGBT3
	STA MSGBT3
	LDA RACTVE
	IFPL
	JSR AWDFUE
	JSR AWDFUE
	JSR AWDFUE
	LDA I,0FF
	STA RACTVE
	THEN
	THEN
	
	LDY PLAYUP
	IFEQ
	BIT APDEAD
	ELSE
	BIT APDEAD+1
	THEN
	IFVS
	LDA I,0
	STA Y,APDEAD
	THEN
	JSR SAVBAS
	JSR SAVFUE
	LDA I,1
	STA BSCALE
	LDA I,0FF
	STA LSCALE
	LDA I,CEXPN2
	LDX PLAYUP
	STA X,STATE
	LDA I,CDEXP2
	STA X,DSTATE
	LDA X,SSFLAG
	AND I,1
	IFEQ
	JSR STRGRV
	ELSE
	JSR STRGR2
	THEN
	LDA I,0
	STA TRSTAT
	STA THSTAT
	RTS

GBLTHL:	.BYTE -60,-60,60,60,0A0,0,-60,0
	.BYTE 60,-0A0,-60,-60,60,60,0A0

GBLTHH:	.BYTE -1,-1,0,0,0,0,-1,0
	.BYTE 0,-1,-1,-1,0,0,0
GBLTVL: .BYTE -60,60,60,-60,0,-60,0,60
	.BYTE 0,-0A0,-60,60,60,-60,0

GBLTVH:	.BYTE -1,0,0,-1,0,-1,0,0
	.BYTE 0,-1,-1,0,0,-1,0
ANGPTR: .BYTE 28,38,8,18,10,20,30,0
	.BYTE 10,28,28,38,8,18,10
LPLCOR: LDA X,PLCPVL
	STA POINT1
	LDA X,PLCPVH
	STA POINT2
	LDA X,PLCPHL
	STA POINT3
	LDA X,PLCPHH
	STA POINT4
	RTS
SAVBAS: LDY I,7
	LDX PLAYUP
	LDA X,CRPLNT
	ASL
	CLC
	ADC PLAYUP
	TAX
	BEGIN
	LDA Y,BSTAT
	CLC
	IFNE
	SEC
	THEN
	ROL X,HBSTAT
	DEY
	MIEND
	RTS

SAVFUE: LDY I,7
	LDX PLAYUP
	LDA X,CRPLNT
	ASL
	CLC
	ADC PLAYUP
	TAX
	BEGIN
	LDA Y,FSTAT+8
	CLC
	IFNE
	SEC
	THEN
	ROR X,LFSTAT
	LDA Y,FSTAT
	CLC
	IFNE
	SEC
	THEN
	ROR X,HFSTAT
	DEY
	MIEND
	RTS

	.PAGE
NEWGAM:	LDA I,0FF
	STA LASPLN
	STA LSCALE
	STA RACTVE
	STA LACPVH
	STA APTIME
	LDA I,1
	STA BSCALE
	LDA I,38
	STA LACPVL
	JSR CLRSHT
	JSR CLRVEL
	LDA OPTSW1
	ASL
	ROL
	ROL
	AND I,3
	TAY
	LDA Y,BOLEVL
	LDX PLAYUP
	STA X,BSCOR3
	LDA I,0
	STA NGFLAG
	STA NGFLAG+1
	STA LACPHH
	STA LACPHL
	STA MSGBT2
	STA MSGBT3
	STA TRSTAT
	STA THSTAT
	STA LANGLE
	STA SPRKLE
	STA SWCNT
	JSR INISOU
	JSR STRGRV
	LDA GRAFCT
	STA GRAVTY
	JSR ASINIT
	LDX PLAYUP
	LDA I,CEXPN2
	STA X,STATE
	LDA I,CDEXP2
	STA X,DSTATE
	RTS


STRGRV: LDA I,STARVL
	STA CNTRVL
	LDA I,STARVH
	STA CNTRVH
	LDA I,STARHL
	STA CNTRHL
	LDA I,STARHH
	STA CNTRHH
	RTS

STRGR2: LDA PLCPVL+10
	STA CNTRVL
	LDA PLCPVH+10
	STA CNTRVH
	LDA PLCPHL+10
	STA CNTRHL
	LDA PLCPHH+10
	STA CNTRHH
	RTS
ASINIT: LDX I,1
	BEGIN
	LDY PLAYUP
	LDA Y,SSFLAG
	AND I,1
	IFEQ
	JSR INITEM
	ELSE
	JSR INITM3
	THEN
	LDA X,ASTART
	STA X,ASDETM
	DEX
	MIEND
	RTS
INITEM: LDA I,IASPVL
	STA X,ASCPVL
	LDA I,IASPVH
	STA X,ASCPVH
	LDA I,IASPHL
	STA X,ASCPHL
	LDA I,IASPHH
	STA X,ASCPHH
	RTS
INITM3: LDA PLCPHL+9
	STA X,ASCPHL
	LDA PLCPHH+9
	STA X,ASCPHH
	LDA PLCPVL+9
	STA X,ASCPVL
	LDA PLCPVH+9
	STA X,ASCPVH
	RTS

ASTART:	.BYTE 10,18
CLRSHT: LDY I,ASHOTS+PSHOTS+1
	LDA I,0
	BEGIN
	STA Y,SHACTM
	DEY
	MIEND
	LDX I,0F
	BEGIN
	STA X,EXTIME
	DEX
	MIEND
	RTS
	.PAGE
	.SBTTL	SET UP BONUS
NXTWAV:	LDX PLAYUP 
	LDY X,CRPLNT
	LDA Y,PLANID
	STY TEMP1
	CMP I,0A8
	IFEQ
	RTS
	THEN
	JSR PLACOR
	JSR STREXP
	JSR STREXP
	JSR STREXP
	JSR EXPLOS
	LDY TEMP1
	TYA
	ASL
	CLC
	ADC PLAYUP
	TAX
	LDA X,SIRNXR
	STA TEMP1
	LDA I,0FF
	STA X,SIRNXR
	LDA I,1C
	CLC
	ADC PLAYUP
	TAX
	LDY I,1C
	BEGIN
	LDA X,SIRNXR
	IFPL
	CMP TEMP1
	IFEQ
	CLC
	THEN
	IFCC
	INY
	LDA Y,PBONUS
	DEY
	THEN
	STA X,SIRNXR
	ASL
	ASL
	ASL
	ASL	
	STA X,SAVBON
	THEN
	DEY
	DEY
	DEX
	DEX
	MIEND
	LDX PLAYUP
	LDA X,SSFLAG
	AND I,3
	TAY
	BEQ CHEAT
	TXA
	BEGIN
	CLC
	ADC I,10.
	DEY
	EQEND
	TAX
CHEAT:	LDA X,SIRNXR
	AND X,DURNXR
	AND X,CNTCXR
	AND X,NIMIXR
LINK:	IFMI
	LDX PLAYUP
	LDA I,0
	STA X,PLNACT
	STA X,APDEAD
	LDA I,1C
	CLC
	ADC PLAYUP
	TAX
	BEGIN
	LDA TEMP1
	CLC
	ADC I,1
	CMP I,9
	IFCS
	LDA I,9
	THEN
	STA X,SIRNXR
	ASL
	ASL
	ASL
	ASL
	STA X,SAVBON
	DEX
	DEX
	MIEND
	LDA I,38
	STA LACPVL
	LDA I,-1
	STA LACPVH
	STA DUCAXR
	STA DUCAXR+1	; NO ROOM LEFT
	LDA I,0
	STA SWCNT
	STA LANGLE
	STA QUADRANT
	STA LACPHL
	STA LACPHH
	LDX PLAYUP
	LDA X,SSFLAG
	PHA
	AND I,3
	CMP I,2
	PLA
	ADC I,1			;USE CARRY FROM COMPARE
	STA X,SSFLAG
	JSR ASINIT
	THEN
	LDX PLAYUP
	LDY X,CRPLNT
	TYA
	ASL
	ADC PLAYUP
	STA X,LASPLN
	STA TEMP1
	LDA X,PLNACT
	LDY TEMP1
	AND Y,PLACTV
	STA X,PLNACT
	RTS
	.PAGE
	.SBTTL	ATTRACT MODE


ATRACT:	LDA I,-1
	STA PLADED
	STA PLADED+1
	STA CRPLNT
	LDA OPTSW2
	STA $CMODE
	LDA I,0
	STA MSGBT2
	LDA FRAME
	AND I,3
	IFEQ
	DEC POINT8
	THEN
	LDA POINT8
	CMP I,0C0
	IFCS
	LDA I,40
	LDY I,1
	ELSE
	LDA I,0
	LDY I,2
	THEN
	STA LSCALE
	STY BSCALE
	LDA POINT8
	IFEQ
	LDA I,CATRSE
	STA STATE
	THEN
	JSR CLRSHT
	LDA I,CDATRC
	STA DSTATE
	RTS


PBONUS:	.BYTE 2,9,4,9,6,9,8,9,9,9
	.BYTE 9,9,9,9,9,9,9,9,9,9
	.BYTE 9,9,9,9,9,9,9,9,9,9
.PAGE
.SBTTL INITIALIZE BASE POSITIONS




INBAPS:
	LDA I,-1
	BEGIN
	CLC
	ADC I,8
	DEX
	MIEND
	TAY
	LDX I,7
	BEGIN
	LDA AY,EABPVL
	STA X,BACPVL	;TAKE INITIAL BASE COORDS
	LDA AY,EABPVH	;FROM ROM AND STORE INTO RAM
	STA X,BACPVH
	LDA AY,EABPHL
	STA X,BACPHL
	LDA AY,EABPHH
	STA X,BACPHH
	DEY
	DEX
	MIEND
	RTS



EABPVL:	.BYTE 60,0E0,40,60,60,40,60,0E0
IBPSVL:	.BYTE 90,30,30,0D0,90,0F0,30,30

CTBPVL: .BYTE 20,60,20,60,20,60,20,0C0
NIBPVL: .BYTE 40,80,-20,-40,60,-0E0,0A0,-40
NOBPVL:	.BYTE 0C0,80,0A0,40,40,0C0,20,40
ERBPVL:	.BYTE 0A0,0E0,0E0,0E0,0,0C0,0,60
BZBPVL:	.BYTE 0A0,0E0,0E0,0A0,0E0,80,60,60
DUBPVL: .BYTE 0,0A0,20,60,0C0,0E0,0C0,0E0
VLBPVL:	.BYTE 0C0,60,0A0,0E0,60,0C0,0E0,80
NOBPV2:	.BYTE 0C0,80,0A0,40,40,0C0,20,40
DCBPVL:	.BYTE 0C0,0C0,0C0,0A0,40,0A0,0C0,0A0
FLBPVL:	.BYTE 0A0,0A0,0E0,60,0,0A0,40,0
SUBPVL:	.BYTE 60,0,80,0C0,0C0,60,20,80
DFBPVL: .BYTE 0,0,0,40,0A0,0E0,40,0A0

NSBAVL:	.BYTE 0C0,80,0A0,40,40,0C0,20,20
EABPVH:	.BYTE 0FD,0FD,0FD,0FC,0FC,0FD,0FD,0FD
IBPSVH:	.BYTE 0FC,0FD,0FD,0FB,0FD,0FC,0FD,0FD
CTBPVH: .BYTE 0FD,0FB,0FD,0FB,0FD,0FB,0FD,0FB
NIBPVH: .BYTE 0,0FE,0FE,0FE,2,0FD,1,0FF
NOBPVH: .BYTE 0FE,0,0FC,0FC,0,0FC,1,0FF
ERBPVH:	.BYTE 0FD,0FF,0FE,0FF,0FE,0FE,0,0FE
BZBPVH: .BYTE 0FC,0FE,0FE,0FC,0FD,0FB,0FD,0FD
DUBPVH: .BYTE 0FD,0FB,0FD,0FC,0FB,0FC,0FC,0FC
VLBPVH:	.BYTE 0FF,1,0FD,1,0FF,0,0FF,0FD
NOBP22: .BYTE 0FE,0,0FC,0FC,0,0FC,1,0FF
DCBPVH:	.BYTE 0FD,0FD,0FD,0FC,0FD,0FC,0FC,0FD
FLBPVH:	.BYTE 0FC,0FC,0FB,0FB,0FE,0FD,0FB,0FD
SUBPVH:	.BYTE 0FE,0FC,0FB,2,2,0FB,0FE,1
DFBPVH:	.BYTE 0FB,0FD,0FD,0FB,0FC,0FC,0FB,0FC
NSBPVH:	.BYTE 0FE,0,0FC,0FC,0,0FC,1,0FF

EABPHL:	.BYTE 80,80,0A0,80,80,60,80,80

IBPSHL: .BYTE 0,0,60,0A0,40,80,0,0A0

CTBPHL: .BYTE 40,80,0C0,80,60,60,40,0A0
NIBPHL: .BYTE 0C0,0C0,0,0C0,80,0E0,0C0,0
NOBPHL:	.BYTE 80,0C0,0A0,80,80,80,0A0,80
ERPPHL:	.BYTE 80,0A0,0A0,60,80,40,80,80
BZBRHL:	.BYTE 0A0,0A0,60,60,0A0,0A0,0A0,60
DUBPHL: .BYTE 0A0,0C0,80,20,60,60,60,60
VLBPHL:	.BYTE 0,0A0,20,60,20,80,60,40
NOBPH2:	.BYTE 80,0C0,0A0,80,80,80,0A0,80
DCBPHL:	.BYTE 0,0,0,60,80,0A0,0E0,20
FLBPHL:	.BYTE 20,0A0,0C0,0,0C0,0E0,20,20
SUBPHL:	.BYTE 0E0,40,80,80,80,60,0E0,40
DFBPHL:	.BYTE 60,20,20,20,80,0,60,80
N3BPHL:	.BYTE 80,0C0,0A0,80,80,80,0A0,80
EABPHH: .BYTE 0F8,0FB,0FD,0FE,0,2,3,6

IBPSHH:	.BYTE 0F8,0F9,0FA,0FB,0FD,0,3,5


CTBPHH: .BYTE 0F8,0FA,0FC,0FE,0,2,4,6
NIBPHH: .BYTE 0FC,0FD,0FF,0FF,0,1,2,4
NOBPHH:	.BYTE 0FB,0FC,0FD,0FE,0FF,0,2,4
ERBPHH:	.BYTE 0F8,0F9,0FB,0FC,0FF,1,2,3
BZBRHH:	.BYTE 0F8,0F9,0FB,0FC,0FD,0FE,2,4
DUBPHH: .BYTE 0F7,0FA,0FB,0FD,1,2,3,5
VLBPHH: .BYTE 0FC,0FC,0FD,0FE,0,1,2,3
NOBP32:	.BYTE 0FB,0FC,0FD,0FE,0FF,0,2,4
DCBPHH:	.BYTE 0F8,0FA,0FC,0FD,0FE,0FF,1,5
FLBPHH:	.BYTE 0F9,0FA,0FB,0FE,0FF,1,2,4
SUBPHH:	.BYTE 0FB,0FC,0FD,0FE,1,2,3,4
DFBPHH:	.BYTE 0F9,0FA,0FC,0FD,0FE,1,4,6
N3BPHH:	.BYTE 0FB,0FC,0FD,0FE,0FF,0,2,4
	.PAGE
	.SBTTL	BEGIN NEW VOYAGE

;INPUTS:NONE
;
;
;OUTPUTS:LANDER POSITIONING INITIALIZED
;	 LANDER VELOCITIES INITIALIZED
;	 GRAVITY INITIALIZED
;	 A, X, BLITZED

NWVYGE:	LDA I,0
	LDX I,0D
	BEGIN
	STA X,LACPVF		;0 VELOCITIES + POSITIONS
	DEX
	MIEND
	LDX I,0A7
	BEGIN
	STA AX,BACPVH
	DEX
	EQEND
	JSR CLRSHT
	LDA I,4
	STA QUADRANT
	JSR INISOU
	LDA I,20
	STA LANGLE
	STA SWCNT
	STA ASDETM+2
	STA ASDETM+3
	LDX PLAYUP
	LDA X,CRPLNT
	TAX
	LDA X,INLAVP
	STA LACPVH
	LDA ATRAFL
	IFNE
	LDA X,PLANID
	CMP I,0A8
	IFNE
	JSR SETLVL
	ELSE
	JSR REINSN
	THEN
	THEN
	JSR INPNTR
	LDX PLAYUP
	LDY X,WAVENO
	LDA OPTSW1
	AND I,10
	IFEQ
	LDA Y,GRAFCT
	ELSE
	LDA Y,EGRAVY
	THEN
	STA GRAVTY		;INITIALIZE GRAVITY
	LDA Y,BPATSP
	STA X,PATSPD
	LDA I,10
	STA PMXVEL
	LDA I,-10
	STA NMXVEL
	STA X,PLADED
	LDA Y,BASPRO
	STA X,SHOTPR
	LDA I,0
	STA CNTRVH
	STA CNTRVL
	STA CNTRHH
	STA CNTRHL
	STA EGEFLG
	STA THSTAT
	STA TRSTAT
	STA SKCTL
	STA ZOOMST
	STA FUESED
	STA LFTSCT
	LDA I,0FF
	STA LACPHH
	STA LAVEVH
	STA LAVEVL
	STA BASTAT
	STA LSCALE
	STA APTIME
	STA OVRSCN
	LDA X,CRPLNT
	TAX
	LDA X,PLANID
	IFMI
	LDA I,0
	STA OVRSCN
	LDA I,7
	ELSE
	LDA I,8
	THEN
	STA SECTOR
	CPX I,0C
	IFEQ
	LDA I,-1
	STA CNTRVH
	THEN
	LDA I,3
	STA SKCTL
	LDA I,4
	STA THRSTV
	STA THRSTH
	LDA I,40
	STA SCTCNT
	STA LACPHL
	JSR INBAPS
	LDX PLAYUP
	LDA X,CRPLNT
	ASL
	CLC
	ADC PLAYUP
	TAY
	LDA Y,SIRNXR
	IFPL
	LDA X,PLNACT
	AND Y,PLACTV
	IFEQ
	LDA Y,PLACTV
	ORA X,PLNACT
	STA X,PLNACT
	LDA Y,SIRNXR
	ASL
	ASL
	ASL
	ASL
	STA X,PLBONS
	LDA X,WAVENO
	CMP I,7
	IFCS
	LDA I,7
	THEN
	LDY PLAYUP
	LDX Y,CRPLNT
	IFNE
	BEGIN
	CLC
	ADC I,8
	DEX
	EQEND
	THEN
	TAY
	LDX PLAYUP
	LDA X,CRPLNT
	ASL
	CLC
	ADC PLAYUP
	TAX
	LDA Y,NFUELL
	STA X,LFSTAT
	LDA Y,NFUELH
	STA X,HFSTAT
	CPY I,20
	IFCS
	LDA I,-1
	ELSE
	LDA Y,NBALIV
	THEN
	STA X,HBSTAT
	ELSE
	LDA Y,SAVBON
	STA X,PLBONS
	THEN
	THEN
	LDX PLAYUP
	LDY X,WAVENO
	LDA OPTSW1
	AND I,10
	IFEQ
	LDA Y,BULRMP
	ELSE
	LDA Y,BULSHT
	THEN
	CLC
	ADC X,SSFLAG
	CMP I,ASHOTS-1
	IFCS
	LDA I,ASHOTS-1
	THEN
	STA NBULET
	LDA X,CRPLNT
	TAX
	LDA X,PLANID
	AND I,20
	IFEQ
	LDX PLAYUP
	LDA X,CRPLNT
	ASL
	CLC
	ADC PLAYUP
	TAX
	LDA X,LFSTAT
	STA TEMP3
	LDA X,HFSTAT
	STA TEMP4
	LDA X,HBSTAT
	STA TEMP1
	ELSE
	LDX PLAYUP
	LDY X,SSFLAG
	CPY I,6
	IFCS 
	LDY I,6
	THEN
	LDA Y,APTMIN
	STA APTIME
	LDA I,0
	STA TEMP3
	STA TEMP4
	LDY I,0FF
	LDA X,SSFLAG
	CMP I,2
	IFCC
	INY
	THEN
	STY TEMP1
	THEN
	JSR IBSTAT
	LDX I,0F
	BEGIN
	LSR TEMP4
	ROR TEMP3
	IFCS
	LDA I,8
	STA X,FSTAT
	ELSE
	LDA I,0
	STA X,FSTAT
	THEN
	DEX
	MIEND
	LDA I,2
	STA BSCALE
	LDX PLAYUP
	LDA I,CEXPND
	STA X,STATE
	LDA I,CDEXPN
	STA X,DSTATE
	RTS

APTMIN: .BYTE 23,21,19,19,17,15,13
BULRMP: .BYTE 1,2,2,3,3,4,4,5,5,6
BULSHT: .BYTE 0,0,0,1,1,2,2,3,3,4
BASPRO: .BYTE 2,2,2,4,6,8,10,20,30,40
BPATSP: .BYTE 1,1,2,3,4,5,6,7,8,8
INLAVP:	.BYTE 3,3,3,4,3,3,3,3,3,3
	.BYTE 3,3,1,3,3
GRAFCT:	.BYTE 10,10,10,11,13,16,1A,1F,25,28
EGRAVY: .BYTE 0,0,10,10,12,12,16,16,20,25
NFUELL: .BYTE 84,84,84,84,84,84,84,85
	.BYTE 4,4,4,4,4,4,4,4
	.BYTE 44,44,44,44,44,44,44,44
	.BYTE 90,90,90,90,90,90,90,90
	.BYTE 0,0,0,0,0,0,0,0
	.BYTE 18,18,18,18,18,18,18,18
	.BYTE 2,2,2,2,2,2,2,2
	.BYTE 11,11,11,11,11,11,11,11
	.BYTE 30,30,30,30,30,30,30,30
	.BYTE 0,0,0,0,0,0,0,0
	.BYTE 1,1,1,1,1,1,1,1
	.BYTE 14,14,14,14,14,14,14,14
	.BYTE 60,60,60,60,60,60,60,60
	.BYTE 0D8,0D8,0D8,0D8,0D8,0D8,0D8,0D8
	.BYTE 0,0,0,0,0,0,0,0
NFUELH:	.BYTE 0,0,0,0,0,0,0,8
	.BYTE 10,10,10,10,10,10,10,54
	.BYTE 2,2,2,2,2,2,2,42
	.BYTE 9,9,9,9,9,9,9,9
	.BYTE 0,0,0,