	.TITLE	LOONYM
	.RADIX	16
	.SBTTL	*********************************
	.SBTTL	*MODULE:LUNAR BATTLE MAIN	*
	.SBTTL	*				*
	.SBTTL	*PROGRAMMER:RICHARD ADAM	*
	.SBTTL	*				*
	.SBTTL	*FUNCTION:LOONY STATE HANDLER	*
	.SBTTL	*				*
	.SBTTL	*********************************

	.INCLUDE	LOONYC	;<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
	.INCLUDE	HLL65F	;<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<

	.ENABL AMA
	.LIST
	.PAGE
	.SBTTL	RAM DECLARATIONS
	;ZERO PAGE GLOBALS
	.GLOBB	XCOMP,VGLIST,VGBRIT,EASRCE
	;EXTERNAL ENTRY POINTS
	.GLOBL	VGADD,VGHALT,VGHEX,VGHEXZ,VGDOT
	.GLOBL	VGJMPL,VGJSRL,VGADD2,VGVCTR1,VGVCTR2,VGSTA1
	.GLOBL	VGRTSL,VGVCTR,VGCNTR,VGSTAT,VGSCAL,VGSCA1
	.GLOBL	START,VGVTR,VGMSGA,VGVTR1,VGMSG,MESGPOS
	.GLOBL	BASE01,BASE02,BASE03,BASE04,BASE05,BASE06,FUEL07,FUEL01
	.GLOBL BASE07,BASE08
	.GLOBL BASE09,BASE10,BASE11,BASE12,BASE13,BASE14,BASE15
	.GLOBL FLAM01,FLAM02,FLAM03,FLAM04,FLAM05,FLAM06,FLAM07,FLAM08
	.GLOBL FLAM09,FLAM10,FLAM11,FLAM12,FLAM13,FLAM14,FLAM15,FLAM16
	.GLOBL FLAM17,FLAM18,FLAM19,FLAM20,FLAM21,FLAM22,FLAM23,FLAM24
	.GLOBL FLAM25,FLAM26,FLAM27,FLAM28,FLAM29,FLAM30,FLAM31,FLAM32
	.GLOBL	TRAC03,TRAC08,ASHIP1,ASHIP2,PTRL01
	.GLOBL	HOPLAN,SIRIN,DURIN,CENTRIC,NESCRE,ASTRE,NOVA
	.GLOBL	ERCTUS,BIZARR,VECRAN,VOLCAN,QUASAR,NOVA2
	.GLOBL	SIRN01,SIRN02,SIRN03,SIRN04,SIRN05,SIRN06,SIRN07,SIRN08
	.GLOBL	SIRN09,SIRN10,SIRN11,SIRN12,SIRN13,SIRN14,SIRN15,SIRN16
	.GLOBL	DURN01,DURN02,DURN03,DURN04,DURN05,DURN06,DURN07,DURN08
	.GLOBL	DURN09,DURN10,DURN11,DURN12,DURN13,DURN14,DURN15,DURN16
	.GLOBL	CNTC01,CNTC02,CNTC03,CNTC04,CNTC05,CNTC06,CNTC07,CNTC08
	.GLOBL	CNTC09,CNTC10,CNTC11,CNTC12,CNTC13,CNTC14,CNTC15,CNTC16
	.GLOBL	NIMI01,NIMI02,NIMI03,NIMI04,NIMI05,NIMI06,NIMI07,NIMI08
	.GLOBL	SNDON,MODSND,INISOU,SHJSRL,GATES,ATRAFL
	.GLOBL	TEMPA,PLADED,FRAME,SFREQ,TPFLG
	.GLOBL	SINDEX,POINT,CURRENT,FRAMES,COUNT1
	.GLOBL	HSCFLG,COMTIMER,POINTR,TEMP2,SHEPIC
	.GLOBL	FU1SN,FU0SN,EN1SN,EN0SN,SH0SN,SH1SN
	.GLOBL	SAUCSN,PL0FIR,PL1FIR,EXTLIF,EXPLOS,THRUSN
	.GLOBL NOVA01,NOVA02,NOVA03,NOVA04,NOVA05,NOVA06
	.GLOBL NOVA07,NOVA08,NOVA09,NOVA10,REACTR
	.GLOBL EASY01,EASY02,EASY03,EASY04,EASY05,EASY06,EASY07,EASY08
	.GLOBL EASY09,EASY10,EASY11,EASY12,EASY13,EASY14,EASY15,EASY16
	.GLOBL BIZR01,BIZR02,BIZR03,BIZR04,BIZR05,BIZR06,BIZR07,BIZR08
	.GLOBL BIZR09,BIZR10,BIZR11,BIZR12,BIZR13,BIZR14,BIZR15,BIZR16
	.GLOBL VOLC01,VOLC02,VOLC03,VOLC04,VOLC05,VOLC06,VOLC07,VOLC08
	.GLOBL VOLC09,VOLC10,LUNAR,BATTLE,WRBOOK,WRHIIN,EAZBOO,EAZHIS
	.GLOBL ERCT01,ERCT02,ERCT03,ERCT04,ERCT05,ERCT06,ERCT07,ERCT08
	.GLOBL ERCT09,ERCT10,ERCT11,ERCT12,ERCT13,ERCT14,ERCT15,ERCT16
	.GLOBL $CMODE,$COINA,$LAM,$LMBIT,$$CRDT,$CMODE,MOOLAH
	.GLOBL $CNSTT,$PSTSL,$INTCT,$LMTIM,$CNCT,$$CRDT,$CCTIM
	.GLOBL	EAUPD,EAZERO,REHIIN,EAFLG,EAREQU,EAZFLG
	.GLOBL	INBUFR,SCBUFR,BOOKKS,EAZBOO,POWERON,MAINLN
	.GLOBL	DIGITZ,POINT1,POINT2,POINT3,TEMP1,TEMP3
	.GLOBL	INHISC,TEMP4,DIFF1,SYNC,IRQ
	.PAGE
	.ASECT
	.=0

STATE:	.BLKB 1*2		;GAME STATE
TESTSW:	.BLKB 1
SYNC:	.BLKB 1
BUFRDY:	.BLKB 1
XCOMP:	.BLKB 4			;VECTOR INFORMATION USED BY VGUT
VGLIST:	.BLKB 2			;POINTER INTO OPEN VGBUFFER
VGBRIT:	.BLKB 1
LACPVF:	.BLKB 1.
LACPHF:	.BLKB 1.
LACPHL:	.BLKB 1		;LANDER X POSITION
LACPHH:	.BLKB 1
LACPVL:	.BLKB 1			;LANDER Y POSITION
LACPVH:	.BLKB 1
LANGLE:	.BLKB 1			;LANDER ANGLE
LAVEHH:	.BLKB 1			;LANDER X VELOCITIES
LAVEHL:	.BLKB 1
LAVEHF:	.BLKB 1
LAVEVH:	.BLKB 1			;LANDER Y VELOCITIES
LAVEVL:	.BLKB 1
LAVEVF:	.BLKB 1
GRAVTY:	.BLKB 1			;MAGNITUDE OF GRAVITY
MUPYER:	.BLKB 1			;MULTIPLER
MUPCAN:	.BLKB 1			;MULTIPLICAND
PRODUCT:.BLKB 2			;PRODUCT
RESL	=PRODUCT
RESH	=PRODUCT+1
SWITCH:	.BLKB 1		;SAVES SWITCH STATES
QUADRANT:	.BLKB 1		;QUADRANT THAT SHIP IS POINTING IN
POINTR:	.BLKB 2		;INDIRECT POINTERS
TEMP1:	.BLKB 1
TEMP2:	.BLKB 1
TEMP3:	.BLKB 1
TEMP4:	.BLKB 1
TEMPA:	.BLKB 2			;USED FOR SOUNDS
SWCNT:	.BLKB 1
THRSTV:	.BLKB 1			;THRUST MAGNITUDE V COMPONENT
THRSTH:	.BLKB 1			;THRUST MAGNITUDE H COMPONENT
SECTOR:	.BLKB 1			;RESIDENT SECTOR OF LANDER
LFTSCT:	.BLKB 1			;LEFTMOST SECTOR TO DISPLAY
OVRSCF:	.BLKB 1.
OVRSCN:	.BLKB 1			;LENGTH OF OVERSCAN
SCTCTF:	.BLKB 1.
SCTCNT:	.BLKB 1			;NUMBER OF DOTS INTO SECTOR
LSCALE:	.BLKB 1			;CURRENT LINEAR SCALE
XACCL:	.BLKB 1			;ACCUMULATOR OF Y LANDSCAPE POSITION LO
XACCH:	.BLKB 1			;ACCUMULATOR OF Y LANDSCAPE POSITION HI
YACCL:	.BLKB 1			;ACCUMULATOR OF X LANDSCAPE POSITION LO
YACCH:	.BLKB 1			;ACCUMULATOR OF X LANDSCAPE POSITION HI
LASFIR:	.BLKB 1
BASTAT:	.BLKB 1
ZOOMST:	.BLKB 1			;ZOOMING STATUS
POINT1:	.BLKB 1			;POINTS USED IN COLTST
POINT2:	.BLKB 1			;
POINT3:	.BLKB 1			;
POINT4:	.BLKB 1			;
POINT5:	.BLKB 1			;
POINT6:	.BLKB 1			;
POINT7:	.BLKB 1			;
POINT8:	.BLKB 1			;
IQTMP1:	.BLKB 1			;IRQ TEMP
OGLORY:	.BLKB 1			;FLAG (WHAT ELSE)
LIVES:	.BLKB 1*2
XACL2:	.BLKB 1
XACH2:	.BLKB 1
YACL2:	.BLKB 1
YACH2:	.BLKB 1
FUESED: .BLKB 1
WAVENO: .BLKB 1*2
TRSTAT: .BLKB 1			;TRACTOR STATUS
THSTAT: .BLKB 1			;THRUST STATUS
CRPLNT: .BLKB 1*2
FRAME:  .BLKB 1
WAIT:	.BLKB 1
CNTRVH: .BLKB 1
CNTRVL: .BLKB 1			;CENTER OF GRAVITY POINT
CNTRHH: .BLKB 1
CNTRHL: .BLKB 1
ASQUAD: .BLKB 1
XDELTA:	.BLKB 2			;INDIRECT POINTERS TO PLANET INFO
YDELTA:	.BLKB 2
SLOPE:	.BLKB 2
SCTVDL:	.BLKB 2
SEGCNT:	.BLKB 2
SGTBIX:	.BLKB 2
SCTVOS:	.BLKB 2
SECADR:	.BLKB 2
VLEGEL:	.BLKB 2
VLEGEH	=VLEGEL+1
FVOS:	.BLKB 2
FHOS:	.BLKB 2
BASADR:	.BLKB 2
BSINDX:	.BLKB 2
SHRANG: .BLKB 2
MNANGL: .BLKB 2
ANGLMA: .BLKB 2 
BAOSVL: .BLKB 2
BAOSHL: .BLKB 2
BASIZV:	.BLKB 2
BASIZH:	.BLKB 2
BASIZD:	.BLKB 2			;END OF POINTERS
EGEFLG: .BLKB 1
DIFF1:  .BLKB 1
DIFF2:  .BLKB 1
DIFF3:  .BLKB 1
DIFF4:  .BLKB 1
RNGFLG: .BLKB 1
SAVCOR: .BLKB 6			;COORDS OF ASHIPS (SAVED)
ASHPID: .BLKB 1			;INDEX OF ASHIP
EXCNTR: .BLKB 1			;INDEX OF CURRENT EXPLOSION
PLADED: .BLKB 1*2		;PLAYER ALIVE STATUS
SFREQ:	.BLKB 1			;USED IN SOUNDS
COMTIM:	.BLKB 1
SINDEX:	.BLKB 1
HSCFLG:	.BLKB 1
PLNACT: .BLKB 1*2		;ACTIVE PLANET STATUS BITS
POINT:	.BLKB NCHANL
CURRENT:	.BLKB NCHANL	;USED IN SOUND
FRAMES:	.BLKB NCHANL
COUNT1:	.BLKB NCHANL
BSCALE:	.BLKB 1			;BINARY SCALE
PLAYUP:	.BLKB 1			;WHO IS UP
ATRAFL: .BLKB 1			;ATRACT FLAG
$BCCNT: .BLKB 1			;COIN ROUTINE STUFF
$BC:	.BLKB 1
$CCTIM: .BLKB 3
$$CRDT: .BLKB 1
$CNCT:	.BLKB 1
$PSTSL: .BLKB 3
$CNSTT: .BLKB 3
$LMTIM: .BLKB 1
$CMODE: .BLKB 1
$INTCT: .BLKB 1
EASRCE: .BLKB 2			;INDIRECT POINTER FOR EAROM
ASHSPD:	.BLKB 2
PATSPD:	.BLKB 2
SHOTPR:	.BLKB 2
PLBONS:	.BLKB 2			;PLANET BONUS
DSTATE: .BLKB 2
NBULET: .BLKB 1
	.PAGE
.=	100
ASVETM: .BLKB 4			;ASHIP VEL TIMER
ASCPVF: .BLKB 4			;ASHIP V POSTION
ASCPVL: .BLKB 4
ASCPVH: .BLKB 4
ASCPHF: .BLKB 4
ASCPHL: .BLKB 4			;ASHIP H POSITION
ASCPHH: .BLKB 4
ASVEVF: .BLKB 4			;ASHIP V VELOCITY
ASVEVL: .BLKB 4
ASVEVH: .BLKB 4
ASVEHF: .BLKB 4			;ASHIP H VELOCITY
ASVEHL: .BLKB 4
ASVEHH: .BLKB 4
ASDETM: .BLKB 4			;ASHIP REACTIVATION TIMER
PMXVEL: .BLKB 1			;MAX VELOCITY FOR PLAYER-POSITIVE
NMXVEL: .BLKB 1			;MAX VELOCITY FOR PLAYER-NEG.
LASPLN: .BLKB 2			;LAST PLANET VISITED
TPFLG:  .BLKB 1
MSGBIT: .BLKB 1			;MESSAGE FLAGS
MSGBT2: .BLKB 1
APDEAD:	.BLKB 1*2		;ALIEN PLANET STATUS
APTIME:	.BLKB 1			;ALIEN PLANET TIMER
TOPLIM:	.BLKB 1			;TOP OF SCREEN LIMIT
BTMLIM:	.BLKB 1			;BOTTOM OF SCREEN LIMIT
PHLIM:	.BLKB 1			;SIDE OF SCREEN LIMIT
NHLIM:	.BLKB 1
RACTVE:	.BLKB 1			;REACTOR STATUS
SSFLAG:	.BLKB 1*2		;SOLOR SYSTEM COUNT
SIRNXR: .BLKB 1*2		;PLANET MULTIPLIERS
DURNXR: .BLKB 1*2
CNTCXR: .BLKB 1*2
NIMIXR: .BLKB 1*2
NOVAXR:	.BLKB 1*2
ERCTXR:	.BLKB 1*2
BIZRXR:	.BLKB 1*2
VECRXR:	.BLKB 1*2
VOLCXR:	.BLKB 1*2
NOV2XR:	.BLKB 1*2
LFSTAT:	.BLKB 20.		;FUEL STATUS FOR EACH PLANET-LO
HFSTAT:	.BLKB 20.		;FUEL STATUS FOR EACH PLANET-HI
HBSTAT:	.BLKB 20.		;BASE STATUS FOR EACH PLANET
SCORE1:	.BLKB 2			;SCORE LO
SCORE2:	.BLKB 2
SCORE3:	.BLKB 2
FUEL1:	.BLKB 2			;FUEL LO
FUEL2:	.BLKB 2
FUEL3:	.BLKB 2
NGFLAG:	.BLKB 2
LULSCA: .BLKB 1			;ATTRACT NAME LSCALE
LUBSCA: .BLKB 1			;ATTRACT NAME BSCALE
BALSCA: .BLKB 1			;ATTRACT NAME LSCALE
BABSCA: .BLKB 1			;ATTRACT NAME BSCALE
EAZFLG:: .BLKB 1			;EAROM STUFF
EAREQU:: .BLKB 1
EARWRQ:: .BLKB 1
EABAD::  .BLKB 1
EAFLG::  .BLKB 1
EABC::	.BLKB 1
EAX::	.BLKB 1
EACNT::	.BLKB 1
EASEL::	.BLKB 1
EACS::	.BLKB 1			;END EAROM
MSGBT3: .BLKB 1			;MESSAGE FLAGS
MSGBT4: .BLKB 1
FIRPLA:	.BLKB 2			;FIRST PLANET FLAG
	.PAGE

.ASECT
.= 200
BACPVH: .BLKB NBASES
SHCPVH:	.BLKB PSHOTS			;SHOT CURRENT POS V MSB
BSCPVH: .BLKB ASHOTS
ESCPVH: .BLKB 2
BACPVL: .BLKB NBASES
SHCPVL:	.BLKB PSHOTS			;SHOT CURRENT POS V LSB
BSCPVL: .BLKB ASHOTS
ESCPVL: .BLKB 2
BACPVF: .BLKB NBASES
SHCPVF:	.BLKB PSHOTS			;SHOT CURRENT POS V FRACTION
BSCPVF: .BLKB ASHOTS
ESCPVF: .BLKB 2
BACPHH: .BLKB NBASES
SHCPHH:	.BLKB PSHOTS			;SHOT CURRENT POS H MSB
BSCPHH: .BLKB ASHOTS
ESCPHH: .BLKB 2
BACPHL: .BLKB NBASES
SHCPHL:	.BLKB PSHOTS			;SHOT CURRENT POS H LSB
BSCPHL: .BLKB ASHOTS
ESCPHL: .BLKB 2
BACPHF: .BLKB NBASES
SHCPHF:	.BLKB PSHOTS			;SHOT CURRENT POS H FRACTION
BSCPHF: .BLKB ASHOTS
ESCPHF: .BLKB 2
SHVEVL:	.BLKB PSHOTS			;SHOT VELOCITY V LSB
BSVEVL: .BLKB ASHOTS
ESHVVL: .BLKB 2
SHVEVH:	.BLKB PSHOTS			;SHOT VELOCITY V MSB
BSVEVH: .BLKB ASHOTS
ESHVVH: .BLKB 2
SHVEVF:	.BLKB PSHOTS			;SHOT VELOCITY V FRACTION
BSVEVF: .BLKB ASHOTS
ESHVVF: .BLKB 2
SHVEHL:	.BLKB PSHOTS			;SHOT VELOCITY H LSB
BSVEHL: .BLKB ASHOTS
ESHVHL: .BLKB 2
SHVEHH:	.BLKB PSHOTS			;SHOT VELOCITY H MSB
BSVEHH: .BLKB ASHOTS
ESHVHH: .BLKB 2
SHVEHF:	.BLKB PSHOTS			;SHOT VELOCITY H FRACTION
BSVEHF: .BLKB ASHOTS
ESHVHF: .BLKB 2
BSTAT:  .BLKB NBASES
SHACTM:	.BLKB PSHOTS			;SHOT ACTIVE TIMER
BSACTM: .BLKB ASHOTS
ESHTIM: .BLKB 2
FSTAT:	.BLKB 16.

	.PAGE


EXCPVH: .BLKB NEXPLO*DOTS	;EXPLOSIONS DATA
EXCPVL: .BLKB NEXPLO*DOTS
EXCPVF: .BLKB NEXPLO*DOTS
EXCPHH: .BLKB NEXPLO*DOTS
EXCPHL: .BLKB NEXPLO*DOTS
EXCPHF: .BLKB NEXPLO*DOTS
EXVEVH: .BLKB NEXPLO*DOTS
EXVEVL: .BLKB NEXPLO*DOTS
EXVEVF: .BLKB NEXPLO*DOTS
EXVEHH: .BLKB NEXPLO*DOTS
EXVEHL: .BLKB NEXPLO*DOTS
EXVEHF: .BLKB NEXPLO*DOTS
EXTIME: .BLKB NEXPLO*DOTS

STCPVL: .BLKB 10		;STAR FIELD DATA
STCPVH: .BLKB 10
STCPHL: .BLKB 10
STCPHH: .BLKB 10
STBRIT: .BLKB 10
SCLDRL:	.BLKB 8			;HI SCORES LO
SCLDRM:	.BLKB 8
SCLDRH:	.BLKB 8
INLDRH:	.BLKB 8			;INITIALS LO
INLDRM:	.BLKB 8
INLDRL:	.BLKB 8
GTIMER: .BLKB 2			;GAME TIMER
N1PGAM: .BLKB 2			;# OF 1 PLAYER GAMES
BOOKKS	=N1PGAM			;POINTER FOR EAROM
N2PGAM:	.BLKB 2			;# 2 PLAYER GAMES
TOTIME: .BLKB 3			;GAME TIMES ACCUM.
TOTSCR: .BLKB 3			;TOTAL SCORE ACCUM.
SCBUFR: .BLKB 9			;BUFFERS FOR EAROM
INBUFR: .BLKB 9
LASPLA:	.BLKB 1			;LAST PLAYER UP
PAUSTM:	.BLKB 1			;PAUSE TIMER
NXSTAT:	.BLKB 2			;NEXT STATE
SAVBON: .BLKB 20.
	.MACRO LXL ARG
	.BYTE 0A2
	.WORD ARG
	.=.-1
	.ENDM

	.MACRO LAH ARG
	.BYTE 0A9
	.ENABL M68
	.WORD ARG
	.DSABL M68
	.=.-1
	.ENDM
	.ASECT
	.=PROG

	.PAGE
	.SBTTL COLD START
COLD:	SEI
	JMP POWERON
CKSUM0:: .BYTE 96
	.PAGE
	.SBTTL	MAINLINE

;INPUTS:GAMEBOARD INITIALIZED BY POWERON
;OUTPUTS:NONE

MAINLN:	LDA I,CATRCT		;SET ATTRACT MODE
	STA STATE
	STA STATE+1
	LDA I,CDATRC
	STA DSTATE
	STA DSTATE+1
	LDA I,2			;SET COIN MODE
	STA $CMODE
	LDA I,0
	STA ATRAFL
	STA FRAME
	JSR INHISC		;INITIALIZE HI SCORES
	JSR INISOU		;INITIALIZE SOUNDS
	JSR TRBFSC		;MOVE FROM EA BUFR TO TABLE
	BEGIN
	LDA I,0
	STA MSGBIT
	JSR EXSTAT		;DO GAME STATE
	JSR ALWAYS
	JSR DISPLAY
	LDA IN0
	AND I,MSTEST
	EQEND
	JMP POWERON
	.PAGE
	.SBTTL	EXECUTE APPROPRIATE GAME STATE

;INPUTS:STATE:CODE FOR STATE TO BE EXECUTED
;OUTPUTS:GAME EXECUTES APPROPRIATE GAME STATE
;	 THRU STALST

EXSTAT:	LDX PLAYUP
	LDA X,STATE
	TAX
	CPX I,ELIST-STALST
	IFCS
	BRK
	THEN
	LDA AX,STALST+1
	PHA
	LDA AX,STALST
	PHA
	RTS

;STATE ADDRESS LIST

STALST:	.WORD START1-1
	.WORD START2-1
	.WORD NEWGAM-1
	.WORD NWVYGE-1
	.WORD PLAY-1
	.WORD DIED-1
	.WORD DIED2-1
	.WORD ENDVYG-1
	.WORD ENDGAM-1
	.WORD PAUSE-1
	.WORD EXPAND-1
	.WORD ATRACT-1
	.WORD DEPSPA-1
	.WORD EXPAN2-1
	.WORD DSBATL-1
	.WORD GETINI-1
	.WORD ATRSEU-1
	.WORD ATRML1-1
	.WORD ATRML2-1
ELIST:

	.SBTTL	NON-STATE DEPENDENT PROCESSING

;INPUTS:NONE
;OUTPUTS:START SWITCHES CHECKED
;	 FREEPLAY CHECKED
;	 CREDITS UPDATED
;	 STATE UPDATED

ALWAYS:	LDX PLAYUP 
	INC FRAME
	LDA ATRAFL
	IFEQ
	LDY I,0FF
	LDA $$CRDT
	IFNE
	CMP I,2
	LDA SWITCH
	IFCS
	AND I,MSTRT2
	IFNE
	LDY I,CSTRT2
	JMP OUT4
	THEN
	THEN
	LDA SWITCH
	AND I,MSTRT1
	IFNE
	LDY I,CSTRT1
	THEN
	THEN
OUT4:	TYA
	IFPL
	STA X,STATE
	THEN
	THEN
	RTS
	.PAGE
	.SBTTL	START 2 PLAYER GAME
START2:	JSR CLRSCF
	DEC $$CRDT
	DEC $$CRDT
	INC N2PGAM
	IFEQ
	INC N2PGAM+1
	THEN
	LDX I,1
	BEGIN
	LDA I,2
	STA X,LIVES
	STA X,ASHSPD
	LDA I,1
	STA X,FUEL3
	LDA I,CNEWGA
	STA X,STATE
	DEX
	MIEND
	STA PLADED
	STA PLADED+1
	JSR SETXR
	LDA I,0
	STA WAVENO
	STA WAVENO+1
	STA PLNACT
	STA PLNACT+1
	STA PLAYUP
	STA GTIMER
	STA GTIMER+1
	STA FIRPLA
	STA FIRPLA+1
	STA SSFLAG
	STA SSFLAG+1
	LDA I,2
	STA ATRAFL
	LDA I,CDSETU
	STA DSTATE
	RTS
	.PAGE
	.SBTTL	START 1 PLAYER GAME
START1:	LDA I,2
	STA LIVES
	STA ASHSPD
	DEC $$CRDT
	INC N1PGAM
	IFEQ
	INC N1PGAM+1
	THEN
	LDA I,1
	STA ATRAFL
	JSR CLRSCF
	LDA I,1
	STA FUEL3
	LDA I,0
	STA GTIMER
	STA GTIMER+1
	STA WAVENO
	STA PLNACT
	STA PLAYUP
	STA FIRPLA
	STA SSFLAG
	JSR SETXR
	LDA I,-1
	STA PLADED
	STA LIVES+1
	LDA I,CNEWGA
	STA STATE
	LDA I,CDSETU
	STA DSTATE
	RTS

CLRSCF: LDX I,5
	LDA I,0
	BEGIN
	STA X,FUEL1
	STA X,SCORE1
	DEX
	MIEND
	RTS

SETXR:  LDY I,13
	LDX I,9
	BEGIN
	LDA X,RNDINI
	STA Y,SIRNXR
	PHA
	ASL
	ASL
	ASL
	ASL
	STA Y,SAVBON
	DEY
	STA Y,SAVBON
	PLA
	STA Y,SIRNXR
	DEY
	DEX
	MIEND
	RTS

RNDINI:	.BYTE 2,4,6,8,9,9,9,9,9,9
	.PAGE
	.SBTTL PLAYER DEATH
DIED:	JSR ASINIT
	JSR CLRSHT
	JSR CLRVEL
	LDX PLAYUP
	DEC X,LIVES
	IFMI
	LDA I,2
	STA PAUSTM
	LDA I,0
	STA MSGBT3
	LDA I,CEXPN2
	STA X,NXSTAT
	LDA I,CPAUSE
	STA X,STATE
	LDA I,CDPAUS
	STA X,DSTATE
	STX LASPLA
	ELSE
	LDA I,CEXPN2
	STA X,STATE
	LDA I,CDPAUS
	STA X,DSTATE
	LDA I,-1
	STA X,PLADED
	JSR SAVBAS
	JSR SAVFUE
	JSR SWAP
	THEN
	LDA I,1
	STA BSCALE
	LDA I,0
	LDX I,0C
	BEGIN
	STA X,LACPVF
	DEX
	MIEND
	STA TRSTAT
	STA THSTAT
	STA MSGBT3
	STA LANGLE
	STA SWCNT
	STX RACTVE
	STX LSCALE
	LDA I,38
	STA LACPVL
	STX LACPVH
	RTS
DIED2:	JSR ASINIT
	JSR CLRSHT
	JSR CLRVEL
	JSR SAVEBO
	LDA I,-1	
	LDX PLAYUP
	STA X,PLADED
	LDA X,CRPLNT
	TAY
	LDA Y,PLANID
	CMP I,0A0
	IFEQ
	LDA I,0FF 
	STA APTIME
	LDY I,CDEXP2
	LDA I,CEXPN2
	ELSE
	LDA I,CNWVYG
	LDY I,CDSETU
	THEN
	PHA
	DEC X,LIVES
	IFMI
	LDA I,0
	STA MSGBT3
	LDA I,2
	STA PAUSTM
	LDA I,CPAUSE
	STA X,STATE
	PLA
	STA X,NXSTAT
	LDA I,CDPAUS
	STA X,DSTATE
	ELSE
	PLA
	STA X,STATE
	STY X,DSTATE
	STX LASPLA
	JSR SWAP
	THEN
	LDA I,38
	STA LACPVL
	LDA I,-1
	STA LSCALE
	STA RACTVE
	STA LACPVH
	LDA I,1
	STA BSCALE
	LDA I,0
	STA LACPHH
	STA LACPHL
	STA MSGBT3
	STA TRSTAT
	STA THSTAT
	STA SWCNT
	STA LANGLE
	RTS
SWAP:	LDX PLAYUP
	LDA X,LIVES
	IFMI
	AND LIVES
	AND LIVES+1
	IFMI
	LDA I,CENDGA
	STA STATE
	STA STATE+1
	RTS
	THEN
	THEN
	LDA PLAYUP
	EOR I,1
	TAX
	LDY X,LIVES
	IFMI
	EOR I,1
	THEN
	STA PLAYUP
	JSR CLRSHT
	JSR CLRVEL
	RTS

SAVEBO: LDX PLAYUP
	LDA X,CRPLNT
	ASL
	CLC
	ADC PLAYUP
	TAY
	LDA X,PLBONS
	STA Y,SAVBON
	RTS
	.PAGE
	.SBTTL LEAVE A PLANET
ENDVYG: 
	LDX PLAYUP
	LDA X,CRPLNT
	TAX
	LDA X,PLCPVL
	ADC X,GBLTVL
	STA LACPVL
	LDA X,PLCPVH
	ADC X,GBLTVH
	STA LACPVH
	LDA X,PLCPHL
	ADC X,GBLTHL
	STA LACPHL
	LDA X,PLCPHH
	ADC X,GBLTHH
	STA LACPHH
	LDA X,ANGPTR
	STA LANGLE
	STA SWCNT
	LSR
	LSR
	LSR
	AND I,6
	STA QUADRANT
	LDA X,PLANID
	CMP I,0A0
	IFEQ
	LDA I,-1
	STA APTIME
	LDA RACTVE
	IFPL
	LDA I,-1
	JSR LINK
	LDA I,MAXWAV-1
	LDX PLAYUP
	STA X,WAVENO
	THEN
	ELSE
	LDX PLAYUP
	LDA X,CRPLNT
	ASL
	CLC
	ADC PLAYUP
	TAX
	LDA X,SIRNXR
	IFPL
	LDA I,0
	LDX I,7
	BEGIN
	ORA X,BSTAT
	DEX
	MIEND
	TAX
	IFEQ
	JSR NXTWAV
	THEN
	THEN
	THEN
	JSR CLRVEL
	JSR CLRSHT
	LDX PLAYUP
	LDA X,CRPLNT
	TAX
	LDA X,PLANID
	CMP I,0A0
	IFEQ
	LDA I,MESTIM?0FF
	AND MSGBT3
	STA MSGBT3
	LDA RACTVE
	IFPL
	JSR AWDFUE
	JSR AWDFUE
	JSR AWDFUE
	LDA I,0FF
	STA RACTVE
	THEN
	THEN
	
	LDY PLAYUP
	IFEQ
	BIT APDEAD
	ELSE
	BIT APDEAD+1
	THEN
	IFVS
	LDA I,0
	STA Y,APDEAD
	THEN
	JSR SAVBAS
	JSR SAVFUE
	LDA I,1
	STA BSCALE
	LDA I,0FF
	STA LSCALE
	LDA I,CEXPN2
	LDX PLAYUP
	STA X,STATE
	LDA I,CDEXP2
	STA X,DSTATE
	LDA X,SSFLAG
	AND I,1
	IFEQ
	JSR STRGRV
	ELSE
	JSR STRGR2
	THEN
	LDA I,0
	STA TRSTAT
	STA THSTAT
	JSR EN0SN
	RTS

GBLTHL:	.BYTE -50,-50,50,50,0A0,0,-50,0,50,-0A0

GBLTHH:	.BYTE -1,-1,0,0,0,0,-1,0,0,-1

GBLTVL:	.BYTE -50,50,50,-50,0,-50,0,50,0,-0A0

GBLTVH:	.BYTE -1,0,0,-1,0,-1,0,0,0,-1

ANGPTR: .BYTE 28,38,8,18,10,20,30,0,10,28

LPLCOR: LDA X,PLCPVL
	STA POINT1
	LDA X,PLCPVH
	STA POINT2
	LDA X,PLCPHL
	STA POINT3
	LDA X,PLCPHH
	STA POINT4
	RTS
SAVBAS: LDY I,7
	LDX PLAYUP
	LDA X,CRPLNT
	ASL
	CLC
	ADC PLAYUP
	TAX
	BEGIN
	LDA Y,BSTAT
	CLC
	IFNE
	SEC
	THEN
	ROL X,HBSTAT
	DEY
	MIEND
	RTS

SAVFUE: LDY I,7
	LDX PLAYUP
	LDA X,CRPLNT
	ASL
	CLC
	ADC PLAYUP
	TAX
	BEGIN
	LDA Y,FSTAT+8
	CLC
	IFNE
	SEC
	THEN
	ROR X,LFSTAT
	LDA Y,FSTAT
	CLC
	IFNE
	SEC
	THEN
	ROR X,HFSTAT
	DEY
	MIEND
	RTS

	.PAGE
	.SBTTL DEEP SPACE
DEPSPA:	
	LDA I,1
	STA BSCALE
	LDX PLAYUP
	LDA X,PLADED
	IFNE
	JSR UPLAPS ; UP SHIP POS.
	JSR UPDVEL	; UP VELOCITY
	JSR PTGRAV
	JSR UPDROT	; ROTATE SHIP
	JSR PLFIRE	; PLAYER FIRE
	JSR CDPVSE	; SHIP VS. SHOT COLLISION THIS IS A TEST
	JSR CDEVSP
	JSR UPDFUL	; UP FUEL COUNT
	JSR PLANCK	; CHECK IN PLANETS RANGE
	JSR SBTEST
	THEN
	JSR SEASVE
	JSR UPASPS
	JSR ASSCTL
	JSR UPASTM
	JSR UPAPTM
	JSR UPSHOP
	JSR UPEXPO
	JSR CKENDW
	JSR UPASPD
	LDX PLAYUP
	LDA I,CDDPSP
	STA X,DSTATE
	RTS

DSBATL:	
	LDA I,1
	STA BSCALE
	LDA I,60
	STA LSCALE
	LDX PLAYUP
	LDA X,PLADED
	IFNE
	JSR UPDVEL
	JSR UPDROT
	JSR UPLAPS
	THEN
	LDX ASHPID
	JSR ASHMOV
	JSR SETVEL
	JSR ONESHT
	JSR CDASHP
	JSR PLFIRE
	JSR SHTOSH
	JSR UPSHOP
	JSR CDPVSE
	JSR UPDFUL
	JSR DSBEND
	JSR UPEXPO
	JSR CKENDW
	JSR UPASPD
	LDX PLAYUP
	LDA I,CDDPSP
	STA X,DSTATE
	RTS

	.PAGE
ENDGAM:	JSR HISCHK	
	JSR UPBOOK
	JSR WRBOOK
	LDA I,0
	STA ATRAFL
	RTS
PAUSE: LDA PAUSTM
	IFMI
	LDY I,0FF
	STY LSCALE
	LDX PLAYUP
	LDY LASPLA
	LDA Y,NXSTAT
	STA X,STATE
	JSR SWAP
	THEN
	RTS


UPBOOK: LDA ATRAFL
	SED
	LDX I,2
	BEGIN
	LDA X,SCORE2-1
	CLC
	ADC TOTSCR
	STA TOTSCR
	LDA X,SCORE3-1
	ADC TOTSCR+1
	STA TOTSCR+1
	LDA TOTSCR+2
	ADC I,0
	STA TOTSCR+2
	DEX
	EQEND
	CLD
	LDA GTIMER
	CLC
	ADC TOTIME
	STA TOTIME
	LDA GTIMER+1
	ADC TOTIME+1
	STA TOTIME+1
	LDA TOTIME+2
	ADC I,0
	STA TOTIME+2
	RTS

	.PAGE
NEWGAM:	LDA I,0FF
	STA LASPLN
	STA LSCALE
	STA RACTVE
	LDA I,1
	STA BSCALE
	LDA I,38
	STA LACPVL
	LDA I,-1
	STA LACPVH
	STA APTIME
	JSR CLRSHT
	JSR CLRVEL
	LDA I,0
	STA APDEAD
	STA APDEAD+1
	STA NGFLAG
	STA NGFLAG+1
	STA LACPHH
	STA LACPHL
	STA MSGBT2
	STA MSGBT3
	STA TRSTAT
	STA THSTAT
	STA LANGLE
	STA SWCNT
	JSR INISOU
	JSR STRGRV
	LDA GRAFCT
	STA GRAVTY
	JSR ASINIT
	LDX PLAYUP
	LDA I,CEXPN2
	STA X,STATE
	LDA I,CDEXP2
	STA X,DSTATE
	RTS


STRGRV: LDA I,STARVL
	STA CNTRVL
	LDA I,STARVH
	STA CNTRVH
	LDA I,STARHL
	STA CNTRHL
	LDA I,STARHH
	STA CNTRHH
	RTS

STRGR2: LDA PLCPVL+0B
	STA CNTRVL
	LDA PLCPVH+0B
	STA CNTRVH
	LDA PLCPHL+0B
	STA CNTRHL
	LDA PLCPHH+0B
	STA CNTRHH
	RTS
ASINIT: LDX I,1
	BEGIN
	LDY PLAYUP
	LDA Y,SSFLAG
	AND I,1
	IFEQ
	JSR INITEM
	ELSE
	JSR INITM3
	THEN
	LDA X,ASTART
	STA X,ASDETM
	DEX
	MIEND
	RTS
INITEM: LDA I,IASPVL
	STA X,ASCPVL
	LDA I,IASPVH
	STA X,ASCPVH
	LDA I,IASPHL
	STA X,ASCPHL
	LDA I,IASPHH
	STA X,ASCPHH
	RTS
INITM3: LDA PLCPHL+9
	STA X,ASCPHL
	LDA PLCPHH+9
	STA X,ASCPHH
	LDA PLCPVL+9
	STA X,ASCPVL
	LDA PLCPVH+9
	STA X,ASCPVH
	RTS

ASTART:	.BYTE 10,18
CLRSHT: LDY I,ASHOTS+PSHOTS+1
	LDA I,0
	BEGIN
	STA Y,SHACTM
	DEY
	MIEND
	LDX I,0F
	BEGIN
	STA X,EXTIME
	DEX
	MIEND
	RTS
	.PAGE
	.SBTTL	SET UP BONUS
NXTWAV:	LDX PLAYUP 
	LDY X,CRPLNT
	LDA Y,PLANID
	CMP I,0A0
	IFEQ
	RTS
	THEN
	TYA
	ASL
	CLC
	ADC PLAYUP
	TAX
	LDA X,SIRNXR
	STA TEMP1
	LDA I,0FF
	STA X,SIRNXR
	LDA I,12
	CLC
	ADC PLAYUP
	TAX
	LDY I,12
	BEGIN
	LDA X,SIRNXR
	IFPL
	CMP TEMP1
	IFEQ
	CLC
	THEN
	IFCC
	INY
	LDA Y,PBONUS
	DEY
	THEN
	STA X,SIRNXR
	ASL
	ASL
	ASL
	ASL	
	STA X,SAVBON
	THEN
	DEY
	DEY
	DEX
	DEX
	MIEND
	LDX PLAYUP
	LDA X,SSFLAG
	AND I,1
	IFEQ
	LDA X,SIRNXR
	AND X,DURNXR
	AND X,CNTCXR
	AND X,NIMIXR
	ELSE
	LDA X,ERCTXR
	AND X,BIZRXR
	AND X,VECRXR
	AND X,VOLCXR
	THEN
LINK:	IFMI
	LDX PLAYUP
	LDA I,0
	STA X,APDEAD
	LDA I,12
	CLC
	ADC PLAYUP
	TAX
	BEGIN
	LDA TEMP1
	CLC
	ADC I,1
	CMP I,9
	IFCS
	LDA I,9
	THEN
	STA X,SIRNXR
	ASL
	ASL
	ASL
	ASL
	STA X,SAVBON
	DEX
	DEX
	MIEND
	LDA I,38
	STA LACPVL
	LDA I,-1
	STA LACPVH
	LDA I,0
	STA SWCNT
	STA LANGLE
	STA QUADRANT
	STA LACPHL
	STA LACPHH
	LDX PLAYUP
	INC X,SSFLAG
	JSR ASINIT
	THEN
	LDX PLAYUP
	LDY X,CRPLNT
	TYA
	ASL
	STA TEMP1
	STA X,LASPLN
	LDA X,PLNACT
	LDY TEMP1
	AND Y,PLACTV
	STA X,PLNACT
	LDA I,0
	STA TRSTAT
	STA THSTAT
	RTS
	.PAGE
	.SBTTL	ATTRACT MODE


ATRACT:	LDA I,-1
	STA PLADED
	STA PLADED+1
	STA CRPLNT
	LDA I,0
	STA MSGBT2
	LDA FRAME
	AND I,3
	IFEQ
	DEC POINT8
	THEN
	LDA POINT8
	CMP I,0C0
	IFCS
	LDA I,40
	LDY I,1
	ELSE
	JSR NAMSCA
	LDA I,0
	LDY I,2
	THEN
	STA LSCALE
	STY BSCALE
	LDA POINT8
	IFEQ
	LDA I,CATRSE
	STA STATE
	THEN
	JSR CLRSHT
	LDA I,CDATRC
	STA DSTATE
	RTS


PBONUS:	.BYTE 2,9,4,9,6,9,8,9,9,9
	.BYTE 9,9,9,9,9,9,9,9,9,9
NAMSCA: LDA FRAME
	STA LULSCA
	LDA I,18
	SEC
	SBC FRAME
	STA BALSCA
	LDA I,3
	STA LUBSCA
	STA BABSCA
	RTS
.PAGE
.SBTTL INITIALIZE BASE POSITIONS




INBAPS: 
	CPX I,9
	IFEQ
	LDX I,4
	THEN
	LDA I,-1
	BEGIN
	CLC
	ADC I,8
	DEX
	MIEND
	TAY
	LDX I,7
	BEGIN
	LDA AY,EABPVL
	STA X,BACPVL	;TAKE INITIAL BASE COORDS
	LDA AY,EABPVH	;FROM ROM AND STORE INTO RAM
	STA X,BACPVH
	LDA AY,EABPHL
	STA X,BACPHL
	LDA AY,EABPHH
	STA X,BACPHH
	DEY
	DEX
	MIEND
	RTS



EABPVL:	.BYTE 60,0E0,40,60,60,40,60,0E0
IBPSVL:	.BYTE 90,50,30,0D0,90,0F0,30,30

CTBPVL: .BYTE 20,60,20,60,20,60,20,0C0
NIBPVL: .BYTE 40,80,-20,-40,60,-0E0,0A0,-40
NOBPVL:	.BYTE 0C0,80,0A0,40,40,0C0,20,40
ERBPVL:	.BYTE 0A0,0E0,0E0,0E0,0,0C0,0,60
BZBPVL:	.BYTE 0A0,0E0,0E0,0A0,0E0,80,60,60
DUBPVL: .BYTE 0,0A0,20,60,0C0,0E0,0C0,0E0
VLBPVL:	.BYTE 0C0,60,0A0,0E0,60,0C0,0E0,80

EABPVH:	.BYTE 0FD,0FD,0FD,0FC,0FC,0FD,0FD,0FD
IBPSVH:	.BYTE 0FC,0FD,0FD,0FB,0FD,0FC,0FD,0FD
CTBPVH: .BYTE 0FD,0FB,0FD,0FB,0FD,0FB,0FD,0FB
NIBPVH: .BYTE 0,0FE,0FE,0FE,2,0FD,1,0FF
NOBPVH: .BYTE 0FE,0,0FC,0FC,0,0FC,1,0FF
ERBPVH:	.BYTE 0FD,0FF,0FE,0FF,0FE,0FE,0,0FE
BZBPVH: .BYTE 0FC,0FE,0FE,0FC,0FD,0FB,0FD,0FD
DUBPVH: .BYTE 0FD,0FB,0FD,0FC,0FB,0FC,0FC,0FC
VLBPVH:	.BYTE 0FF,1,0FD,1,0FF,0,0FF,0FD

EABPHL:	.BYTE 80,80,0A0,80,80,60,80,80

IBPSHL: .BYTE 0,0E0,60,0A0,40,80,0,0A0

CTBPHL: .BYTE 40,80,0C0,80,60,60,40,0A0
NIBPHL: .BYTE 0C0,0C0,0FF,0C0,80,0E0,0C0,0FF
NOBPHL:	.BYTE 80,0C0,0A0,80,80,80,0A0,80
ERPPHL:	.BYTE 80,0A0,0A0,60,80,40,80,80
BZBRHL:	.BYTE 0A0,0A0,60,60,0A0,0A0,0A0,60
DUBPHL: .BYTE 0A0,0C0,80,20,60,60,60,60
VLBPHL:	.BYTE 0,0A0,20,60,20,80,60,40
EABPHH: .BYTE 0F8,0FB,0FD,0FE,0,2,3,6

IBPSHH:	.BYTE 0F8,0F8,0FA,0FB,0FD,0,3,5


CTBPHH: .BYTE 0F8,0FA,0FC,0FE,0,2,4,6
NIBPHH: .BYTE 0FC,0FD,0FE,0FF,0,1,2,3
NOBPHH:	.BYTE 0FB,0FC,0FD,0FE,0FF,0,2,4
ERBPHH:	.BYTE 0F8,0F9,0FB,0FC,0FF,1,2,3
BZBRHH:	.BYTE 0F8,0F9,0FB,0FC,0FD,0FE,2,4
DUBPHH: .BYTE 0F7,0FA,0FB,0FD,1,2,3,5
VLBPHH: .BYTE 0FC,0FC,0FD,0FE,0,1,2,3
	.PAGE
	.SBTTL	EXPAND VIEW

EXPAN2: LDA LSCALE
	CMP I,80
	IFCS 
	DEC LSCALE
	LDX PLAYUP
	LDA X,FIRPLA
	CMP I,1
	IFEQ
	LDA I,MSUPBO
	ORA MSGBT4
	STA MSGBT4
	THEN
	ELSE
	LDX PLAYUP
	LDA X,SSFLAG
	AND I,1
	IFEQ
	JSR STRGRV
	ELSE
	JSR STRGR2
	THEN
	LDA I,7
	STA PMXVEL
	LDA I,-7
	STA NMXVEL
	LDA I,1
	STA THRSTV
	STA THRSTH
	LDA I,CDPSPA
	STA X,STATE
	LDA X,FIRPLA
	CMP I,1
	IFEQ
	LDA I,MSUPBO?0FF
	AND MSGBT4
	STA MSGBT4
	INC X,FIRPLA
	THEN
	THEN
	LDX PLAYUP
	LDA I,CDEXP2
	STA X,DSTATE
	RTS
CLRVEL: LDA I,0
	LDX I,5
	BEGIN
	STA X,LAVEHH
	DEX
	MIEND
	RTS


EXPAND:	LDA LSCALE
	LDY I,1
	BEGIN
	LDX PLAYUP
	LDA X,CRPLNT
	TAX
	LDA LSCALE
	CMP X,PLSCAL
	IFCS
	DEC LSCALE
	ELSE
	LDA ATRAFL
	IFEQ
	LDA I,CATRM2
	ELSE
	LDA I,CPLAY
	THEN
	LDX PLAYUP
	STA X,STATE
	THEN
	DEY
	MIEND
	LDX PLAYUP
	LDA I,CDEXPN
	STA X,DSTATE
	RTS
PLSCAL: .BYTE 81,81,81,64,60,81,81,81,50,60

PBSCAL:	.BYTE 2,2,2,3,3,2,2,2,3,3
	.PAGE
	.SBTTL	BEGIN NEW VOYAGE

;INPUTS:NONE
;
;
;OUTPUTS:LANDER POSITIONING INITIALIZED
;	 LANDER VELOCITIES INITIALIZED
;	 GRAVITY INITIALIZED
;	 A, X, BLITZED

NWVYGE:	LDA I,0
	LDX I,0D
	BEGIN
	STA X,LACPVF		;0 VELOCITIES + POSITIONS
	DEX
	MIEND
	LDX I,0A7
	BEGIN
	STA AX,BACPVH
	DEX
	EQEND
	JSR CLRSHT
	LDA I,4
	STA QUADRANT
	LDA I,20
	STA LANGLE
	STA SWCNT
	LDX I,1
	BEGIN
	STA X,ASDETM+2
	DEX
	MIEND
	LDX PLAYUP
	LDA X,CRPLNT
	TAX
	LDA X,INLAVP
	STA LACPVH
	LDA ATRAFL
	IFNE
	LDA X,PLANID
	CMP I,0A0
	IFNE
	JSR SETLVL
	THEN
	THEN
	JSR INPNTR
	LDX PLAYUP
	LDY X,WAVENO
	LDA Y,GRAFCT
	STA GRAVTY		;INITIALIZE GRAVITY
	LDA Y,BPATSP
	STA X,PATSPD
	LDA I,10
	STA PMXVEL
	LDA I,-10
	STA NMXVEL
	STA X,PLADED
	LDA Y,BASPRO
	STA X,SHOTPR
	LDA I,0
	STA CNTRVH
	STA CNTRVL
	STA CNTRHH
	STA CNTRHL
	STA EGEFLG
	STA THSTAT
	STA TRSTAT
	STA SKCTL
	STA ZOOMST
	STA FUESED
	STA LFTSCT
	LDA I,0FF
	STA SCTCNT
	STA LACPHH
	STA LAVEVH
	STA LAVEVL
	STA BASTAT
	STA OVRSCN		;SET OVER SCAN
	STA LSCALE
	STA APTIME
	LDX PLAYUP
	LDA X,CRPLNT
	TAX
	LDA X,PLANID
	IFMI
	LDA I,0
	STA OVRSCN
	STA SCTCNT
	THEN
	LDA I,3
	STA SKCTL
	LDA I,7
	STA SECTOR
	LDA I,4
	STA THRSTV
	STA THRSTH
	JSR INBAPS
	LDX PLAYUP
	LDA X,CRPLNT
	ASL
	CLC
	ADC PLAYUP
	TAY
	LDA Y,SIRNXR
	IFPL
	LDA X,PLNACT
	AND Y,PLACTV
	IFEQ
	LDA Y,PLACTV
	ORA X,PLNACT
	STA X,PLNACT
	LDA Y,SIRNXR
	ASL
	ASL
	ASL
	ASL
	STA X,PLBONS
	LDA X,WAVENO
	LDY PLAYUP
	LDX Y,CRPLNT
	IFNE
	BEGIN
	CLC
	ADC I,8
	DEX
	EQEND
	THEN
	TAY
	LDX PLAYUP
	LDA X,CRPLNT
	ASL
	CLC
	ADC PLAYUP
	TAX
	LDA Y,NFUELL
	STA X,LFSTAT
	LDA Y,NFUELH
	STA X,HFSTAT
	LDA Y,NBALIV
	STA X,HBSTAT
	ELSE
	LDA Y,SAVBON
	STA X,PLBONS
	THEN
	THEN
	LDX PLAYUP
	LDY X,WAVENO
	LDA Y,BULRMP
	STA NBULET
	LDA X,CRPLNT
	TAX
	LDA X,PLANID
	AND I,20
	IFEQ
	LDX PLAYUP
	LDA X,CRPLNT
	ASL
	CLC
	ADC PLAYUP
	TAX
	LDA X,LFSTAT
	STA TEMP3
	LDA X,HFSTAT
	STA TEMP4
	LDA X,HBSTAT
	STA TEMP1
	ELSE
	LDX PLAYUP
	LDY X,SSFLAG
	CPY I,5
	IFCS 
	LDY I,5
	THEN
	LDA Y,APTMIN
	STA APTIME
	LDA I,0
	STA TEMP3
	STA TEMP4
	LDY I,0FF
	LDX PLAYUP
	LDA X,SSFLAG
	CMP I,2
	IFCC
	INY
	THEN
	STY TEMP1
	THEN
	JSR IBSTAT
	LDX I,0F
	BEGIN
	LSR TEMP4
	ROR TEMP3
	IFCS
	LDA I,8
	STA X,FSTAT
	ELSE
	LDA I,0
	STA X,FSTAT
	THEN
	DEX
	MIEND
	LDA I,2
	STA BSCALE
	LDX PLAYUP
	LDA I,CEXPND
	STA X,STATE
	LDA I,CDEXPN
	STA X,DSTATE
	RTS

APTMIN: .BYTE 23,21,19,17,15,13
BULRMP: .BYTE 4,5,6,7,8,9,9,9
	
BASPRO:	.BYTE 2,4,8,10,18,20,30,40
BPATSP:	.BYTE 2,3,4,5,6,7,8,9
INLAVP:	.BYTE 3,3,3,4,3,3,3,3,4,3
GRAFCT:	.BYTE 10,11,13,16,1A,1F,25,2B

NFUELL: .BYTE 84,84,84,84,84,84,84,85
	.BYTE 4,4,4,4,4,4,4,4
	.BYTE 44,44,44,44,44,44,44,44
	.BYTE 90,90,90,90,90,90,90,90
	.BYTE 0,0,0,0,0,0,0,0
	.BYTE 18,18,18,18,18,18,18,18
	.BYTE 2,2,2,2,2,2,2,2
	.BYTE 11,11,11,11,11,11,11,11
	.BYTE 30,30,30,30,30,30,30,30
NFUELH:	.BYTE 0,0,0,0,0,0,0,8
	.BYTE 10,10,10,10,10,10,10,54
	.BYTE 2,2,2,2,2,2,2,42
	.BYTE 9,9,9,9,9,9,9,9
	.BYTE 0,0,0,0,0,0,0,0
	.BYTE 24,24,24,24,24,24,24,24
	.BYTE 29,29,29,29,29,29,29,29
	.BYTE 0C,0C,0C,0C,0C,0C,0C,0C
	.BYTE 14,14,14,14,14,14,14,14

PLACTV:	.BYTE 1,1,2,2,4,4,8,8
	.BYTE 10,10,1,1,2,2,4,4
	.BYTE 8,8,10,10
IBSTAT: LDA I,7F 
	LDX I,7
	BEGIN
	ASL TEMP1
	IFCS
	LDA I,07F
	STA X,BSTAT
	ELSE
	LDA I,0
	STA X,BSTAT
	THEN
	DEX
	MIEND
	RTS

NBALIV:	.BYTE 28,28,28,28,28,28,28,0FF
	.BYTE 74,74,74,74,74,74,74,0FF
	.BYTE 0BE,0BE,0BE,0BE,0BE,0BE,0BE,0FF
	.BYTE 0FF,0FF,0FF,0FF,0FF,0FF,0FF,0FF
	.BYTE 0FF,0FF,0FF,0FF,0FF,0FF,0FF,0FF
	.BYTE 0FF,0FF,0FF,0FF,0FF,0FF,0FF,0FF
	.BYTE 0FF,0FF,0FF,0FF,0FF,0FF,0FF,0FF
	.BYTE 0FF,0FF,0FF,0FF,0FF,0FF,0FF,0FF
	.BYTE 8D,0DD,0FD,0FD,0FF,0FF,0FF,0FF
	.BYTE 0FF,0FF,0FF,0FF,0FF,0FF,0FF,0FF
SETLVL:	LDX PLAYUP
	LDA X,CRPLNT
	ASL
	CLC
	ADC PLAYUP
	TAX
	LDY PLAYUP
	LDA X,SIRNXR
	CMP I,MAXWAV-1
	IFCS
	LDA I,MAXWAV-1
	THEN
	STA Y,WAVENO
	RTS
PLEVEL:	.BYTE 0,4,7,1,5,7,2,6
	.BYTE 7,3,7,7,0,0,0,4
	.BYTE 7,7,5,7,7,6,7,7
	.BYTE 7,7,7,0,0,0
.PAGE
INPNTR:
	LDX PLAYUP
	LDA X,CRPLNT
	ASL
	CLC
	ADC I,BADPTR-XDPNTR
	STA TEMP2
	LDA I,BASADR-XDELTA
	STA TEMP3
	BEGIN
	LDY TEMP2
	LDA Y,XDPNTR
	LDX TEMP3
	STA X,XDELTA
	LDA Y,XDPNTR+1
	STA X,XDELTA+1
	LDA Y,BIXPTR
	STA X,BSINDX
	LDA Y,BIXPTR+1
	STA X,BSINDX+1
	LDA TEMP2
	SEC
	SBC I,YDPNTR-XDPNTR
	STA TEMP2
	LDA TEMP3
	SEC
	SBC I,2
	STA TEMP3
	CCEND
	RTS
XDPNTR:	.WORD XEASY,XSIRN,XCNTC,XNIMI,XNOVA,XERCT,XBIZR,XDURN,XVOLC,XNOVA
YDPNTR:	.WORD YEASY,YSIRN,YCNTC,YNIMI,YNOVA,YERCT,YBIZR,YDURN,YVOLC,YNOVA
SLPNTR:	.WORD SLEASY,SLSIRN,SLCNTC,SLNIMI,SLNOVA,SLERCT,SLBIZR,SLDURN,SLVOLC,SLNOVA
SVDPTR:	.WORD VDEASY,VDSIRN,VDCNTC,VDNIMI,VDNOVA,VDERCT,VDBIZR,VDDURN,VDVOLC,VDNOVA
CNTPTR:	.WORD CTEASY,CTSIRN,CTCNTC,CTNIMI,CTNOVA,CTERCT,CTBIZR,CTDURN,CTVOLC,CTNOVA
SIXPTR:	.WORD IXEASY,IXSIRN,IXCNTC,IXNIMI,IXNOVA,IXERCT,IXBIZR,IXDURN,IXVOLC,IXNOVA
VOSPTR:	.WORD OVEASY,OVSIRN,OVCNTC,OVNIMI,OVNOVA,OVERCT,OVBIZR,OVDURN,OVVOLC,OVNOVA
SADPTR:	.WORD EASYAD,SIRNAD,CNTCAD,NIMIAD,NOVAAD,ERCTAD,BIZRAD,DURNAD,VOLCAD,NOVAAD
YINITL:	.WORD 0FDA0,0FC70,0FB00,0,0FF00,0FC80,0FD40,0FDA0,0FE00,0FF00
FVOPTR: .WORD EAFVOS,SIFVOS,CTFVOS,NIFVOS,0,ERFVOS,BZFVOS,DUFVOS,VLFVOS,0
FHOPTR: .WORD EAFHOS,SIFHOS,CTFHOS,NIFHOS,0,ERFHOS,BZFHOS,DUFHOS,VLFHOS,0
BADPTR: .WORD EABADR,SIBADR,CTBADR,NIBADR,NOBADR,ERBADR,BZBADR,DUBADR,VLBADR,NOBADR
BIXPTR: .WORD EABIX,SIBIX,CTBIX,NIBIX,NOBIX,ERBIX,BZBIX,DUBIX,VLBIX,NOBIX
SRGPTR: .WORD EASHRG,SISHRG,CTSHRG,NISHRG,NOSHRG,ERSHRG,BZSHRG,DUSHRG,VLSHRG,NOSHRG
SMAPTR: .WORD EAMNAN,SIMNAN,CTMNAN,NIMNAN,NOMNAN,ERMNAN,BZMNAN,DUMNAN,VLMNAN,NOMNAN
SAMPTR: .WORD EAANMA,SIANMA,CTANMA,NIANMA,NOANMA,ERANMA,BZANMA,DUANMA,VLANMA,NOANMA
BOVLPT: .WORD EAOSVL,SIOSVL,CTOSVL,NIOSVL,NOOSVL,EROSVL,BZOSVL,DUOSVL,VLOSVL,NOOSVL
BOHLPT: .WORD EAOSHL,SIOSHL,CTOSHL,NIOSHL,NOOSHL,EROSHL,BZOSHL,DUOSHL,VLOSHL,NOOSHL
BSVPTR:	.WORD EASIZV,SISIZV,CTSIZV,NISIZV,SISIZV,SISIZV,SISIZV,SISIZV,SISIZV,SISIZV
BSHPTR: .WORD EASIZH,SISIZH,CTSIZH,NISIZH,SISIZH,SISIZH,SISIZH,SISIZH,SISIZH,SISIZH
BSDPTR:	.WORD EASIZD,SISIZD,CTSIZD,NISIZD,SISIZD,SISIZD,SISIZD,SISIZD,SISIZD,SISIZD
	.PAGE
	.SBTTL	EXECUTE PLAY STATE

;INPUTS:NONE
;OUTPUTS:

PLAY:	
	JSR UPSHOP
	JSR UPLAPS		;UPDATE LANDER POSITION
	JSR SHVSGR
	LDX PLAYUP
	LDA X,PLADED
	IFNE
	JSR UPDVEL		;UPDATE LANDER VELOCITY
	JSR ADGRAV
	JSR ADDRAG
	JSR UPDROT
	JSR PLFIRE
	JSR CDPVSE
	JSR COLDCT
	JSR CDREAC
	JSR SHTOPA
	JSR UPAPTM
	THEN
	JSR BAFIRE
	JSR TRACTR
	JSR CTLZUM
	JSR ZOOM
	JSR PATVEL
	JSR PATMOV
	JSR CKDONE
	JSR PSVSEB
	JSR CDPVSP
	JSR UPASTM
	JSR UPDFUL
	JSR UPEXPO
	JSR CKENDW
	JSR INMSG1
	JSR UPATSP
	LDX PLAYUP
	LDA I,CDPLAY
	STA X,DSTATE
	RTS
	.PAGE
	.SBTTL	SET-UP ATTRACT
ATRSEU:	LDA I,38
	STA LACPVL
	LDX I,-1
	STX LACPVH
	INX
	STX LACPHL
	STX LACPHH
	STX QUADRANT
	STX TRSTAT
	STX SIRNXR
	STX APDEAD
	LDA I,4
	STA SWCNT
	STA LANGLE
	STA THSTAT
	STX PLNACT
	STX PLAYUP
	STX SSFLAG
	STA THRSTV
	STA THRSTH
	LDA I,6
	STA PMXVEL
	LDA I,-6
	STA NMXVEL
	LDA I,7
	STA WAVENO
	JSR CLRVEL
	JSR CLRSHT
	LDA I,80
	STA LSCALE
	LDA I,1
	STA BSCALE
	LDA GRAFCT
	STA GRAVTY
	LDA I,CATRM1
	STA STATE
	LDA I,CDATR1
	STA X,DSTATE
	RTS
	.PAGE
	.SBTTL	SPACE ATRACT
ATRML1:	JSR ATLAPS
	JSR ATHRST
	JSR UPSHOP
	JSR ADGRAV
	JSR ATPLCK
	JSR UPEXPO
	JSR CKENDW
	JSR SETSTA
	LDA I,MDANGR!MATARI
	JSR CRDMES
	STA MSGBT3
	LDA I,CDATR1
	STA DSTATE
	RTS
	.PAGE
	.SBTTL	PLANET ATTRACT
ATRML2:	JSR UPSHOP
	LDA PLADED
	IFNE
	JSR ATLAPS
	JSR SHVSGR
	JSR ATUVEL
	JSR ADGRAV
	JSR ADDRAG
	JSR ATUROT
	JSR SETQUA
	JSR CDPVSE
	JSR COLDCT
	JSR SHTOPA
	JSR ATPLFR
	JSR ATRCTR
	JSR BAFIRE
	JSR CTLZUM
	JSR ZOOM
	JSR PATVEL
	JSR PATMOV
	JSR CKDONE
	JSR PSVSEB
	JSR CDPVSP
	THEN
	JSR UPEXPO
	JSR CKENDW
	LDA I,MSHBNK!MTRFUL
	JSR CRDMES
	STA MSGBT3
	JSR SETSTA
	LDA I,CDATR2
	STA DSTATE
	RTS

CRDMES: LDX $$CRDT
	IFNE
	ORA I,MCREDS
	ELSE
	AND I,MCREDS?0FF
	THEN
	RTS
ATUROT:
	LDA LANGLE
	CMP I,30
	IFCC
	CMP I,28
	IFCC
	INC LANGLE
	ELSE
	LDA FRAME
	CMP I,0D0
	IFCC
	INC LANGLE
	ELSE
	DEC LANGLE
	THEN
	THEN
	ELSE
	CMP I,38
	IFCS
	DEC LANGLE
	ELSE
	LDA FRAME
	CMP I,90
	IFCC
	INC LANGLE
	ELSE
	DEC LANGLE
	THEN
	THEN
	THEN
	RTS
SETSTA:	LDA STATE
	TAY
	CMP I,CPLAY
	IFEQ
	LDY I,CATRM2
	THEN
	CMP I,CDIED
	IFEQ
	LDY I,CATRCT
	THEN
	STY STATE
	LDA I,0FF
	STA POINT8
	RTS
	.PAGE
	.SBTTL	UPDATE VELOCITY

;INPUTS:GRAVYH,GRAVYL=GRAVITY
;	LAVEV,LAVEH=LANDER VELOCITIES
;OUTPUTS:

UPDVEL:	LDX PLAYUP
	LDA X,FUEL1
	ORA X,FUEL2
	ORA X,FUEL3
	IFEQ
	STA THSTAT
	LDA I,-1
	STA X,LIVES
	LDA I,CDIED
	LDX PLAYUP
	STA X,STATE
	RTS
	THEN
	LDA SWITCH
	AND I,MTHRST
	IFEQ
	LDA I,0
	STA THSTAT
	RTS
	THEN
	CLC
	SED
	LDA FUESED
	ADC I,6
	STA FUESED
	CLD
	STA THSTAT
	JSR THRUSN
ATHRST:	LDA LANGLE
	AND I,0F		;STRIP QUADRANT BITS
	TAY
	LDA QUADRANT		;DETERMINE QUADRANT
	LSR
	LSR
	IFCC
	LDA AY,COSTBL		;USE COS FOR QUADS 1,3
	ELSE
	LDA AY,SINTBL		;SIN FOR QUADS 0,2
	THEN
	STA MUPCAN
	LDA THRSTV		;IMES THRUST FOR V COMPONENT
	STA MUPYER
	JSR MULTIPLY
	LDY QUADRANT
	LSR RESH
	ROR RESL
	LSR RESH
	ROR RESL
	LSR RESH
	ROR RESL
	LDA RESL
	EOR AY,VSIGN		;FLIP SIGN ACCORDING TO QUADRANT
	STA RESL
	LDA RESH
	EOR AY,VSIGN
	STA RESH
	LDA LAVEVF
	CLC
	ADC RESL
	STA LAVEVF
	LDA LAVEVL
	ADC RESH
	STA LAVEVL
	IFMI			;SET SIGN OF VELOCITY
	LDA I,-1
	ELSE
	LDA I,0
	THEN
	STA LAVEVH
	TYA
	LSR
	LSR
	LDA LANGLE		;SAME METHOD FOR H COMPONENT
	AND I,0F
	TAY
	IFCC
	LDA AY,SINTBL
	ELSE
	LDA AY,COSTBL
	THEN
	STA MUPCAN
	LDA THRSTH
	STA MUPYER
	JSR MULTIPLY
	LDY QUADRANT
	LSR RESH
	ROR RESL
	LSR RESH
	ROR RESL
	LSR RESH
	ROR RESL
	LDA RESL
	EOR AY,HSIGN
	STA RESL
	LDA RESH
	EOR AY,HSIGN
	STA RESH
	LDA LAVEHF
	CLC
	ADC RESL
	STA LAVEHF
	LDA LAVEHL
	ADC RESH
	STA LAVEHL
	IFMI
	LDA I,-1
	ELSE
	LDA I,0
	THEN
	STA LAVEHH
	JMP MAXCHK
CKSUM1:: .BYTE 26
VSIGN:	.WORD 0,-1,-1,0
HSIGN:	.WORD 0,0,-1,-1
ATUVEL: LDA I,0
	STA THSTAT
	LDA LANGLE
	CMP I,30
	IFCS
	LDA LACPVH
	BMI THROK
	CMP I,3
	IFCC
THROK:	LDA RANDOM
	CMP I,10
	IFCS
	STA THSTAT
	JMP ATHRST
	THEN
	THEN
	THEN
	RTS
	.PAGE
UPDFUL:	LDX PLAYUP
	LDA FUESED
	IFNE
	LDA X,FUEL1
	ORA X,FUEL2
	ORA X,FUEL3
	IFNE
	SED
	LDA X,FUEL1
	SEC
	SBC FUESED
	STA X,FUEL1
	IFCC
	LDA X,FUEL2
	SBC I,0
	STA X,FUEL2
	PHP
	AND I,0F0
	IFEQ
	LDA X,FUEL3
	IFEQ
	JSR FU0SN
	THEN
	THEN
	PLP
	THEN
	IFCC
	LDA X,FUEL3
	SBC I,0
	STA X,FUEL3
	IFCC
	LDA I,0
	STA X,FUEL1
	STA X,FUEL2
	STA X,FUEL3
	THEN
	THEN
	CLD
	THEN
	THEN
	LDA I,0
	STA FUESED
	RTS
	.PAGE
	.SBTTL	KEEP VELOCITIES WITHIN LIMITS

;INPUTS:LAVEV,LAVEH=SHIP VELOCITIES
;
;
;OUTPUTS:

MAXCHK:	LDY I,0
	LDX PMXVEL
	LDA LAVEVL
	IFMI			;MINUS VEL CHECK MINUS LIMIT
	CMP NMXVEL
	IFCC
	LDA NMXVEL		;SET AT LIMIT
	STA LAVEVL
	STY LAVEVF
	THEN
	ELSE
	CMP PMXVEL
	IFCS
	STY LAVEVF
	STX LAVEVL
	THEN
	THEN
	LDA LAVEHL		;SAME PROCEDURE FOR H VEL
	IFMI
	CMP NMXVEL
	IFCC
	LDA NMXVEL
	STA LAVEHL
	STY LAVEHF
	THEN
	ELSE
	CMP PMXVEL
	IFCS
	STX LAVEHL
	STY LAVEHF
	THEN
	THEN
	RTS
	.PAGE
	.SBTTL	UPDATE ROTATION

;INPUTS:SWITCHS:STATE OF SWITCHES
;	LANGLE:CURRENT LANDER ANGLE
;	QUADRANT:CURRENT LANDER QUADRANT
;OUTPUTS:LANGLE,QUADRANT UPDATED
;	   A DESTROYED

UPDROT:
	LDX PLAYUP
	LDA X,FUEL1
	ORA X,FUEL2
	ORA X,FUEL3
	IFNE
	LDA SWITCH
	AND I,MROTRT
	IFNE
	INC SWCNT
	LDA SWCNT		;YES CHANGE ANGLE
	AND I,3F
	STA LANGLE
	THEN
	LDA SWITCH
	AND I,MROTLF
	IFNE
	DEC SWCNT
	LDA SWCNT
	AND I,3F
	STA LANGLE
	THEN
SETQUA:	LDA LANGLE		;SET QUADRANT
	LSR
	LSR			;D1 & D2 DEFINE QUADRANT
	LSR
	AND I,6
	STA QUADRANT
	THEN
	RTS
	.SBTTL	SIN AND QUADRANT TABLES

SINTBL:	.BYTE 0,19,32,4A,62,79,8E,0A2
	.BYTE 0B5,0C6,0D5,0E2,0ED,0F5,0FB,0FF
COSTBL:	.BYTE 0FF,0FB,0F5,0ED,0E2,0D5,0C6,0B5
	.BYTE 0A2,8E,79,62,4A,32,19,0
	.PAGE
	.SBTTL	ADD IN GRAVITY

ADGRAV: LDX PLAYUP
	LDA X,CRPLNT
	TAX
	LDA X,PLANID
	AND I,10
	BNE PTGRAV
	LDA X,PLANID
	AND I,20
	BNE SKIP2
	LDX PLAYUP
	LDA X,SSFLAG
	AND I,2
	BNE NEGRAV
	LDA LAVEVF
	SEC
	SBC GRAVTY
	STA LAVEVF
	LDA LAVEVL
	SBC I,0
	STA LAVEVL
	LDA LAVEVH
	SBC I,0
	STA LAVEVH
SKIP2:	RTS
NEGRAV:	LDA LAVEVF
	CLC
	ADC GRAVTY
	STA LAVEVF
	LDA LAVEVL
	ADC I,0
	STA LAVEVL
	LDA LAVEVH
	ADC I,0
	STA LAVEVH
	RTS
	.PAGE
	.SBTTL GRAVITY ABOUT A POINT



PTGRAV: LDA FRAME
	AND I,1
	IFEQ
	RTS
	THEN
	LDA LACPVL
	SEC
	SBC CNTRVL
	STA POINT1
	LDA LACPVH
	SBC CNTRVH
	STA DIFF1
	STA POINT2
	LDA LACPHL
	SEC
	SBC CNTRHL
	STA POINT3
	LDA LACPHH
	SBC CNTRHH
	STA POINT4
	STA DIFF2
	JSR COMPNT
	LDY PLAYUP
	LDA Y,STATE
	CMP I,CPLAY
	IFEQ
	LDA Y,SSFLAG
	AND I,2
	IFNE
	LDA DIFF1
	EOR I,80
	STA DIFF1
	THEN
	THEN
	LDA LAVEVF
	LDX DIFF1
	IFPL
	SEC
	SBC POINT1
	STA LAVEVF
	LDA LAVEVL
	SBC I,0
	STA LAVEVL
	LDA LAVEVH
	SBC I,0
	STA LAVEVH
	ELSE
	CLC
	ADC POINT1
	STA LAVEVF
	LDA LAVEVL
	ADC I,0
	STA LAVEVL
	LDA LAVEVH
	ADC I,0
	STA LAVEVH
	THEN
	LDA Y,STATE
	CMP I,CPLAY
	IFEQ
	LDA Y,SSFLAG
	AND I,2
	IFNE
	LDA DIFF2
	EOR I,80
	STA DIFF2
	THEN
	THEN
	LDA LAVEHF
	LDX DIFF2
	IFPL
	SEC
	SBC POINT3
	STA LAVEHF
	LDA LAVEHL
	SBC I,0
	STA LAVEHL
	LDA LAVEHH
	SBC I,0
	STA LAVEHH
	ELSE
	CLC
	ADC POINT3
	STA LAVEHF
	LDA LAVEHL
	ADC I,0
	STA LAVEHL
	LDA LAVEHH
	ADC I,0
	STA LAVEHH
	THEN
	RTS

COMPNT:	LDX I,2
	LDY I,0
	BEGIN
	LDA X,POINT2
	IFMI
	TYA
	SEC
	SBC X,POINT1
	STA X,POINT1
	TYA
	SBC X,POINT2
	STA X,POINT2
	THEN
	DEX
	DEX
	MIEND
	LDX I,2
	BEGIN
	LDY I,3
	BEGIN
	LSR X,POINT2
	ROR X,POINT1
	DEY
	MIEND
	LSR X,POINT1
	LSR X,POINT1
	LDA X,POINT1
	LSR
	CLC
	ADC X,POINT1
	STA X,POINT1
	DEX
	DEX
	MIEND
	RTS
	.PAGE
	.SBTTL	PUT DRAG ON SHIP

;INPUTS:DRGCNT:TIMER FOR ADDING INDRAG
;	LAVEXX:LANDER VELOCITIES
;	STICKY:DRAG CONSTANT
;
;
;OUTPUTS:DRGCNT UPDATED
;	LAVEXX UPDATED
;	A,X UPDATED
ADDRAG:
	LDX PLAYUP
	LDA X,CRPLNT
	TAX
	LDA X,PLANID
	AND I,20
	IFEQ
	LDX I,3
	LDY WAVENO
	BEGIN
	LDA X,LAVEHF
	IFNE
	LDA X,LAVEHH
	IFPL
	LDA X,LAVEHF
	SEC
	SBC Y,STICKY
	STA X,LAVEHF
	LDA X,LAVEHL
	SBC I,0
	STA X,LAVEHL
	LDA X,LAVEHH
	SBC I,0
	STA X,LAVEHH
	ELSE
	LDA X,LAVEHF
	CLC
	ADC Y,STICKY
	STA X,LAVEHF
	LDA X,LAVEHL
	ADC I,0
	STA X,LAVEHL
	LDA X,LAVEHH
	ADC I,0
	STA X,LAVEHH
	THEN
	THEN
	DEX
	DEX
	DEX
	MIEND
	THEN
	RTS
STICKY: .BYTE 3,3,3,3,2,2,2,2
	.PAGE
	.SBTTL	UPDATE LANDER POSITION

;INPUTS:LACPV,LACPH,SHIP CORRDINATES
;	LAVEV,LAVEH SHIP VELOCITY
;
;
;OUTPUTS:POSITION ARRAY UPDATED
;	 ACC DESTROYED

UPLAPS:	LDX PLAYUP
	LDA X,PLADED
	IFEQ
	RTS
	THEN
	LDA SWITCH
	AND I,MTRCTR
	IFNE
	SED
	LDA I,20
	CLC
	ADC FUESED
	STA FUESED
	CLD
	THEN
ATLAPS:	LDA LACPVF
	CLC
	ADC LAVEVF
	STA LACPVF
	LDA LACPVL
	ADC LAVEVL
	STA LACPVL
	LDA LACPVH
	ADC LAVEVH
	IFPL
	CMP I,5			;AT V LIMIT?
	IFCS
	LDY I,0			;YES SET COORDS AT MAX
	STY LACPVL
	LDA I,5
	THEN
	THEN
	STA LACPVH
	LDX PLAYUP
	LDA X,STATE
	CMP I,CATRM2
	BEQ EXCEPT
	CMP I,CPLAY
	IFEQ
EXCEPT:	JSR UPLSCTR		;UPDATE SECTOR INFORMATION
	JSR LIMTCK		;CHECK H LIMITS
	ELSE
	CMP I,CDSBTL
	IFEQ
	LDA I,-2
	STA TEMP1
	LDA I,2
	STA TEMP2
	LDA I,3
	STA TEMP3
	LDA I,-3
	STA TEMP4
	ELSE
	LDA I,0FD
	STA TEMP1
	LDA I,2
	STA TEMP2
	LDA I,4
	STA TEMP3
	LDA I,0FC
	STA TEMP4
	THEN
	JSR LIMEGE
	THEN
	LDA LACPHF
	CLC
	ADC LAVEHF
	STA LACPHF
	LDA LACPHL
	ADC LAVEHL
	STA LACPHL
	LDA LACPHH
	ADC LAVEHH
	STA LACPHH
	LDA EGEFLG
	IFNE
	JSR NEGVEL
	THEN
OUT:	RTS
NEGVEL:	BIT EGEFLG
	IFMI
	LDA I,0
	SEC
	SBC LAVEHF
	STA LAVEHF
	LDA I,0
	SBC LAVEHL
	STA LAVEHL
	LDA I,0
	SBC LAVEHH
	STA LAVEHH
	ELSE
	LDA I,0
	SEC
	SBC LAVEVF
	STA LAVEVF
	LDA I,0
	SBC LAVEVL
	STA LAVEVL
	LDA I,0
	SBC LAVEVH
	STA LAVEVH
	THEN
	RTS
	.PAGE
	.SBTTL	CHECK SCROLL LIMITS

; FUNCTION:	DECIDE WHETHER TO MOVE SHIP
;		OR TERRAIN

LIMTCK: LDX PLAYUP
	LDA X,CRPLNT
	TAX
	LDA X,PLANID
	AND I,0F0
	IFNE
	LDA X,NVLIMT
	STA TEMP1
	LDA X,PVLIMT
	STA TEMP2
	LDA X,PHLIMT
	STA TEMP3
	LDA X,NHLIMT
	STA TEMP4
	JMP LIMEGE
	THEN
	LDA LAVEHH
	AND LACPHH
	LDX LSCALE
	CPX I,50
	IFCC
	CMP I,0FF
	ELSE
	CMP I,0FD
	THEN
	IFCC
	CMP I,10
	IFCS
	JSR SCRADJ
	JSR UPDSCTR		;GET HERE IF TRAVELLING LEFT
	PLA			;AND AT LEFT LIMIT
	PLA			;SO GO SCROLL TERRAIN INSTEAD
	JMP OUT
	THEN
	THEN
	LDA LAVEHH
	IFPL
	LDA LACPHH
	LDX LSCALE
	CPX I,50
	IFCC
	CMP I,1
	ELSE
	CMP I,2
	THEN
	IFCS
	IFNE
	CMP I,10
	IFCC
	JSR SCRADJ
	JSR UPDSCTR		;AT RIGHT LIMIT AND
	PLA			;TRAVELLING TO THE RIGHT
	PLA
	JMP OUT
	THEN
	THEN
	THEN
	THEN
	RTS
NVLIMT:	.BYTE 0,0,0,0FA,0F8,0,0,0,0,0F8
PVLIMT:	.BYTE 0,0,0,6,3,0,0,0,4,3
PHLIMT:	.BYTE 0,0,0,6,8,0,0,0,8,8
NHLIMT:	.BYTE 0,0,0,0FA,0F8,0,0,0,0,0F8
	.PAGE
	.SBTTL STOP SHIP AT SCREEN EDGE - DEEP SPACE

LIMEGE: LDA I,0
	STA EGEFLG
	LDA LACPHH
	IFMI
	CMP TEMP4
	IFCC
	LDA I,80
	STA EGEFLG
	LDA I,8
	STA LACPHL
	INC LACPHH
	THEN
	ELSE
	CMP TEMP3
	IFCS
	LDA I,80
	STA EGEFLG
	LDA I,0F7
	STA LACPHL
	DEC LACPHH
	THEN
	THEN
	LDA LACPVH
	IFMI
	CMP TEMP1
	IFCC
	LDA I,40
	STA EGEFLG
	LDA I,0FF
	STA LACPVL
	THEN
	ELSE
	CMP TEMP2
	IFCS
	LDA I,40
	STA EGEFLG
	LDA I,0
	STA LACPVL
	THEN
	THEN
	RTS
	.PAGE
	.SBTTL	UPDATE LANDSCAPE SECTOR INFO

;INPUTS:	OVRSCN = AMMOUNT OF SCAN PAST LEFT
;		HDIR = AMMOUNT TO BUMP OVERSCAN
;		SECTOR = RESIDENT SECTOR OF LANDER
;		LFTSCT = LEFTMOST SECTOR #
;OUTPUTS:	OVRSCN,SECTOR,LFTSCT,A UPDATED

UPLSCTR:	LDA EGEFLG
	IFEQ
	CLC
	LDA SCTCTF
	ADC LAVEHF
	STA SCTCTF
	LDA SCTCNT
	ADC LAVEHL
	STA SCTCNT
	BIT LAVEHH
	IFPL
	IFCS
	INC SECTOR
	THEN
	ELSE
	IFCC
	DEC SECTOR
	THEN
	THEN
	LDA SECTOR
	AND I,0F
	STA SECTOR
	THEN
	RTS
UPDSCTR:	LDA OVRSCF
	CLC
	ADC LAVEHF
	STA OVRSCF
	LDA OVRSCN
	ADC LAVEHL
	STA OVRSCN
	BIT LAVEHH
	IFPL
	IFCS
	INC LFTSCT
	THEN
	ELSE
	IFCC
	DEC LFTSCT
	THEN
	THEN
	LDA LFTSCT
	AND I,0F
	STA LFTSCT
	RTS
.PAGE
.SBTTL ADJUST SHOT AND BASE COORDS DURING SCROLLING



SCRADJ:	LDX I,ASHOTS+PSHOTS+NBASES+1
	BEGIN
	LDA X, BACPHF	;ADD IN H VELOCITY TO POSITIONS
	SEC
	SBC 	LAVEHF
	STA	X,BACPHF
	LDA	X,BACPHL
	SBC	LAVEHL
	STA	X,BACPHL
	LDA	X,BACPHH
	SBC	LAVEHH
	STA	X,BACPHH
	JSR WRPRND
	CPX I,2
	IFCC
	LDA X,ASCPHF+2
	SEC
	SBC LAVEHF
	STA X,ASCPHF+2
	LDA X,ASCPHL+2
	SBC LAVEHL
	STA X,ASCPHL+2
	LDA X,ASCPHH+2
	SBC LAVEHH
	STA X,ASCPHH+2
	JSR WRPRND
	THEN
	DEX
	MIEND
	RTS


WRPRND: LDA X,BACPHH
	IFMI
	CMP I,0F8
	IFCC
	JMP BITXOR
	THEN
	ELSE
	CMP I,8
	IFCS
	JMP BITXOR
	THEN
	THEN
	RTS


BITXOR: LDA X,BACPHH
	IFPL
	SEC
	SBC I,10
	ELSE
	CLC
	ADC I,10
	THEN
	STA X,BACPHH
	RTS
	.PAGE



	.SBTTL	MOVE ALIEN SHIPS

UPASPS: LDX I,1
	BEGIN
	JSR ASHMOV
	DEX
	MIEND
	RTS
ASHMOV:
	LDA I,3
	STA TOPLIM
	LDA I,0FB
	STA BTMLIM
	STA NHLIM
	LDA I,4
	STA PHLIM
	LDY PLAYUP
	LDA Y,ASHSPD
	STA DIFF1
	LDA Y,APDEAD
	IFNE
	RTS
	THEN
ASHMV1:	LDA X,ASDETM
	IFNE
	RTS
	THEN
	LDA X,ASCPVH
	IFMI
	BIT BTMLIM
	BPL 10$
	CMP BTMLIM
	IFEQ
	CMP LACPVH
	IFCC
	CPX I,2
	IFCS
	LDA I,0
	STA X,ASVEVH
	STA X,ASVEVL
	STA X,ASVEVF
	ELSE
10$:	JSR MOVUP
	THEN
	ELSE
	JSR MOVUP
	THEN
	THEN
	ELSE
	CMP BTMLIM
	IFEQ
	CMP LACPVH
	IFCS
	CPX I,2
	IFCS
	LDA I,0
	STA X,ASVEVH
	STA X,ASVEVL
	STA X,ASVEVF
	ELSE
	JSR MOVUP
	THEN
	ELSE
	JSR MOVUP
	THEN
	ELSE
	CMP TOPLIM
	IFCS
	JSR MOVDN
	THEN
	THEN
	THEN
	LDA X,ASCPVF
	CLC
	ADC X,ASVEVF
	STA X,ASCPVF
	LDA X,ASCPVL
	ADC X,ASVEVL
	STA X,ASCPVL
	LDA X,ASCPVH
	ADC X,ASVEVH
	STA X,ASCPVH
	LDA X,ASCPHH
	IFMI
	CMP NHLIM
	IFEQ
	JSR MOVRT
	THEN
	ELSE
	CMP PHLIM
	IFCS
	JSR MOVLF
	THEN
	THEN
	LDA X,ASCPHF
	CLC
	ADC X,ASVEHF
	STA X,ASCPHF
	LDA X,ASCPHL
	ADC X,ASVEHL
	STA X,ASCPHL
	LDA X,ASCPHH
	ADC X,ASVEHH
	STA X,ASCPHH
	RTS
UPATSP:	LDA FRAME
	AND I,0FF
	IFEQ
	LDA GTIMER
	AND I,1
	IFEQ
	LDX PLAYUP
	CLC
	LDA X,PATSPD
	ADC I,1
	CMP I,10
	IFCS
	LDA I,10
	THEN
	STA X,PATSPD
	LDA X,WAVENO
	TAY
	LDA Y,SHRAMP
	CLC
	ADC X,SHOTPR
	IFMI
	LDA I,80
	THEN
	STA X,SHOTPR
	THEN
	THEN
	RTS

UPASPD:	LDA FRAME
	AND I,07F
	IFEQ
	LDX PLAYUP
	LDA X,ASHSPD
	CLC
	ADC I,1
	CMP I,7
	IFCS
	LDA I,7
	THEN
	STA X,ASHSPD
	THEN
	RTS
SHRAMP:	.BYTE 1,2,3,4,5,6,7,8
	.PAGE
	.SBTTL SET SHIP VEL
SEASVE: LDX I,1
	BEGIN
	JSR SETVEL
	DEX
	MIEND
	RTS
RNDVEL: 
	LDA X,ASVETM
	IFEQ
	JSR UPRDN
	JSR LFORRT
	ELSE
	DEC X,ASVETM
	THEN
	RTS

PATVEL: LDA I,1
	STA TEMP1
	LDX PLAYUP
	LDA X,PATSPD
	STA DIFF1
	LDX I,3
	BEGIN
	JSR SETVL1
	DEX
	DEC TEMP1
	MIEND
	RTS
PATPLM: .BYTE 3,3,3,0,0,4,3,3

PABTLM: .BYTE -1,-1,-1,-1,0,3,1,-1

PATMOV: LDA ZOOMST
	IFEQ
	LDX PLAYUP
	LDA X,PATSPD
	STA DIFF1
	LDA X,CRPLNT
	TAX
	LDA X,PLANID
	IFPL
	LDA X,PATPLM
	STA TOPLIM
	LDA X,PABTLM
	STA BTMLIM
	LDA I,0F7
	STA NHLIM
	LDA I,9
	STA PHLIM
	LDA I,1
	STA TEMP1
	LDX I,3
	BEGIN
	JSR ASHMV1
	LDA X,ASCPHH
	IFMI
	CMP I,0F8
	IFCC
	LDA I,7
	STA X,ASCPHH
	THEN
	ELSE
	CMP I,8
	IFCS
	LDA I,0F9
	STA X,ASCPHH
	THEN
	THEN
	DEX
	DEC TEMP1
	MIEND
	THEN
	THEN
	RTS
SETVEL:	LDY PLAYUP 
	LDA Y,ASHSPD
	STA DIFF1
	LDA Y,APDEAD
	IFEQ
SETVL1:	LDA X,ASVETM
	IFEQ
	LDA X,ASCPVL
	STA POINT1
	LDA X,ASCPVH
	STA POINT2
	LDA X,ASCPHL
	STA POINT3
	LDA X,ASCPHH
	STA POINT4
	JSR LODCOR
	STX TEMP4
	JSR ABSDIF
	LDX TEMP4
	CPX I,3
	IFEQ
	JSR MOVRT
	ELSE
	CPX I,2
	IFEQ
	JSR MOVLF
	ELSE
	LDA ASQUAD
	AND I,2
	IFEQ
	JSR MOVLF
	ELSE
	JSR MOVRT
	THEN
	THEN
	THEN
	LDA ASQUAD
	AND I,1
	IFEQ
	JSR MOVDN
	ELSE 
	JSR MOVUP
	THEN
	ELSE
	DEC X,ASVETM
	THEN
	THEN
	RTS

MOVRT:
	 LDA I,0
	STA X,ASVEHH
	LDA RANDOM
	STA X,ASVEHF
	AND I,0F
	STA X,ASVETM
	LDA DIFF1
	STA X,ASVEHL
	RTS


MOVLF:
	LDA I,0
	SEC
	SBC DIFF1
	STA X,ASVEHL
	LDA RANDOM
	STA X,ASVEHF
	AND I,0F
	STA X,ASVETM
	LDA I,0FF
	STA X,ASVEHH
	RTS


MOVUP:
	LDA I,0
	STA X,ASVEVH
	LDA RANDOM
	STA X,ASVEVF
	AND I,7
	STA X,ASVETM
	LDA DIFF1
	STA X,ASVEVL
	RTS


MOVDN:
	LDA I,0
	SEC
	SBC DIFF1
	STA X,ASVEVL
	LDA RANDOM
	STA X,ASVEVF
	AND I,7
	STA X,ASVETM
	LDA I,0FF
	STA X,ASVEVH
	RTS

UPRDN:  LDA RANDOM
	BMI MOVDN
	BPL MOVUP

LFORRT: LDA RANDOM
	BMI MOVLF
	JMP MOVRT
	
	.PAGE
	.SBTTL	UPDATE ZOOM STATUS
CTLZUM: LDX PLAYUP
	LDA X,CRPLNT
	TAX
	LDA X,PLANID
	IFPL
	LDA ZOOMST
	IFEQ			;IF ZOOM INACTIVE
	LDA LACPVH
	IFEQ			;IF @ ZOOM IN LIMIT-NEG.
	LDA LSCALE
	IFNE			;IF NOT ALREADY ZOOMED IN
	LDA I,80
	STA ZOOMST		;SET STATUS TO ZOOM IN
	THEN
	ELSE
	IFPL
	CMP I,3			;IF @ ZOOM OUT LIMIT
	IFCS
	LDA LSCALE		;IF NOT ALREADY ZOOMED OUT
	IFPL
	LDA I,40		;SET STATUS ZOOM OUT
	STA ZOOMST
	THEN
	THEN
	THEN
	THEN
	THEN
	THEN
	RTS
	.PAGE
	.SBTTL	ZOOM UPDATE
; FUNCTION:	UPDATE LINEAR SCALE TO ZOOM IN OR
;		OUT PICTURE
; INPUTS:	LACPVH: LANDER V POSITION HI BYTE
;		LSCALE: CURRENT LINEAR SCALE FACTOR
;		ZUMIN:	V COORD TO START ZOOMING IN
;		ZUMOUT:	V COORD TO START ZOOMING OUT
; OUTPUTS:	LSCALE:	UPDATED
;		ZOOMST:	UPDATED (IF NECESSARY)
;		LANDER COORDS: UPDATED (IF NECESSARY)
ZOOM:	BIT ZOOMST
	IFMI			;IF CURRENTLY ZOOMING IN
	LDA LSCALE
	IFNE			;IF NOT ZOOMED ALL THE WAY IN
	SEC
	SBC I,4
	STA LSCALE
	JSR ADDPOS
	LDA LACPHH
	IFMI
	CMP I,0FF
	IFCC			;IF HPOS PAST READJUST LIMITS
	JSR ADHPOS		;GO READJUST
	THEN
	ELSE
	CMP I,1
	IFCS
	JSR SBHPOS
	THEN
	THEN
	ELSE
	STA ZOOMST		;SET ZOOM STATUS INACTIVE
	THEN
	THEN
	BIT ZOOMST
	IFVS			;IF ZOOMING OUT
	LDA LSCALE
	IFPL			;IF NOT AT ZOOM OUT LIMIT
	CLC
	ADC I,4
	STA LSCALE
	JSR SUBPOS		;ADJUST VCOORD
	LDA LACPHH
	IFMI
	CMP I,0FF
	IFCC			;IF PAST NEGATIVE ADJUST LIMIT
	JSR SBHPOS		;ADJUST
	THEN
	ELSE
	CMP I,1
	IFCS			;IF PAST POSITIVE ADJUST LIMIT
	JSR ADHPOS
	THEN
	THEN
	ELSE
	LDA I,0
	STA ZOOMST
	THEN
	THEN
	RTS
	.PAGE
ADDPOS:	LDA LACPVL
	CLC
	ADC I,10
	STA LACPVL
	IFCS
	INC LACPVH
	THEN
	LDA VLEGEL
	CLC
	ADC I,10
	STA VLEGEL
	IFCS
	INC VLEGEH
	THEN
	JMP	ZADVPS
SUBPOS:	LDA LACPVL
	SEC
	SBC I,10
	STA LACPVL
	IFCC
	DEC LACPVH
	THEN
	LDA VLEGEL
	SEC
	SBC I,10
	STA VLEGEL
	IFCC
	DEC VLEGEH
	THEN
	JMP	ZSBVPS
SBHPOS:	LDA LACPHL
	SEC
	SBC I,9
	STA LACPHL
	IFCC
	DEC LACPHH
	THEN
	LDA OVRSCN
	CLC
	ADC I,9
	STA OVRSCN
	IFCS
	LDA LFTSCT
	ADC I,0
	AND I,0F
	STA LFTSCT
	THEN
	JMP	ZSBHPS
ADHPOS:	LDA LACPHL
	CLC
	ADC I,9			;ADJUST LANDER POSITION VECTOR
	STA LACPHL
	IFCS
	INC LACPHH
	THEN
	LDA OVRSCN		;ADJUST MONITOR OVERSCAN
	SEC
	SBC I,9
	STA OVRSCN
	IFCC
	LDA LFTSCT		;WRAPPED SECTOR
	SBC I,0
	AND I,0F
	STA LFTSCT
	THEN
	JMP	ZADHPS
	.PAGE
	.SBTTL ADJUST V POSITIONS DURING ZOOMING



ZADVPS:	LDX	I,ASHOTS+PSHOTS+NBASES+1
	BEGIN
	LDA	X,BACPVL
	CLC
	ADC	I,10
	STA	X,BACPVL
	IFCS
	INC	X,BACPVH
	THEN
	CPX I,2
	IFCC
	LDA X,ASCPVL+2
	ADC I,10
	STA X,ASCPVL+2
	IFCS
	INC X,ASCPVH+2
	THEN
	THEN
	DEX
	MIEND
	RTS



ZSBVPS:	LDX	I,ASHOTS+PSHOTS+NBASES+1
	BEGIN
	LDA	X,BACPVL
	SEC
	SBC	I,10
	STA	X,BACPVL
	IFCC
	DEC	X,BACPVH
	THEN
	CPX I,2
	IFCC
	LDA X,ASCPVL+2
	SBC I,0F
	STA X,ASCPVL+2
	IFCC
	DEC X,ASCPVH+2
	THEN
	THEN
	DEX
	MIEND
	RTS
	.PAGE
	.SBTTL ADJUST H POSITIONS DURING SCROLLING


ZADHPS:	LDX	I,ASHOTS+PSHOTS+NBASES+1
	BEGIN
	LDA	X,BACPHL
	CLC
	ADC	I,9
	STA	X,BACPHL
	IFCS
	INC	X,BACPHH
	THEN
	CPX I,2
	IFCC
	LDA X,ASCPHL+2
	ADC I,9
	STA X,ASCPHL+2
	IFCS
	INC X,ASCPHH+2
	THEN
	THEN
	DEX
	MIEND
	RTS



ZSBHPS:	LDX	I,ASHOTS+PSHOTS+NBASES+1
	BEGIN
	LDA	X,BACPHL
	SEC
	SBC	I,9
	STA	X,BACPHL
	IFCC
	DEC	X,BACPHH
	THEN
	CPX I,2
	IFCC
	LDA X,ASCPHL+2
	SBC I,8
	STA X,ASCPHL+2
	IFCC
	DEC X,ASCPHH+2
	THEN
	THEN
	DEX
	MIEND
	RTS
	.PAGE
	.SBTTL SHOT VS. LAND COLLISION




SHVSGR: LDA I,HEDGEL
	SEC
	SBC OVRSCN
	STA XACL2
	LDA I,HEDGEH
	SBC I,0
	STA XACH2
	LDA LFTSCT
	ASL
	TAY
	LDA VLEGEL
	CLC
	ADC NY,SCTVOS
	STA YACL2
	LDA VLEGEH
	INY
	ADC NY,SCTVOS
	DEY
	STA YACH2
	LDX I,PSHOTS-1
	STX TEMP1
	BEGIN
	LDX TEMP1
	LDA X,SHACTM
	IFEQ
	JMP SKIP
	THEN
	LDA X,SHCPHL
	SEC
	SBC XACL2
	STA POINT1
	LDA X,SHCPHH
	SBC XACH2
	STA POINT2
	CLC
	ADC LFTSCT
	AND I,0F
	STA TEMP2
	LDA LFTSCT
	STA TEMP3
	LDY I,3
	BEGIN
	LDA Y,XACL2
	STA Y,XACCL
	DEY
	MIEND
	BEGIN
	LDA TEMP3 
	ASL
	TAY
	INC XACCH
	LDA YACCL
	CLC
	ADC NY,SCTVDL
	STA YACCL
	LDA YACCH
	INY
	ADC NY,SCTVDL
	DEY
	STA YACCH
	INC TEMP3
	LDA TEMP3
	AND I,0F
	STA TEMP3
	CMP TEMP2
	EQEND
	TAY
	LDA NY,SEGCNT
	IFEQ
	JMP SKIP
	THEN
	STA TEMP3
	LDA NY,SGTBIX
	TAY
	BEGIN
	JSR SEGTST
	IFCS
	LDA NY,XDELTA
	IFNE
	LDA NY,SLOPE
	STA MUPYER
	LDA X,SHCPHL
	SEC
	SBC XACCL
	STA TEMP4
	LDA X,SHCPHH
	SBC XACCH
	IFMI
	LDA I,0
	SEC
	SBC TEMP4
	STA TEMP4
	THEN
	LDA TEMP4
	STA MUPCAN
	STY TEMP2
	JSR MULTIPLY
	LDA RESH
	STA POINT1
	LDY TEMP2
	INY
	LDA NY,SLOPE
	DEY
	AND I,7F
	STA MUPYER
	JSR MULTIPLY
	LDY TEMP2
	LDA RESL
	CLC
	ADC POINT1
	STA POINT1
	LDA I,0
	ADC RESH
	STA POINT2
	INY
	LDA NY,SLOPE
	JSR ADDELT
	DEY
	LDA X,SHCPHL
	STA POINT3
	LDA X,SHCPHH
	STA POINT4
	JSR LSHCOR
	LDA I,0
	STA OGLORY
	STX TEMP4
	JSR COLTST
	LDX TEMP4
	IFCC
	LDA I,0
	STA X,SHACTM
	THEN
	THEN
	THEN
	LDA YACCL
	CLC
	ADC NY,YDELTA
	
	STA YACCL
	LDA YACCH
	INY
	ADC NY,YDELTA
	DEY
	STA YACCH
	LDA XACCL
	CLC
	ADC NY,XDELTA
	STA XACCL
	LDA XACCH
	INY
	ADC NY,XDELTA
	STA XACCH
	INY
	DEC TEMP3
	EQEND
SKIP:	DEC TEMP1
	MIEND
	RTS


ADDELT: IFMI
	LDA YACCL
	SEC
	SBC POINT1
	STA POINT1
	LDA YACCH
	SBC POINT2
	STA POINT2
	ELSE
	LDA YACCL
	CLC
	ADC POINT1
	STA POINT1
	LDA YACCH
	ADC POINT2
	STA POINT2
	THEN
	RTS
	.PAGE


LSHCOR:
	LDA X,SHCPVL
	STA POINT5
	LDA X,SHCPVH
	STA POINT6
	LDA X,SHCPHL
	STA POINT7
	LDA X,SHCPHH
	STA POINT8
	RTS



SEGTST: LDA XACCL
	SEC
	SBC X,SHCPHL
	LDA XACCH
	SBC X,SHCPHH
	STA TEMP4
	LDA XACCL
	CLC
	ADC NY,XDELTA
	STA POINT3
	LDA XACCH
	INY
	ADC NY,XDELTA
	DEY
	STA POINT4
	LDA POINT3
	SEC
	SBC X,SHCPHL
	LDA POINT4
	SBC X,SHCPHH
	EOR TEMP4
	IFMI
	SEC
	ELSE
	CLC
	THEN
	RTS
	.PAGE
	.SBTTL	CONTROL LOOP FOR COLLISION DETECT
; INPUTS:	LACPV,LACPH=LANDER POSITION
;
;		SECTOR=RESIDENT SECTOR OF LANDER
;
;		SECADR=TABLE OF ADDRESSES OF
;			ROUTINES THAT DRAW A SECTOR
;
; OUTPUTS:
;
COLDCT: LDX I,3
	BEGIN
	LDA X,XACL2
	STA X,XACCL
	DEX
	MIEND
	LDA LFTSCT
	STA TEMP1
	BEGIN			;POINT TO STARTING POINT OF EACH SECTOR
	LDA TEMP1
	ASL
	TAY
	INC XACCH	; SECTORS 100 WIDE
	LDA YACCL
	CLC
	ADC NY,SCTVDL
	STA YACCL
	LDA YACCH
	INY
	ADC NY,SCTVDL
	STA YACCH
	INC TEMP1
	LDA TEMP1		;POINTING TO LANDER SECTOR?
	AND I,0F
	STA TEMP1
	CMP SECTOR
	EQEND
	LDX I,3
	BEGIN
	LDA X,XACCL
	STA X,XACL2
	DEX
	MIEND
	LDY SECTOR
	LDA NY,SEGCNT
	IFEQ
	RTS		; NOTHING HERE
	THEN
	STA TEMP1
	LDA NY,SGTBIX		;LOAD INDEX OF THIS SECTOR DATA
	TAY
	BEGIN
	JSR CKUNDR
	IFCS
	LDA NY,XDELTA
	IFNE
	LDA NY,SLOPE		;GET FRACTIONAL SLOPE AND
	STA MUPYER		;MULTIPLY BY DIFFERENCE BETWEEN
	LDA LACPHL
	SEC
	SBC XACCL
	STA TEMP4
	LDA LACPHH
	SBC XACCH
	IFMI
	LDA I,0
	SEC
	SBC TEMP4
	STA TEMP4
	THEN
	LDA TEMP4
	STA MUPCAN
	STY TEMP2
	JSR MULTIPLY
	LDY TEMP2
	LDA RESH
	STA POINT1		;SAVE INTEGER RESLT OF FRACTIONAL MULTIPLY
	INY
	LDA NY,SLOPE		;GET INTEGER SLOPE
	AND I,7F
	STA MUPYER
	JSR MULTIPLY		;MULTIPLY BY SAME DIFFERENCE
	LDY TEMP2
	LDA RESL
	CLC
	ADC POINT1		;ACCUMULATE RESULT
	STA POINT1
	LDA I,0
	ADC RESH
	STA POINT2
	INY
	LDA NY,SLOPE
	JSR ADDELT
	DEY
	LDA	LACPHL
	STA	POINT3
	LDA	LACPHH
	STA	POINT4
	JSR LODCOR		;TRANSFER SHIP COORDS TO POINT LOCS
	LDA I,0
	STA OGLORY
	JSR COLTST		;TEST FOR COLLISION
	IFCC
	JMP COLIDE		;GO KILL SHIP
	THEN
	THEN
	THEN
	LDA YACCL		;ADD IN Y VECTOR DELTA
	CLC
	ADC NY,YDELTA
	STA YACCL
	LDA YACCH
	INY
	ADC NY,YDELTA
	DEY
	STA YACCH
	LDA XACCL
	CLC
	ADC NY,XDELTA
	STA XACCL
	LDA XACCH
	INY
	ADC NY,XDELTA
	STA XACCH
	INY
	DEC TEMP1
	EQEND
	RTS
SIRNAD:	.WORD SIRN01,SIRN02,SIRN03,SIRN04
	.WORD SIRN05,SIRN06,SIRN07,SIRN08
	.WORD SIRN09,SIRN10,SIRN11,SIRN12
	.WORD SIRN13,SIRN14,SIRN15,SIRN16
NIMIAD: .WORD 0,0,0,0
	.WORD NIMI01,NIMI02,NIMI03,NIMI04
	.WORD NIMI05,NIMI06,NIMI07,NIMI08
	.WORD 0,0,0,0
DURNAD:	.WORD DURN01,DURN02,DURN03,DURN04
	.WORD DURN05,DURN06,DURN07,DURN08
	.WORD DURN09,DURN10,DURN11,DURN12
	.WORD DURN13,DURN14,DURN15,DURN16
CNTCAD:	.WORD CNTC01,CNTC02,CNTC03,CNTC04
	.WORD CNTC05,CNTC06,CNTC07,CNTC08
	.WORD CNTC09,CNTC10,CNTC11,CNTC12
	.WORD CNTC13,CNTC14,CNTC15,CNTC16
NOVAAD:
	.WORD 0,0,0,NOVA01
	.WORD NOVA02,NOVA03,NOVA04,NOVA05
	.WORD NOVA06,NOVA07,NOVA08,NOVA09
	.WORD NOVA10,0,0,0
EASYAD:	.WORD EASY01,EASY02,EASY03,EASY04
	.WORD EASY05,EASY06,EASY07,EASY08
	.WORD EASY09,EASY10,EASY11,EASY12
	.WORD EASY13,EASY14,EASY15,EASY16
BIZRAD:	.WORD BIZR01,BIZR02,BIZR03,BIZR04
	.WORD BIZR05,BIZR06,BIZR07,BIZR08
	.WORD BIZR09,BIZR10,BIZR11,BIZR12
	.WORD BIZR13,BIZR14,BIZR15,BIZR16
VOLCAD:	.WORD 0,0,0,VOLC01
	.WORD VOLC02,VOLC03,VOLC04,VOLC05
	.WORD VOLC06,VOLC07,VOLC08,VOLC09
	.WORD VOLC10,0,0,0
ERCTAD:	.WORD ERCT01,ERCT02,ERCT03,ERCT04
	.WORD ERCT05,ERCT06,ERCT07,ERCT08
	.WORD ERCT09,ERCT10,ERCT11,ERCT12
	.WORD ERCT13,ERCT14,ERCT15,ERCT16
	.PAGE
VDSIRN:	.WORD -100,0,1E0,-140
	.WORD -0E0,0,1C0,-0C0
	.WORD 0,0C0,-80,-120
	.WORD 0,0,220,-100
VDNIMI: .WORD 0,0,0,0
	.WORD -120,-60,0A0,-60
	.WORD 0,-0E0,1E0,0
	.WORD 0,0,0,0
VDDURN:	.WORD -180,-0E0,0,60,0E0,-120,40,200
	.WORD -180,-40,-80,80,1C0,-1A0,40,160
VDCNTC:	.WORD 80,-40,20,0,-60,0,80,40
	.WORD 0,-80,40,-60,20,0,40,-80
VDNOVA:	.WORD 0,0,0,-100
	.WORD -100,-100,0,0
	.WORD 0,0,100,100
	.WORD 100,0,0,0
VDEASY:	.WORD 0,-100,0,100
	.WORD 0,0,-100,-100
	.WORD 0,100,0,100
	.WORD -100,0,100,0
VDBIZR:	.WORD -100,0,0,0
	.WORD 0,200,-300,0
	.WORD 100,100,0,100
	.WORD -100,0,0,0
VDVOLC:	.WORD 0,0,0,-100
	.WORD -100,0,200,0
	.WORD -100,0,0,100
	.WORD 100,0,0,0
VDERCT:	.WORD 0,2C0,0C0,200
	.WORD -440,0,-1C0,0
	.WORD 100,0,0,0
	.WORD 0,0,0,-80
	.PAGE
	.SBTTL	TEST FOR COLLISION BETWEEN 2 POINTS
;INPUTS:POINT1,POINT2 VCOORDS FOR OBJ 1
;	POINT3,POINT4 HCOORDS FOR OBJ 1
;	POINT5,POINT6 VCOORDS FOR OBJ 2
;	POINT7,POINT8 HCOORDS FOR OBJ 2
;
;
;OUTPUTS0

ABSDIF:	LDX I,2			;LOOP FOR H&V
	BEGIN
	LDA X,POINT1
	SEC			;GET DELTAS
	SBC X,POINT5
	STA X,POINT1
	LDA X,POINT2
	SBC X,POINT6
	STA X,POINT2
	DEX
	DEX
	MIEND
	LDA I,0
	STA ASQUAD
	LDX I,2
	BEGIN
	LDA X,POINT2		;MAKE SURE DELTA IS
	IFMI			;POSITIVE
	LDA I,0
	SEC
	SBC X,POINT1
	STA X,POINT1
	LDA I,0
	SBC X,POINT2
	STA X,POINT2
	LDA ASQUAD
	ORA X,QUABIT
	STA ASQUAD
	THEN
	DEX
	DEX
	MIEND
	RTS

QUABIT: .BYTE 1,0,2

COLTST: JSR ABSDIF
	LDA POINT2
	ORA POINT4
	IFEQ
	LDX OGLORY
	IFMI
	JMP TSTBAS
	THEN
	LDA POINT1		;CHECK V RANGE
	CMP X,VSIZE
	IFCC
	LDA POINT3		;CHECK H RANGE
	CMP X,HSIZE
	IFCC
	ADC POINT1
	CMP X,DSIZE
	THEN
	THEN
	LDA POINT1
	ELSE
	SEC
	THEN
	RTS			;RETURN CC=COLLIDED, CS=NO COLLISION
TSTBAS: LDY TEMP2
	LDA POINT1
	CMP NY,BASIZV
	IFCC
	LDA POINT3
	CMP NY,BASIZH
	IFCC
	ADC POINT1
	CMP NY,BASIZD
	THEN
	THEN
	RTS
SISIZV: .BYTE 24,14,28,30,30,30,40,30
NISIZV:
CTSIZV:
EASIZV:
DUSIZV: .BYTE 24,24,24,24,24,24,24,24

SISIZH: .BYTE 20,30,22,20,20,20,20,20
NISIZH:
CTSIZH:
EASIZH:
DUSIZH: .BYTE 2E,2E,2E,2E,2E,2E,2E,2E

SISIZD: .BYTE 32,38,38,38,38,38,50,38
NISIZD:
CTSIZD:
EASIZD:
DUSIZD: .BYTE 40,40,40,40,40,40,40,40
VSIZE:	.BYTE 13,10,23
HSIZE:	.BYTE 11,14,24
DSIZE:	.BYTE 18,1C,32
	.PAGE
	.PAGE
	.SBTTL	CHECK FOR SEGMENT UNDER/OVER LANDER
CKUNDR:	LDA XACCL		;GET DIFF BETWEEN LEFT
	SEC			;COORDINATE AND LANDER
	SBC LACPHL
	LDA XACCH
	SBC LACPHH
	STA TEMP2
	LDA XACCL		;ADD IN VECTOR DELTAS
	CLC
	ADC NY,XDELTA
	STA TEMP3
	LDA XACCH
	INY
	ADC NY,XDELTA
	DEY
	STA TEMP4
	LDA TEMP3		;GET NEW DIFFERENCE
	SEC
	SBC LACPHL
	LDA TEMP4
	SBC LACPHH
	EOR TEMP2		;CHECK FOR OPOSITE SIGNS
	IFMI
	SEC			;MUST BE UNDER/OVER LANDER
	ELSE
	CLC			;ISN'T
	THEN
	RTS
	.PAGE
	.SBTTL	LOAD LANDER COORDS FOR COLTST
LODCOR:	LDA LACPVL
	STA POINT5
	LDA LACPVH
	STA POINT6
	LDA LACPHL
	STA POINT7
	LDA LACPHH
	STA POINT8
	RTS
COLIDE: JSR LAEXCO
	JSR STREXP
	JSR EXPLOS
	RTS

LAEXCO: LDA I,0
	LDX PLAYUP
	STA X,PLADED
FRSTOS: LDA LACPVL
	STA POINT1
	LDA LACPVH
	STA POINT2
	LDA LACPHL
	STA POINT3
	LDA LACPHH
	STA POINT4
	RTS
	.PAGE
	.SBTTL	COLLISION DATA TABLES
XSIRN:	.WORD 64.,96.,96.,-96.,96.
	.WORD 64.,192.
	.WORD 192.,-192.,96.,64.,96.
	.WORD 128.,-128.,255.
	.WORD 96.,-96.,64.,192.,-192.,192.
	.WORD 255.
	.WORD 160.,-160.,255.
	.WORD 255.
	.WORD 255.
	.WORD 255.
	.WORD 255.
	.WORD 255.
	.WORD 255.,0,-192.,64.,-128.,255.
	.WORD 255.,-255.,255.,-255.
	.WORD 160.,-160.,128.,128.
	.WORD 192.,64.
YSIRN:	.WORD 0,96.,-192.,-160.,0
	.WORD 0,0
	.WORD 320.,192.,64.,0,-96.
	.WORD -128.,-192.,0
	.WORD 192.,224.,0,-96.,-416.,-128.
	.WORD 0
	.WORD 320.,224.,-96.
	.WORD -192.
	.WORD 0
	.WORD 192.
	.WORD -128.
	.WORD -288.
	.WORD 0,224.,64.,128.,160.,0
	.WORD 0,224.,352.,0
	.WORD 0,288.,0,256.
	.WORD -256.,0
SLSIRN:	.WORD 0,100,8200,81AA,0
	.WORD 0,0
	.WORD 1AA,100,0AA,0,8100
	.WORD 8100,8180,0
	.WORD 200,255,0,8080,8220,80AA
	.WORD 0
	.WORD 200,166,8060
	.WORD 80C0
	.WORD 0
	.WORD 0C0
	.WORD 8080
	.WORD 8110
	.WORD 0,8100,55,200,140,0
	.WORD 0,8100,160,0
	.WORD 0,1CC,0,200
	.WORD 8155,0
IXSIRN: .BYTE 0,0A,0E,18,1E,2A,2C,32
	.BYTE 34,36,38,3A,3C,48,50,58
CTSIRN:	.BYTE 5,2,5,3,6,1,3,1
	.BYTE 1,1,1,1,6,4,4,2
	.PAGE
	.RADIX 10
XNIMI:	.WORD 192.,64.,0,-160.,160.,-128,-128
	.WORD 96,96,64,0,-64,64,0,-64,-192
	.WORD 96,-96,0,96,160,-128,128,0,-96,96,-128,-128
	.WORD 160,96,0,-128,-128
	.WORD 255,0,-255,96,128,-128,-96
	.WORD 255,-255,160,96,-96,96,0,-96,-160
	.WORD 64,128,64,-96,96,-64,64,0,-64,64,-160,-96
	.WORD 192,64,-128,-32,-96
YNIMI:	.WORD 0,64.,-352.,0,160.,0,128.
	.WORD -96,64,-64,-192,-64,0,896,0,-192
	.WORD -96,-96,-64,0,160,128,128,800,-96,-96,0,-128
	.WORD 0,-96,768,0,128
	.WORD 0,1150,-128,0,-128,-128,0
	.WORD 0,-256,-160,96,0,96,992,0,384
	.WORD 64,0,64,96,128,64,64,288,64,64,0,96
	.WORD 0,64,128,0,96
	.RADIX 16
SLNIMI:	.WORD 0,100,0,0,100,0,100
	.WORD 8100,0AA,8100,100,8100,0,100,0,8100
	.WORD 8100,8100,100,0,100,100,100,8100,8100,8100,0,8100
	.WORD 0,8100,100,0,100
	.WORD 0,0,8081,0,8100,8100,0
	.WORD 0,8100,8100,100,0,100,0,0,266
	.WORD 100,0,100,100,100,100,100,0,100,100,0,100
	.WORD 0,100,100,0,100
IXNIMI:	.BYTE 0,0,0,0,0,0E,20,38
	.BYTE 42,50,62,7A,0,0,0,0
CTNIMI:	.BYTE 0,0,0,0,7,9,12.,5
	.BYTE 7,9,12.,5,0,0,0,0
CKSUM2:: .BYTE 0A6
	.PAGE
	.RADIX 10
XDURN:	.WORD 255.,-192,192
	.WORD 255.,0.,-96,96.
	.WORD 255.,0.,-96,-160,0.,255.
	.WORD 160.,96.,0.,-255,0.,255.
	.WORD 96,96,64.,0.,-96.,-64.,-96.,0.,255.
	.WORD 128,-128,0.,255.,0.,-160,96.,64.
	.WORD 96,160.,0.,-64,-96.,-96.,0.,64.,-64.,255.
	.WORD 160.,96,-255,255.
	.WORD 255.,0.,-96.,96.
	.WORD 255.,0.,-64.,-192.,0.,255
	.WORD 128.,128.,0.,-224.,-32.,0.,255.
	.WORD 96.,160.,0.,-224.,-32.,0.,255
	.WORD 96.,160.,-192.,-64.,0.,255.
	.WORD 64.,64.,128
	.WORD 128.,64.,64.,0.,-255.,128,128.
	.WORD 192.,-192,0.,255
YDURN:	.WORD 0.,-192,-192.
	.WORD -224.,384.,224,0.
	.WORD 0.,224.,0.,160.,224.,0.
	.WORD 0.,96.,384.,-256,384.,0.
	.WORD 160.,0.,64.,160.,64.,0.,-64.,128.,0.
	.WORD 0.,160.,128.,0.,-256,-224.,-96.,0.
	.WORD 64.,0.,256.,-128.,128.,0.,256.,-64.,-64.,-128.
	.WORD 128.,160.,224.,0.
	.WORD -384.,256.,128.,0.
	.WORD -64.,192.,0.,128.,128.,0.
	.WORD -128.,0,384.,0,-64.,256,0.
	.WORD 128.,0.,320.,0.,-64.,192,0.
	.WORD 128.,0.,192.,0.,128.,0.
	.WORD -128.,-96.,192
	.WORD 0.,64.,0.,224.,0.,128.,0
	.WORD 0,224,128,0.
	.RADIX 16
SLDURN:	.WORD 0,8100,8100
	.WORD 80E0,0,255,0
	.WORD 0,0,0,100,0,0
	.WORD 0,100,0,8100,0,0
	.WORD 1AA,0,100,0,0AA,0,80AA,0,0
	.WORD 0,100,0,0,0,8166,8100,0
	.WORD 0AA,0,0,8200,155,0,0,8100,8100,8080
	.WORD 0CC,1AA,0E0,0
	.WORD 8180,0,155,0
	.WORD 8040,0,0,0AA,0,0
	.WORD 8100,0,0,0,8200,0,0
	.WORD 155,0,0,0,8200,0,0
	.WORD 155,0,100,0,0,0
	.WORD 8200,8180,8180
	.WORD 0,100,0,0,0,100,0
	.WORD 0,12B,0,0
IXDURN:	.BYTE 0,6,0E,1A,26,38,48,5C
	.BYTE 64,6C,78,86,94,0A0,0A6,0B4
CTDURN:	.BYTE 3,4,6,6,9,8,10.,4
	.BYTE 4,6,7,7,6,3,7,4
	.PAGE
XCNTC:	.WORD 255.,0.,-64.,-64.,-128.,0.,255.
	.WORD 128.,64.,64.,0.,-64.,-64.,-128.,0.,255.
	.WORD 32.,96.,64.,64.,0.,-255.,0.,255.
	.WORD 64.,128.,64.,0.,-255.,0.,224.,32.
	.WORD 64.,64.,128.
	.WORD 32.,64.,160.,0,-128.,-64.,-64.,64.,192.
	.WORD 32.,64.,32.,128.,0.,-128.,-64.,-64.,0.,192.,64.
	.WORD 64.,96.,96.
	.WORD 128.,64.,64.,0,-64.,-64.,-128.,0,255.
	.WORD 64.,64.,96.,32.,0.,-64.,-64.,-128.,0,255.
	.WORD 64.,64.,64.,64.,0.,-255.,0.,255.
	.WORD 64.,64.,64.,64.,0.,-255.,0.,255.
	.WORD 64.,64.,64.,64.,0.,-255.,0.,255.
	.WORD 64.,64.,128.,0,-64.,-64.,-128.,0,192.,64.
	.WORD 96.,160.
	.WORD 64.,64.,64.,64.,0.,-255.,64.,192.
YCNTC:	.WORD 128.,480.,-160.,160.,0.,64.,0.
	.WORD 0.,128.,-192.,544.,-96.,96.,0.,64.,0.
	.WORD 32.,0.,96.,-96.,512.,0.,64.,0.
	.WORD -64.,0.,64.,512.,0.,64.,0.,-64.
	.WORD 192.,-192.,-96.
	.WORD 128.,0.,-128.,608.,0.,-160.,160.,64.,0.
	.WORD 128.,0.,-128.,128.,480.,0.,-128.,128.,64.,0.,-64.
	.WORD -96.,0.,160.
	.WORD 0,96.,-96.,416.,-160.,160.,0.,64.,0.
	.WORD 96.,-96.,0,-128.,544.,-96.,96.,0.,64.,0.
	.WORD 0.,64.,128.,-128.,480.,0.,64.,0.
	.WORD -96.,0.,128.,-128.,576.,0.,64.,0.
	.WORD 32.,0.,192.,-192.,544.,0.,64.,0.
	.WORD 64.,0.,-64.,544.,-128.,128.,0.,64.,0.,-64.
	.WORD 64.,0.
	.WORD 160.,-160.,0.,-128.,608.,0.,64.,0.
SLCNTC:	.WORD 80,0,8280,280,0,0,0
	.WORD 0,200,8300,0,8180,180,0,0,0
	.WORD 100,0,180,8180,0,0,0,0
	.WORD 8100,0,100,0,0,0,0,8100
	.WORD 300,8300,80C0
	.WORD 400,0,80CC,0,0,8280,280,100,0
	.WORD 400,0,8400,100,0,0,8200,200,0,0,8100
	.WORD 8180,0,1AA
	.WORD 0,180,8180,0,8280,280,0,100,0
	.WORD 180,8180,0,8400,0,8180,180,0,0,0
	.WORD 0,100,200,8200,0,0,0,0
	.WORD 8155,0,200,8200,0,0,0,0
	.WORD 80,0,300,8300,0,0,0,0
	.WORD 100,0,8080,0,8200,200,0,0,0,8100
	.WORD 0AA,0
	.WORD 280,8280,0,8155,0,0,100,0
IXCNTC: .BYTE 0,0E,20,30,40,46,58,6E
	.BYTE 74,86,9A,0AA,0BA,0CA,0DE,0E2
CTCNTC:	.BYTE 7,9,8,8,3,9,11.,3
	.BYTE 9,10.,8,8,8,10.,2,8
	.PAGE
	.RADIX 10
XNOVA:	.WORD 255,0,-255
	.WORD 255,0,-128,128,0,-255,255,-128
	.WORD 255,0,-255,0,255,0,-255,0,255
	.WORD 255,0,-128,-127,0,255
	.WORD 255,0,-255,0,255,0,-255,0,255
	.WORD 255,0,-255,0,255,0,-128,128,0,-255,0,255
	.WORD 255,0,-128,-127,0,255,0,-255,0,255,0,-255
	.WORD 255,-255,255,0,-127,-128,255,-255,0,255,0,-128,-127
	.WORD 255,-255,128,-128,0,255
	.WORD 255,-255
YNOVA:	.WORD -256,512,-256
	.WORD -256,384,128,128,384,-256,768,128
	.WORD -256,384,256,256,256,384,-256,512,-256
	.WORD 0,256,0,128,768,256
	.WORD 0,256,0,896,-256,512,-256,256,256
	.WORD 0,256,0,640,-256,384,128,0,256,0,256,256
	.WORD 0,384,-128,0,384,0,384,0,128,0,256,0
	.WORD 256,128,256,128,-128,0,128,256,128,-256,384,128,0
	.WORD 256,128,128,128,384,-256
	.WORD 256,256
	.RADIX 16
SLNOVA:	.WORD 8100,0,8100
	.WORD 8100,0,100,100,0,8100,100,100
	.WORD 8100,0,100,0,100,0,8100,100,8100
	.WORD 0,0,0,100,0,100
	.WORD 0,0,0,0,8100,100,8100,100,100
	.WORD 0,0,0,0,8100,100,100,0,0,0,0,100
	.WORD 0,0,8100,0,0,0,0,0,0,0,0,0
	.WORD 100,8100,100,0,8100,0,0,100,0,8100,0,100,0
	.WORD 100,8100,100,100,0,8100
	.WORD 100,100
IXNOVA:	.BYTE 0,0,0,0,6,16,28,34
	.BYTE 46,5E,76,90,9C,0,0,0
CTNOVA:	.BYTE 0,0,0,3,8,9,6,9
	.BYTE 12.,12.,13.,6,2,0,0,0
	.PAGE
	.PAGE
	.RADIX 10
XEASY:	.WORD 128,128
	.WORD 128,128
	.WORD 128,128
	.WORD 128,128
	.WORD 128,128
	.WORD 128,128
	.WORD 128,128
	.WORD 128,128
	.WORD 128,128
	.WORD 128,128
	.WORD 128,128
	.WORD 128,128
	.WORD 128,128
	.WORD 128,128
	.WORD 128,128
	.WORD 128,128
YEASY:	.WORD 0,0
	.WORD -128,-128
	.WORD 0,0
	.WORD 128,128
	.WORD 0,0
	.WORD 0,0
	.WORD -128,-128
	.WORD -128,-128
	.WORD 0,0
	.WORD 128,128
	.WORD 0,0
	.WORD 128,128
	.WORD -128,-128
	.WORD 0,0
	.WORD 128,128
	.WORD 0,0
	.RADIX 16
SLEASY:	.WORD 0,0
	.WORD 8100,8100
	.WORD 0,0
	.WORD 100,100
	.WORD 0,0
	.WORD 0,0
	.WORD 8100,8100
	.WORD 8100,8100
	.WORD 0,0
	.WORD 100,100
	.WORD 0,0
	.WORD 100,100
	.WORD 8100,8100
	.WORD 0,0
	.WORD 100,100
	.WORD 0,0
CTEASY:	.BYTE 2,2,2,2,2,2,2,2
	.BYTE 2,2,2,2,2,2,2,2
IXEASY:	.BYTE 0,4,8,0C,10,14,18,1C
	.BYTE 20,24,28,2C,30,34,38,3C
	.PAGE
	.RADIX 10
XBIZR:	.WORD 255
	.WORD 255,-255,255,0,-255
	.WORD 255,-255,255,-255
	.WORD 255
	.WORD 255,0,-255,255
	.WORD 255,-255,0,255
	.WORD 255
	.WORD 255,-255,255,0,-255
	.WORD 255,-255,255,-255
	.WORD 255
	.WORD 255
	.WORD 255,-255,255
	.WORD 255
	.WORD 255,-255,255,-255
	.WORD 255
	.WORD 255
YBIZR:	.WORD 256
	.WORD 0,0,256,512,-256
	.WORD 0,256,256,256
	.WORD 0
	.WORD 0,256,256,256
	.WORD 0,256,512,-256
	.WORD -256
	.WORD 0,0,256,256,0
	.WORD 256,0,256,0
	.WORD 256
	.WORD 0
	.WORD 0,256,0
	.WORD 0
	.WORD 0,0,256,0
	.WORD 0
	.WORD 0
	.RADIX 16
SLBIZR:	.WORD 100
	.WORD 0,0,100,8100,8100
	.WORD 0,0,100,100
	.WORD 0
	.WORD 0,0,100,100
	.WORD 0,100,8100,8100
	.WORD 8100
	.WORD 0,0,100,8100,0
	.WORD 100,8100,100,0
	.WORD 100
	.WORD 0
	.WORD 0,100,0
	.WORD 0
	.WORD 0,0,100,0
	.WORD 0
	.WORD 0
CTBIZR:	.BYTE 1,5,4,1,4,4,1,5
	.BYTE 4,1,1,3,1,4,1,1
IXBIZR:	.BYTE 0,2,0C,14,16,1E,26,28
	.BYTE 32,3A,3C,3E,44,46,4E,50
	.PAGE
	.RADIX 10
XERCT:	.WORD 255
	.WORD 255,-255,255
	.WORD 128,128
	.WORD 255
	.WORD 255,0,-255,255
	.WORD 255,-255,255,-255
	.WORD 255
	.WORD 255
	.WORD 255,0,-255,255,-255,255
	.WORD 255,0,-255,255,-128,128,-255,255,-255,128,128
	.WORD 255,0,-255,0,255,0,-255,0,255
	.WORD 255,0,-255,0,255,0,-255,0,255
	.WORD 255,-255,96,-96,0,128,128,-96,96,0,-255
	.WORD 255,-255,255,-255
	.WORD 255
	.WORD 128,128
YERCT:	.WORD 0
	.WORD 448,0,256
	.WORD 0,192
	.WORD 512
	.WORD 0,-640,0,-448
	.WORD 0,448,448,192
	.WORD -448
	.WORD 0
	.WORD 256,192,256,0,256,0
	.WORD 0,192,0,96,0,160,256,0,448,128,0
	.WORD 0,192,0,96,0,416,-256,832,-448
	.WORD 0,192,0,96,0,128,288,128,-256
	.WORD 0,192,0,96,128,0,-128,-96,0,384,0
	.WORD 0,192,0,384
	.WORD 0
	.WORD 0,-128
	.RADIX 16
SLERCT:	.WORD 0
	.WORD 1C0,0,100
	.WORD 0,180
	.WORD 200
	.WORD 0,0,0,81C0
	.WORD 0,0,1C0,0C0
	.WORD 81C0
	.WORD 0
	.WORD 100,8100,100,0,100,0
	.WORD 0,0,0,0,0,100,8100,0,1C0,100,0
	.WORD 0,0,0,0,0,0,8100,100,81C0
	.WORD 0,0,0,0,0,0,120,0,8100
	.WORD 0,0,0,100,0,0,8100,8100,0,0,0
	.WORD 0,0,0,180
	.WORD 0
	.WORD 0,8100
CTERCT:	.BYTE 1,3,2,1,4,4,1,1
	.BYTE 6,11.,9,9,11.,4,1,2
IXERCT:	.BYTE 0,2,8,0C,0E,16,1E,20
	.BYTE 22,2E,44,56,68,7E,86,88
	.PAGE
	.RADIX 10
XVOLC:	.WORD 255,-255,255,-255,255
	.WORD 255,-255,255,-255,255
	.WORD 255,0,-255,255,0,-255
	.WORD 255,-255,0,255,-255,255,-255,255,-255
	.WORD 255,0,-255,255
	.WORD 255,0,-255,0,255,0,-255,0,255
	.WORD 255,0,-255,0,255,0,-255
	.WORD 255,0,-255,255,-255,255,-255,0,255
	.WORD 255,-255,255,-255,255,-255,255,-255
	.WORD 255,-255
YVOLC:	.WORD -256,256,256,0,256
	.WORD -256,1024,256,0,256
	.WORD 0,256,256,0,768,256
	.WORD 256,0,256,0,768,0,255,255,255
	.WORD 256,512,256,256
	.WORD -256,256,0,256,256,512,256,512,256
	.WORD 0,256,0,512,256,256,0
	.WORD 0,256,0,256,256,256,0,256,0
	.WORD 256,0,256,0,256,0,256,256
	.WORD 256,256
	.RADIX 16
SLVOLC:	.WORD 8100,100,100,0,100
	.WORD 8100,100,100,0,100
	.WORD 0,0,100,0,0,100
	.WORD 100,0,0,0,0,0,100,100,100
	.WORD 100,8100,100,100
	.WORD 8100,0,0,0,100,8100,100,8100,100
	.WORD 0,0,0,0,100,8100,0
	.WORD 0,0,0,0,100,100,0,0,0
	.WORD 100,8100,100,0,0,0,100,100
	.WORD 100,100
CTVOLC:	.BYTE 0,0,0,5,5,6,9,4
	.BYTE 9,7,9,8,2,0,0,0
IXVOLC:	.BYTE 0,0,0,0,0A,14,20,32
	.BYTE 3A,4C,5A,6C,7C,0,0,0
	.PAGE
	.SBTTL SHIP TO SHIP COLLISION


SHTOSH:	LDX PLAYUP
	LDA X,PLADED
	IFNE
	LDX ASHPID
SHTOS2: LDA X,ASDETM
	IFEQ
	JSR FRSTOS
	LDA I,0
	STA TEMP4
	LDA I,2
	STA OGLORY
	JSR TESTOO
	LDA TEMP4
	IFNE
	JMP EXPLYR
	THEN
	THEN
	THEN
	RTS
	
EXPLYR: JSR LAEXCO
	JSR STREXP
	JSR EXPLOS
	RTS
SHTOPA:	LDX PLAYUP
	LDA X,PLADED
	IFNE
	LDX I,3
	JSR SHTOS2
	LDX I,2
	JSR SHTOS2
	THEN
	RTS
	.PAGE
	.SBTTL PLAYER SHOTS VS ALIEN SHIPS


CDEVSP: 
	LDA I,PSHOTS-1
	STA TEMP1
	BEGIN
	LDY TEMP1
	LDA Y,SHACTM
	IFNE
	LDX I,1
	BEGIN
	STX TEMP2
	JSR TSTONE
	LDX TEMP2
	DEX
	MIEND
	THEN
	DEC TEMP1
	MIEND
	RTS

TSTONE: LDA X,ASDETM
	IFEQ
	LDA I,2
	STA OGLORY
	 JSR LODLSH
TESTOO: LDA X,ASCPVL
	STA POINT5
	LDA X,ASCPVH
	STA POINT6
	LDA X,ASCPHL
	STA POINT7
	LDA X,ASCPHH
	STA POINT8
	STX TEMP2
	JSR COLTST
	LDX TEMP2
	LDY TEMP1
	IFCC
	INC TEMP4
	JSR DEASHP
	THEN
	THEN
	RTS
CDASHP: LDY I,PSHOTS-1
	STY TEMP1
	BEGIN
	LDY TEMP1
	LDA Y,SHACTM
	IFNE
	LDX ASHPID
	JSR TSTONE
	THEN
	DEC TEMP1
	MIEND
	RTS

DEASHP: LDA RANDOM
	ORA I,40
	CPX I,2
	IFCS
	LDA I,30
	THEN
	STA X,ASDETM
	LDA I,0
	STA Y,SHACTM
	JSR LASHCO
	JSR STREXP
	JSR EXPLOS
	LDA I,0
	STA TEMP3
	LDA I,1
	STA TEMP4
	JMP AWDPTS
	
CDPVSP: 
	LDA I,PSHOTS-1
	STA TEMP1
	BEGIN
	LDY TEMP1
	LDA Y,SHACTM
	IFNE
	LDX I,3
	BEGIN
	STX TEMP2
	JSR TSTONE
	LDX TEMP2
	DEX
	CPX I,1
	EQEND
	THEN
	DEC TEMP1
	MIEND
	RTS
LASHCO: LDA X,ASCPVL
	STA POINT1
	LDA X,ASCPVH
	STA POINT2
	LDA X,ASCPHL
	STA POINT3
	LDA X,ASCPHH
	STA POINT4
	RTS
	.PAGE
	.SBTTL TEST COLLISION SHIP VS ENEMY SHOTS


CDPVSE:	JSR	LODCOR
	LDX	I,ASHOTS+3
	BEGIN
	LDA	X,BSACTM
	IFNE
	STX TEMP1
	JSR LSCRD
	LDA	I,0
	STA	OGLORY
	JSR	COLTST
	IFCC
	LDA SWITCH
	AND I,MTRCTR
	IFNE
	JSR SHIELD
	ELSE
	JSR LAEXCO
	JSR STREXP
	JSR EXPLOS
	THEN
	THEN
	LDX	TEMP1
	THEN
	DEX
	MIEND
	RTS

LSCRD:	LDX TEMP1
	LDA	X,BSCPVL
	STA 	POINT1
	LDA	X,BSCPVH
	STA	POINT2
	LDA	X,BSCPHL
	STA	POINT3
	LDA	X,BSCPHH
	STA	POINT4
	RTS
SHIELD: LDA I,70
	CLC
	ADC FUESED
	STA FUESED
	LDX TEMP1
	LDA I,0
	STA X,BSACTM
	RTS
	.PAGE
	.SBTTL	COLLISION-SHIP SHOTS VS BASES




PSVSEB:	LDA	I,PSHOTS-1
	STA	TEMP1
	BEGIN
	LDY	TEMP1
	LDA	AY,SHACTM
	IFNE
	LDX	I,7
	BEGIN
	LDA	AX,BSTAT
	IFNE
	JSR	LODLSH
	JSR	LODBAS
	STX	TEMP2
	LDA	I,0FF
	STA	OGLORY
	JSR	COLTST
	LDX	TEMP2
	LDY TEMP1
	IFCC
	JSR	DEABAS
	THEN
	LDX TEMP2
	THEN
	DEX
	MIEND
	THEN
	DEC	TEMP1
	MIEND
	RTS
SIOSVL:	.BYTE	-32.,0,-32.,-20,-20,-20,48.,-32.
DUOSVL: .BYTE 20,-20,20,20,-20,20,20,20
CTOSVL: .BYTE 20,-20,20,-20,20,-20,20,-20
NIOSVL: .BYTE -20,20,10,20,10,10,-20,20


SIOSHL: .BYTE   -32.,48.,-32.,0,0,0,32.,32.
DUOSHL: .BYTE -20,20,0,0,-20,0,20,0
CTOSHL: .BYTE 0,0,0,0,0,0,0,0
NIOSHL: .BYTE 10,0,-10,0,20,-10,0,-10

EAOSVL:	.BYTE -20,-20,-20,-20,-20,-20,-20,-20
EAOSHL:	.BYTE 0E0,0,0E0,0E0,20,20,20,0

EROSVL: .BYTE -20,-20,-20,20,20,20,-20,20
EROSHL: .BYTE 20,20,-20,-20,20,-20,-20,20
BZOSVL: .BYTE 20,-20,-20,20,-20,-20,-20,20
BZOSHL: .BYTE -20,-20,20,20,-20,0,20,-20

VLOSVL:	.BYTE 20,20,20,-20,-20,-20,-20,-20
VLOSHL:	.BYTE -20,-20,20,-20,20,20,-20,20
NOOSVL: .BYTE -10,10,-10,-10,-10,10,10,10
NOOSHL: .BYTE -10,-10,-10,0,-10,0,10,10
CKSUM3:: .BYTE 0CB
	.PAGE
	.SBTTL	COLLISION SHOTS VS. REACTOR

CDREAC:	LDX PLAYUP
	LDA X,CRPLNT
	TAX
	LDA X,PLANID
	AND I,20
	IFNE
	LDA I,MESTIM
	ORA MSGBT3
	STA MSGBT3
	LDA I,PSHOTS-1
	STA TEMP1
	BEGIN
	LDY TEMP1
	LDA Y,SHACTM
	IFNE
	JSR LODLSH
	JSR LODREA
	LDA I,0FF
	STA OGLORY
	LDA I,0
	STA TEMP2
	JSR COLTST
	LDY TEMP1
	IFCC
	JMP MLTDWN
	THEN
	THEN
	DEC TEMP1
	MIEND
	THEN
	RTS

LODREA:	LDA I,0E0
	STA POINT5
	LDA I,RECPVH
	STA POINT6
	LDA I,0
	STA POINT7
	LDA I,RECPHH
	STA POINT8
	RTS
MLTDWN:
	LDA RACTVE
	IFMI
	LDA I,0
	STA RACTVE
	LDA I,20
	STA ASDETM
	STA ASDETM+1
	LDA I,0
	STA Y,SHACTM
	STA TEMP3
	LDA I,5
	STA TEMP4
	JSR AWDPTS
	LDX PLAYUP
	LDA X,SSFLAG
	IFNE
	LDA I,-1
	STA TEMP1
	JSR IBSTAT
	THEN
	THEN
	RTS

UPAPTM:	LDA APTIME
	IFPL
	LDA FRAME
	AND I,3F
	IFEQ
	SED
	LDA APTIME
	SEC
	SBC I,1
	STA APTIME
	CLD
	IFEQ
	DEC APTIME
	LDX PLAYUP
	LDA I,0
	STA X,PLADED
	LDA I,CDPSPA
	STA X,STATE
	THEN
	THEN
	THEN
	RTS
	.PAGE
; INPUTS:	X= INDEX INTO BASE ARRAY
; 
; OUTPUTS: 	POINT5-8 LOADED WITH CENTER OF BASE COORDS

LODBAS: TXA
	TAY
	LDA NY,BAOSVL
	STA DIFF1
	IFMI
	LDA I,0FF
	ELSE
	LDA I,0
	THEN
	STA DIFF2
	LDA	Y,BACPVL
	CLC
	ADC DIFF1
	STA	POINT5
	LDA	Y,BACPVH
	ADC DIFF2
	STA	POINT6
	LDA NY,BAOSHL
	STA DIFF1
	IFMI
	LDA I,0FF
	ELSE
	LDA I,0
	THEN
	STA DIFF2
	LDA	Y,BACPHL
	CLC
	ADC DIFF1
	STA	POINT7
	LDA	Y,BACPHH
	ADC DIFF2
	STA	POINT8
	RTS


; INPUTS: Y= INDEX INTO SHOT ARRAY
; OUTPUTS: POINT1-4 LOADED WITH LANDER SHOT COORDS


LODLSH:	LDA	Y,SHCPVL
	STA	POINT1
	LDA	Y,SHCPVH
	STA	POINT2
	LDA	Y,SHCPHL
	STA	POINT3
	LDA	Y,SHCPHH
	STA	POINT4
	RTS

	.PAGE
	.SBTTL TRACTOR BEAM
;INPUTS: TRACTV: TASK ACTIVE FLAG
;	 ONTIME: TIMER FOR DEPOSIT ON
;	 OFTIME:    "   "    "     OFF
;	 SECTOR: RESIDENT SECTOR OF LANDER
;	 FSTAT:  DEPOSIT STATUS BYTE
;
;OUTPUTS:
;
;
TRACTR: LDA I,0
	STA TRSTAT
	LDX PLAYUP
	LDA X,PLADED
	IFNE
	LDA SWITCH
	AND I,MTRCTR
	IFNE
ATRACK:	LDX PLAYUP
	LDA X,CRPLNT
	CMP I,3
	IFEQ
	LDA LACPVH
	IFMI
	JMP FLTRCT
	THEN
	THEN
	LDA LANGLE
	CMP I,20
	IFCS
	EOR I,3F
	THEN
	CMP I,4
SPCAS1: LDA I,-1
	STA TRSTAT
	JSR SH0SN
	JSR RNGECK
	IFCC
	LDX SECTOR
	DEC X,FSTAT
	IFEQ
	JMP AWDFUE
	THEN
	THEN
	THEN
	THEN
	RTS

FLTRCT: LDA LANGLE
	CMP I,20
	IFCS
	EOR I,3F
	THEN
	CMP I,1C 
	JMP SPCAS1
	RTS


ATRCTR: LDA I,0
	STA TRSTAT
	LDA LACPVH
	BEQ TRACOK
	IFMI
TRACOK: LDX SECTOR
	LDA X,FSTAT
	IFNE
	LDA LSCALE
	IFEQ
	JMP ATRACK
	THEN
	THEN
	THEN
	RTS

AWDFUE: LDA ATRAFL
	IFNE
	SED
	LDY PLAYUP
	LDA I,0
	CLC
	ADC Y,FUEL1
	STA Y,FUEL1
	LDA Y,FUEL2
	ADC I,25
	STA Y,FUEL2
	LDA Y,FUEL3
	ADC I,0
	STA Y,FUEL3
	CLD
	JMP EXTLIF
	THEN
	RTS
	.PAGE

DSBEND: LDX ASHPID
	LDA X,ASDETM
	IFNE
	LDX I,NEXPLO*DOTS-1
	LDA I,0
	BEGIN
	ORA X,EXTIME
	DEX
	MIEND
	TAX
	IFEQ
	LDX I,5
	BEGIN
	LDA X,SAVCOR
	STA X,LACPVF
	DEX
	MIEND
	LDA I,0FF
	STA LSCALE
	JSR CLRSHT
	LDX PLAYUP
	LDA I,CEXPN2
	STA X,STATE
	THEN
	THEN
	RTS


CKDONE: LDX PLAYUP
	LDA X,CRPLNT
	TAX
	LDA LACPVH
	IFMI
	LDA X,NVEXIT
	CMP LACPVH
	ELSE
	CMP X,PVEXIT
	THEN
	LDY PLAYUP
	IFCS
	LDA I,CENVYG
	STA Y,STATE
	LDA X,PLANID
	CMP I,0A0
	IFNE
	JMP AWDBON
	ELSE
	LDA RACTVE
	IFPL
	JMP AWDBON
	THEN
	THEN
	THEN
	LDA LACPHH
	IFMI
	LDA I,0
	SEC
	SBC LACPHH
	SEC
	SBC I,1
	THEN
	CMP X,HEXIT
	IFCS
	LDA I,CENVYG
	STA Y,STATE
	JMP AWDBON
	THEN
	RTS
PVEXIT:	.BYTE 5,5,5,5,3,5,5,5,4,3
NVEXIT:	.BYTE -5,-5,-5,-6,-5,-6,-6,-6,-6,-6
HEXIT:	.BYTE 6,6,6,6,8,8,8,8,8,8
AWDBON: LDX PLAYUP
	LDA X,CRPLNT
	TAX
	LDA X,PLANID
	CMP I,0A0
	BEQ CHEAT
	LDA I,0
	LDX I,7
	BEGIN
	ORA X,BSTAT
	DEX
	MIEND
	TAX
	IFEQ
CHEAT:	LDX PLAYUP
	LDA X,PLBONS
	LDY I,0
	STY TEMP3
	STY X,PLBONS
	STA TEMP4
	JSR AWDPTS
	LDX PLAYUP
	LDA X,FIRPLA
	IFEQ
	INC X,FIRPLA
	LDA X,CRPLNT
	IFNE
	TAY
	LDA Y,SKPBON
	TAY
	LDA I,10
	STA TEMP4
	BEGIN
	JSR AWDPTS
	DEY
	MIEND
	THEN
	THEN
	ELSE
	LDX PLAYUP
	LDA X,PLBONS
	PHA
	LDA X,CRPLNT
	ASL
	CLC
	ADC PLAYUP
	TAX
	PLA
	STA X,SAVBON
	THEN
	RTS


SKPBON:	.BYTE 0,1,5,0B,13
CKENDW: LDX I,NEXPLO*DOTS-1
	LDA I,0
	BEGIN
	ORA X,EXTIME
	DEX
	MIEND
	LDX PLAYUP
	ORA X,PLADED
	IFEQ
	LDX PLAYUP
	LDA X,STATE
	CMP I,CPLAY
	IFEQ
	JSR SAVBAS
	JSR SAVFUE
	LDA I,CDIED2
	ELSE
	LDA I,CDIED
	THEN
	LDX PLAYUP
	STA X,STATE
	THEN
	RTS
	.PAGE
	.SBTTL CHECK SHIP IN PLANETS RANGE


PLANCK: LDX PLAYUP
	LDA X,PLADED
	IFEQ
	RTS
	THEN
ATPLCK:	LDX I,0B
	BEGIN
	LDY PLAYUP
	LDA X,SSACTV
	EOR Y,SSFLAG
	AND I,1
	IFEQ
	JMP SKIP3
	THEN
	TXA
	ASL
	CLC
	ADC PLAYUP
	TAY
	CPX I,0A
	IFCC
	LDA Y,SIRNXR
	IFMI
	JMP SKIP3
	THEN
	THEN
	LDA LACPHL
	SEC
	SBC X,PLCPHL
	STA POINT1
	LDA LACPHH
	SBC X,PLCPHH
	IFMI
	EOR I,0FF
	PHA
	LDA POINT1
	EOR I,0FF
	STA POINT1
	PLA
	THEN
	STA POINT2
	LDA LACPVL
	SEC
	SBC X,PLCPVL
	STA POINT3
	LDA LACPVH
	SBC X,PLCPVH
	IFMI
	EOR I,0FF
	PHA
	LDA POINT3
	EOR I,0FF
	STA POINT3
	PLA
	THEN
	STA POINT4
	IFEQ
	LDA POINT3
	CMP X,PLSIZE
	IFCC
	LDA POINT2
	IFEQ
	LDA POINT1
	CMP X,PLSIZE
	IFCC
	LDA X,PLANID
	AND I,40
	IFNE
	JSR LAEXCO
	JSR STREXP
	JSR EXPLOS
	ELSE
	LDY PLAYUP
	LDA X,PLANID
	IFMI
	AND I,20
	IFNE
	LDA Y,APDEAD
	IFNE
	RTS
	THEN
	THEN
	THEN
	STX Y,CRPLNT
	JSR INPNTR
	JSR EN1SN
	LDA I,CNWVYG
	LDX PLAYUP
	STA X,STATE
	THEN
	THEN
	LDX I,0
	THEN
	THEN
	THEN
SKIP3:	DEX
	MIEND
	RTS

PLCPVH: .BYTE 1,0FE,0FD,1,0,1,0FF,0FD,0FF,1,0,0
PLCPVL:	.BYTE 0F4,70,0BC,0C2,37,0C2,9C,0C0,10,0C0,0C8,0C8
PLCPHL:	.BYTE 40,66,0A8,4C,0A8,0CE,58,14,98,60,96,6
PLCPHH: .BYTE  2,2,0FD,0FE,0FD,0FF,2,0,0FD,2,0,0FF
PLSIZE:	.BYTE 40,40,30,28,40,40,30,40,30,40,20,20
PLANID:	.BYTE 0,0,0,90,0A0,0,0,0
	.BYTE 80,0A0,40,40
SSACTV:	.BYTE 1,1,1,1,1,0,0,0
	.BYTE 0,0,1,0
	.PAGE


SBTEST: LDX PLAYUP
	LDA X,PLADED
	IFNE
	LDY PLAYUP
	LDA Y,APDEAD
	IFEQ
	LDX I,1
	BEGIN
	LDA X,ASDETM
	IFEQ
	JSR LODCOR
	JSR LASCOR
	JSR TSTCOL
	LDA RNGFLG
	IFNE
	STX ASHPID
	LDX I,5
	BEGIN
	LDA I,0
	LDA X,LACPVF
	STA X,SAVCOR
	DEX
	MIEND
	JSR INIPOS
	LDY PLAYUP
	LDA I,CDSBTL
	STA Y,STATE
	LDX I,0
	THEN
	THEN
	DEX
	MIEND
	THEN
	THEN
	RTS

TSTCOL: LDA I,0
	STA RNGFLG
	LDA POINT1
	SEC
	SBC POINT5
	STA DIFF1
	LDA POINT2
	SBC POINT6
	IFMI
	EOR I,0FF
	PHA
	LDA DIFF1
	EOR I,0FF
	STA DIFF1
	PLA
	THEN
	STA DIFF2
	LDA POINT3
	SEC
	SBC POINT7
	STA DIFF3
	LDA POINT4
	SBC POINT8
	IFMI
	EOR I,0FF
	PHA
	LDA DIFF3
	EOR I,0FF
	STA DIFF3
	PLA
	THEN
	STA DIFF4
	IFEQ
	LDA DIFF3
	CMP I,50
	IFCC
	LDA DIFF2
	IFEQ
	LDA DIFF1
	CMP I,50
	IFCC
	LDA I,-1 
	STA RNGFLG
	THEN 
	THEN
	THEN
	THEN
	RTS



LASCOR: LDA X,ASCPVL
	STA POINT1
	LDA X,ASCPVH
	STA POINT2
	LDA X,ASCPHL
	STA POINT3
	LDA X,ASCPHH
	STA POINT4
	RTS
INIPOS: LDX ASHPID
	LDA X,ASCPVH
	CMP LACPVH
	IFEQ
	LDA X,ASCPVL
	CMP LACPVL
	THEN
	IFCS
	LDA I,0FE
	ELSE
	LDA I,1
	THEN
	STA LACPVH
	EOR I,0FF
	STA X,ASCPVH
	LDA X,ASCPHH
	CMP LACPHH
	IFEQ
	LDA X,ASCPHL
	CMP LACPHL
	THEN
	IFCS
	LDA I,0FE
	ELSE
	LDA I,1
	THEN
	STA LACPHH
	EOR I,0FF
	STA X,ASCPHH
	RTS

	
	
	.PAGE
	.SBTTL TRACTOR BEAM RANGE CHECK


RNGECK: LDY SECTOR
	LDA Y,FSTAT
	SEC
	IFEQ
	RTS
	THEN
	CLC
	LDA I,10
	ADC XACL2
	IFCS
	INC XACH2
	CLC
	THEN
	ADC NY,FHOS
	STA XACL2
	LDA XACH2
	ADC I,0
	STA XACH2
	LDA NY,FVOS
	STA POINT1
	LDA I,0
	STA POINT2
	LDX I,3
	BEGIN
	ASL POINT1
	ROL POINT2
	DEX
	MIEND
	LDA POINT2
	CMP I,8
	IFCS
	ORA I,0F0
	THEN
	STA POINT2
	LDA YACL2
	CLC
	ADC POINT1
	STA YACL2
	LDA YACH2
	ADC POINT2
	STA YACH2
	LDA LACPHL
	SEC
	SBC XACL2
	STA TEMP1
	LDA LACPHH
	SBC XACH2
	IFMI
	EOR I,0FF
	STA TEMP2
	LDA TEMP1
	EOR I,0FF
	STA TEMP1
	LDA TEMP2
	THEN
	IFNE
	SEC
	ELSE
	LDA TEMP1
	CMP I,HFRANG
	IFCC
	LDA LACPVL
	SEC
	SBC YACL2
	STA TEMP3
	LDA LACPVH
	SBC YACH2
	IFMI
	EOR I,0FF
	STA TEMP4
	LDA TEMP3
	EOR I,0FF
	STA TEMP3
	LDA TEMP4
	THEN
	IFNE
	SEC
	ELSE
	LDA TEMP3
	CMP I,VFRANG
	THEN
	THEN
	THEN
	RTS
	.PAGE
; INPUTS: X= INDEX INTO BASE ARRAY
; 
; OUTPUTS: Y=  "    "     "    "
; 
; OUTPUTS: TIMER FOR BASE DEACTIVATION SET
; 	   SHOT SLOT CLEARED FOR REFIRING


DEABAS:	LDA	I,0
	STA	X,BSTAT
	STA	Y,SHACTM
	JSR LODLSH
	JSR STREXP
	JSR EXPLOS
	LDA I,50
	STA TEMP3
	LDA I,2
	STA TEMP4
AWDPTS: LDA ATRAFL
	IFNE
	 SED
	CLC
	LDX PLAYUP
	LDA TEMP3
	ADC X,SCORE1
	STA X,SCORE1
	LDA TEMP4
	ADC X,SCORE2
	STA X,SCORE2
	PHP
	LDA X,SCORE3
	ADC I,0
	STA X,SCORE3
	PLP
	CLD
	IFCS
	INC X,LIVES
	JSR SAUCSN
	THEN
	THEN
	RTS

UPASTM:
	LDX PLAYUP
	LDA X,STATE
	CMP I,CPLAY
	IFEQ
	LDA X,CRPLNT
	TAX
	LDA X,PLANID
	IFMI
	RTS
	THEN
	THEN
	LDA FRAME
	AND I,1
	IFEQ
	LDX I,3
	BEGIN
	LDA X,ASDETM
	IFNE
	DEC X,ASDETM
	IFEQ
	CPX I,2
	IFCC
	LDY PLAYUP
	LDA Y,APDEAD
	IFEQ
	LDA Y,SSFLAG
	AND I,1
	IFEQ
	JSR INITEM
	ELSE
	JSR INITM3
	THEN
	THEN
	ELSE
	JSR INITM2
	THEN
	JSR SETVEL
	THEN
	THEN
	DEX
	MIEND
	THEN
	RTS
INITM2:	LDA I,0F8
	STA X,ASCPHH
	STX TEMP1
	LDX PLAYUP
	LDY X,CRPLNT
	LDA I,3
	LDX TEMP1
	STA X,ASCPVH
	LDA RANDOM
	STA X,ASCPHL
	STA X,ASCPVL
	RTS
	.PAGE
	.SBTTL	PLAYER FIRE
; INPUTS:	SWSTAT:	SWITCH INPUTS
; OUTPUTS:	SHOT POSITION AND VELOCITY ARRAYS UPDATED
;		SWSTAT CLEARED - IF PRESSED
;		A,X,Y UPDATED

PLFIRE:	LDX LASFIR
	LDA SWITCH
	AND I,MFIRE
	STA LASFIR
	IFEQ
	RTS
	THEN
	TXA
	AND I,MFIRE
	IFNE
	RTS
	THEN
	LDX PLAYUP
	LDA X,PLADED
	IFEQ
	RTS
	THEN
	LDA SWITCH
	AND I,MTRCTR
	IFNE
	RTS
	THEN
ATFIRE:	LDX I,PSHOTS-1
	BEGIN
	LDA X,SHACTM		;FIND OPEN SLOT
	IFNE
	JMP SKIP1
	THEN
	JSR PL0FIR
	LDA LACPVH		;PUT LANDER POSITION
	STA X,SHCPVH		;INTO CURRENT SHOT POSITION
	LDA LACPVL
	STA X,SHCPVL
	LDA LACPHH
	STA X,SHCPHH
	LDA LACPHL
	STA X,SHCPHL
	LDA I,0
	STA X,SHCPHF		;0 FRACTIONAL PORTIONS
	STA X,SHCPVF
	LDY PLAYUP
	LDA Y,STATE
	CMP I,CDPSPA
	IFEQ
	LDA I,0E
	ELSE
	LDA I,38
	THEN
	STA X,SHACTM		;SET ACTIVE TIMER
	LDY LANGLE
	LDA AY,SHVEFV		;TAKE SHOT VELOCITIES, AND IN
	CLC
	ADC	LAVEVF
	STA X,SHVEVF
	LDA AY,SHVELV
	ADC LAVEVL
	STA X,SHVEVL
	LDA AY,SHVHF
	CLC
	ADC	LAVEHF
	STA X,SHVEHF
	LDA AY,SHVHL
	ADC	LAVEHL
	STA X,SHVEHL
	LDY I,0FF		;USE LO BYTE VELOCITY TO
	LDA X,SHVEVL		;SET SIGN OF HIGH BYTE
	IFMI
	TYA
	STA X,SHVEVH
	ELSE
	INY
	TYA
	STA X,SHVEVH
	DEY
	THEN
	LDA X,SHVEHL
	IFMI
	TYA
	STA X,SHVEHH
	ELSE
	INY
	TYA
	STA X,SHVEHH
	THEN
	LDX I,0
SKIP1: 	DEX
	MIEND
	RTS
ATPLFR:	LDA LANGLE
	CMP I,30
	IFCC
	LDA RANDOM
	CMP I,80
	IFCS
	JMP ATFIRE
	THEN
	THEN
	RTS
	.PAGE
	.SBTTL 	CONTROL BASE FIRING
; INPUTS: ZOOMST: STATUS OF ZOOM TASK
; DBFIR:  SHOT DELAY TIMER
; BSACTM: SHOT ACTIVE TIMER
;
;OUTPUTS: DBFIRE- UPDATED
;JUMP TO SHOOT IF OK TO FIRE

BAFIRE:	LDA	ZOOMST	; CANT FIRE IF ZOOMING TRANSITION
	IFEQ
	LDA	RANDOM
	LDX PLAYUP
	CMP X,SHOTPR
	IFCC
	LDA X,CRPLNT
	CMP I,3
	IFEQ
	JMP NIMIFR
	THEN
	LDA	RANDOM ; RANDOMLY CHOOSE ANY OF THE 
	IFMI
	SEC
	ELSE
	CLC
	THEN
	AND	I,3	; EIGHT BASES
	ADC 	SECTOR
	TAX
	DEX
	TXA
	LSR
	AND	I,7
	TAY
	LDA	I,7
	STA	TEMP1
	BEGIN
	LDA	Y,BSTAT
	IFNE
	LDX	I,ASHOTS-1
	BEGIN		; LOOP FOR ALL BASES
	LDA	X,BSACTM	; SHOT ACTIVE
	IFEQ
	JMP SHOOT
	THEN
	DEX
	MIEND
	ELSE
	DEY
	IFMI
	LDY	I,7
	THEN
	THEN
	DEC	TEMP1
	MIEND
	THEN
	THEN
	RTS


	.PAGE
NIMIFR:	LDX I,0
	LDA LACPVH
	IFMI
	INX
	THEN
	LDA LACPHH
	IFMI
	INX
	INX
	THEN
	TXA
	ASL
	STA TEMP1
	LDA FRAME
	AND I,1
	CLC
	ADC TEMP1
	TAY
	LDA Y,NIFIRX
	TAY
	LDA I,7
	STA TEMP1
	BEGIN
	LDA Y,BSTAT
	IFNE
	LDX I,ASHOTS-1
	BEGIN
	LDA X,BSACTM
	IFEQ
	JMP SHOOT
	THEN
	DEX
	MIEND
	ELSE
	DEY
	IFMI
	LDY I,7
	THEN
	THEN
	DEC TEMP1
	MIEND
	RTS

NIFIRX:	.BYTE 6,7,5,7,0,4,1,3
	.PAGE
	.SBTTL ALIEN SHIPS FIRE



ASSCTL: LDX PLAYUP
	LDA X,APDEAD
	IFEQ
	LDX I,1
	BEGIN
	JSR ONESHT
	DEX
	MIEND
	THEN
	RTS

ONESHT:
	LDY PLAYUP
	LDA Y,PLADED
	IFNE
	LDA X,ASDETM
	IFEQ
	LDA X,ESHTIM
	IFEQ
	LDA PLAYUP
	IFEQ
	LDY WAVENO
	ELSE
	LDY WAVENO+1
	THEN
	LDA RANDOM
	CMP Y,ASHPRO
	IFCC
	JMP ASHOOT
	THEN
	THEN
	THEN
	THEN
	RTS

ASHPRO: .BYTE 8,10,20,40,50,60,70,80


ASHOOT: STX TEMP1
	LDA X,ASCPVL
	STA X,ESCPVL
	STA POINT1
	LDA X,ASCPVH
	STA X,ESCPVH
	STA POINT2
	LDA X,ASCPHL
	STA X,ESCPHL
	STA POINT3
	LDA X,ASCPHH
	STA X,ESCPHH
	STA POINT4
	JSR LODCOR
	JSR ABSDIF
	LDA ASQUAD
	ASL
	TAY
	LDA POINT1
	SEC
	SBC POINT3
	LDA POINT2
	SBC POINT4
	IFMI
	INY
	THEN
	LDA Y,ANGTBL
	STA TEMP2
	LDA RANDOM
	AND I,7
	CLC
	ADC TEMP2
	TAY
	LDX TEMP1
	LDA Y,SHVHF
	STA X,ESHVHF
	LDA Y,SHVHL
	STA X,ESHVHL
	LDA Y,SHVEFV
	STA X,ESHVVF
	LDA Y,SHVELV
	STA X,ESHVVL
	PHA
	LDY I,0FF
	PLA
	IFMI
	TYA
	ELSE
	INY
	TYA
	DEY
	THEN
	STA X,ESHVVH
	LDA X,ESHVHL
	IFMI
	TYA
	ELSE
	INY
	TYA
	THEN
	STA X,ESHVHH
	LDA PLAYUP
	IFEQ
	LDA STATE
	ELSE
	LDA STATE+1
	THEN
	CMP I,CDPSPA
	IFCS
	CMP I,CDSBTL
	IFEQ
	LDA I,30
	ELSE
	LDA I,0A
	THEN
	ELSE
	LDA PLAYUP
	IFEQ
	LDY CRPLNT
	ELSE
	LDY CRPLNT+1
	THEN
	LDA Y,SHPRNG
	THEN
	STA X,ESHTIM
	JSR PL1FIR
	RTS
	
ANGTBL: .BYTE 20,28,38,30,10,18,0,8
SHPRNG:	.BYTE 28,28,1C,0,0,8,18,28,0
	.PAGE
	.SBTTL SHOOT FROM A BASE
; INPUTS: X= INDEX OF SHOT SLOT
; 	Y=     "   "  BASE
; OUTPUTS: POSITION AND VELOCTIY ARRAYS SET UP

SHOOT: 	LDA	I,0
	STA	X,BSCPVF
	STA	X,BSCPHF	; ZERO FRACTIONAL PORTIONS
	LDA	AY,BACPVL
	STA	X,BSCPVL
	LDA 	AY,BACPVH
	STA	X,BSCPVH
	LDA	AY,BACPHL
	STA	X,BSCPHL
	LDA	AY,BACPHH
	STA	X,BSCPHH
	LDA	RANDOM
	IFMI
	TYA
	CLC
	ADC I,8
	TAY
	THEN
	LDA	NY,SHRANG
	STA	X,BSACTM
	LDA 	NY,MNANGL
	STA	TEMP1
	LDA	RANDOM
	AND	NY,ANGLMA
	CLC
	ADC	TEMP1
	AND	I,3F
	TAY
	LDA	AY,SHVHF	;SETUP SHT VELOCITIES
	STA	X,BSVEHF
	LDA	AY,SHVHL
	STA	X,BSVEHL
	LDA	AY,SHVEFV
	STA	X,BSVEVF
	LDA	AY,SHVELV
	STA	X,BSVEVL
	PHA
	LDY	I,0FF
	PLA
	IFMI
	TYA
	ELSE
	INY
	TYA
	DEY
	THEN
	STA X,BSVEVH
	LDA	X,BSVEHL
	IFMI
	TYA
	ELSE
	INY
	TYA
	THEN
	STA X,BSVEHH
	JSR PL1FIR
	RTS


EASHRG:	.BYTE 48,48,40,48,48,48,48,48
	.BYTE 20,20,20,20,20,40,18,20
SISHRG:	.BYTE	48,48,48,48,48,48,38,48
	.BYTE 18,20,10,18,20,1E,20,28
CTSHRG:	.BYTE	1A,20,1A,20,16,20,1B,30
	.BYTE	30,48,1C,48,20,26,30,48
NISHRG: .BYTE	40,40,40,40,40,40,40,40
	.BYTE	40,40,28,40,40,40,40,40
DUSHRG: .BYTE	  30,20,26,0F,1E,11,30,0E
	.BYTE	28,28,0F,0F,48,1C,32,0E

EAMNAN:	.BYTE	38,30,35,38,38,38,3E,30
	.BYTE	0E,30,10,0E,2E,2C,36,30
SIMNAN:	.BYTE	39,31,3A,3,0,38,2F,38
	.BYTE	5,1D,1C,0B,0,2E,1F,26
CTMNAN:	.BYTE	14,36,17,35,17,38,17,0A
	.BYTE	26,3,24,3E,18,39,2C,38
NIMNAN: .BYTE	2E,20,18,1D,30,8,38,7
	.BYTE 38,20,12,1D,32,8,38,7
DUMNAN: .BYTE	  5,2F,25,14,7,0F,2C,15
	.BYTE 15,30,17,16,37,24,2D,22

EAANMA:	.BYTE	0F,1F,1F,0F,0F,0F,0F,1F
	.BYTE	3,1F,7,3,3,1F,1F,1F
SIANMA:	.BYTE	0F,0F,0F,3,0F,0F,0F,0F
	.BYTE	7,7,3,3,0F,0F,3,3
CTANMA:	.BYTE	0F,0F,0F,0F,0F,0F,0F,3
	.BYTE	1,3,1,3,1,1,3,1
NIANMA:	.BYTE	0F,0F,7,7,3,1F,1F,1F
	.BYTE	0F,0F,7,7,3,1F,1F,1F
DUANMA: .BYTE	3,3,3,0F,7,1F,1,1F
	.BYTE	3,3,3,0F,0,3,1,7
BZSHRG:	.BYTE	30,40,40,30,40,48,38,48
	.BYTE	48,40,40,40,40,48,38,28
BZMNAN:	.BYTE	8,38,34,30,38,8,30,8
	.BYTE	7,3C,39,37,0,7,30,8
BZANMA:	.BYTE	7,0F,0F,7,0F,1,7,3
	.BYTE	1,0F,0F,1,0F,1,7,7
ERSHRG:	.BYTE 40,48,30,30,30,18,48,18
	.BYTE 54,48,28,20,18,2C,48,18
ERMNAN:	.BYTE 32,29,8,8,2F,11,3C,1F
	.BYTE 2E,35,0D,10,23,13,4,20
ERANMA:	.BYTE 7,0F,7,7,7,3,0F,1
	.BYTE 1,0F,7,0F,7,0,0F,1
VLSHRG:	.BYTE 30,40,28,20,1C,40,20,20
	.BYTE 40,30,18,37,38,40,20,28
VLMNAN:	.BYTE 10,14,39,6,28,29,6,5
	.BYTE 0C,15,18,17,3B,28,10,2F
VLANMA:	.BYTE 0F,1,1,3,1F,7,3,3
	.BYTE 3,1,1F,1,0,7,3,3
NOSHRG:	.BYTE 38,28,38,38,30,18,30,40
	.BYTE 38,28,38,40,20,28,28,38
NOMNAN:	.BYTE 7,8,39,0D,0C,0E,18,27
	.BYTE 15,22,0F,37,14,2C,2D,34
NOANMA:	.BYTE 1,1,1,1,1,7,3,1
	.BYTE 1,3,1,1,3,3,3,1
	.PAGE
	.SBTTL	UPDATE SHOT POSITIONS

; INPUTS:	SHVEVI,SHVEHI=SHOT V VELOCITIES
;		SHVEHI,SHVEHF=SHOT V VELOCITIES
;
;		SHCPH=CURRENT SHOT POSITION
;
; OUTPUTS:	SHOT POSITION ARRAY UPDATED
;		SHOT ACTIVE TIMER UPDATED
;		DEACTIVATE SHOT IF TIME OUT




UPSHOP:	LDX I,ASHOTS+PSHOTS+1
	BEGIN
	LDA X,SHACTM
	IFNE
	LDA X,SHCPVF		;ADD IN FRACTIONAL DELTA
	CLC
	ADC X,SHVEVF
	STA X,SHCPVF
	LDA X,SHCPVL		;ADD IN LSB INTEGER
	ADC X,SHVEVL
	STA X,SHCPVL
	LDA X,SHCPVH
	ADC X,SHVEVH		;ADD IN MSB INTEGER
	STA X,SHCPVH
	IFMI
	CMP	I,0FA
	IFCC
	LDA	I,1
	STA	X,SHACTM
	THEN
	ELSE
	CMP	I,6
	IFCS
	LDA	I,1
	STA	X,SHACTM
	THEN
	THEN
	LDA X,SHCPHF
	CLC
	ADC X,SHVEHF
	STA X,SHCPHF
	LDA X,SHCPHL
	ADC X,SHVEHL
	STA X,SHCPHL
	LDA X,SHCPHH
	ADC X,SHVEHH
	STA X,SHCPHH
	IFMI
	CMP	I,0F8
	IFCC
	LDA	I,1
	STA	X,SHACTM
	THEN
	ELSE
	CMP	I,8
	IFCS
	LDA	I,1
	STA	X,SHACTM
	THEN
	THEN
	DEC X,SHACTM
	IFEQ
	JSR DEASHO
	THEN
	THEN
	DEX
	MIEND
	RTS

; INPUTS:	X=INDEX INTO SHOT INFO ARRAY
;
; OUTPUTS:	;SHOT POSITION ARRAY 0'D

DEASHO:	LDA I,0
	STA X,SHCPVH
	STA X,SHCPHH
	STA X,SHCPVL
	STA X,SHCPHL
	RTS
	.PAGE
	.SBTTL	SHOT VELOCITY TABLES

		;VERTICAL SHOT VELOCITIES
SHVELV:	.BYTE 10,0F,0F,0E,0E,0D,0C,0B
	.BYTE 0A,8,7,6,4,3,1,0
	.BYTE 0,0FE,0FD,0FB,0F9,0F8,0F7,0F5
	.BYTE 0F4,0F3,0F2,0F1,0F1,0F0,0F0,0F0
	.BYTE 0F0,0F0,0F0,0F1,0F1,0F2,0F3,0F4
	.BYTE 0F5,0F7,0F8,0F9,0FB,0FD,0FE,0FF
SHVHL:	.BYTE 0,1,3,4,6,7,8,0A
	.BYTE 0B,0C,0D,0E,0E,0F,0F,0F
	.BYTE 10,0F,0F,0E,0E,0D,0C,0B
	.BYTE 0A,8,7,6,4,3,1,0
	.BYTE 0,0FE,0FD,0FB,0F9,0F8,0F7,0F5
	.BYTE 0F4,0F3,0F2,0F1,0F1,0F0,0F0,0F0
	.BYTE 0F0,0F0,0F0,0F1,0F1,0F2,0F3,0F4
	.BYTE 0F5,0F7,0F8,0F9,0FB,0FD,0FE,0FF
		;HORIZONTAL SHOT VELOCITIES
SHVEFV:	.BYTE 0,0B0,50,0D0,20,50,60,50
	.BYTE 20,0E0,90,20,0A0,20,90,10
	.BYTE 0,70,0E0,60,0E0,70,20,0E0
	.BYTE 0B0,0A0,0B0,0E0,30,0B0,50,10
	.BYTE 0,50,0B0,30,0E0,0B0,0A0,0B0
	.BYTE 0E0,20,70,0E0,60,0E0,70,0FF
SHVHF:	.BYTE 0,90,20,0A0,20,90,0E0,20
	.BYTE 50,60,50,20,0D0,50,0B0,0F0
	.BYTE 0,0B0,50,0D0,20,50,60,50
	.BYTE 20,0E0,90,20,0A0,20,90,10
	.BYTE 0,70,0E0,60,0E0,70,20,0E0
	.BYTE 0B0,0A0,0B0,0E0,30,0B0,50,10
	.BYTE 0,50,0B0,30,0E0,0B0,0A0,0B0
	.BYTE 0E0,20,70,0E0,60,0E0,70,0FF
	.PAGE
	.SBTTL UP EXPLOSIONS

UPEXPO: LDX I,NEXPLO*DOTS/2
	LDA FRAME
	AND I,1
	IFEQ
	LDX I,NEXPLO*DOTS
	THEN
	LDY I,NEXPLO*DOTS/2
	BEGIN
	LDA X,EXTIME
	IFNE
	LDA X,EXCPVF
	CLC
	ADC X,EXVEVF
	STA X,EXCPVF
	LDA X,EXCPVL
	ADC X,EXVEVL
	STA X,EXCPVL
	LDA X,EXCPVH
	ADC X,EXVEVH
	STA X,EXCPVH
	LDA X,EXCPHF
	CLC
	ADC X,EXVEHF
	STA X,EXCPHF
	LDA X,EXCPHL
	ADC X,EXVEHL
	STA X,EXCPHL
	LDA X,EXCPHH
	ADC X,EXVEHH
	STA X,EXCPHH
	DEC X,EXTIME
	THEN
	DEX
	DEY
	MIEND
	RTS
	.PAGE
	.SBTTL START EXPLOSION


STREXP: LDY EXCNTR
	INY
	CPY I,3
	IFCS
	LDY I,0
	THEN
	STY EXCNTR
	LDA I,4
	STA TEMP3
	LDA Y,EXPIX
	TAY
	BEGIN
	LDA POINT1
	STA Y,EXCPVL
	LDA POINT2
	STA Y,EXCPVH
	LDA POINT3
	STA Y,EXCPHL
	LDA POINT4
	STA Y,EXCPHH
	LDA RANDOM
	AND I,3F
	ORA I,8
	STA Y,EXTIME
	JSR PICKV
	JSR PICKH
	INY
	DEC TEMP3
	MIEND
	RTS

EXPIX:  .BYTE 0,5,0A

SHTLF:  LDA I,0F8
	STA Y,EXVEHL
	LDA RANDOM
	STA Y,EXVEHF
	AND I,7
	CLC
	ADC Y,EXVEHL
	STA Y,EXVEHL
	LDA I,0FF
	STA Y,EXVEHH
	RTS

SHTRT:  LDA I,0
	STA Y,EXVEHH
	STA Y,EXVEHL
	LDA RANDOM
	STA Y,EXVEHF
	AND I,7
	CLC
	ADC Y,EXVEHL
	STA Y,EXVEHL
	RTS

SHTUP:  LDA I,0
	STA Y,EXVEVH
	STA Y,EXVEVL
	LDA RANDOM
	STA Y,EXVEVF
	AND I,7
	CLC
	ADC Y,EXVEVL
	STA Y,EXVEVL
	RTS

SHTDN:  LDA I,0F8
	STA Y,EXVEVL
	LDA RANDOM
	STA Y,EXVEVF
	AND I,7
	CLC
	ADC Y,EXVEVL
	STA Y,EXVEVL
	LDA I,0FF
	STA Y,EXVEVH
	RTS

PICKV:  LDA RANDOM
	BMI SHTDN
	BPL SHTUP

PICKH:  LDA RANDOM
	BMI SHTLF
	BPL SHTRT
	.PAGE
	.SBTTL START/END MESSAGES

INMSG1: LDX PLAYUP
	LDA X,CRPLNT
	TAX
	LDA X,PLANID
	AND I,20
	IFEQ
	LDA I,0
	LDX I,7
	BEGIN
	ORA X,BSTAT
	DEX
	MIEND
	LDY I,MISCOM
	TAX
	IFEQ
	TYA
	ORA MSGBIT
	STA MSGBIT
	ELSE
	TYA
	EOR I,0FF
	AND MSGBIT
	STA MSGBIT
	THEN
	ELSE 
	LDA APTIME
	CMP I,20
	IFCC
	LDA I,10
	STA TEMP1
	BEGIN
	LDA APTIME
	AND TEMP1
	IFNE
	LDA MSGBIT
	AND I,1F 
	ORA TEMP1
	STA MSGBIT
	RTS
	THEN
	LSR TEMP1
	EQEND
	THEN
	THEN
	RTS


	.PAGE
HISCHK:	LDX I,1
	STX TEMP1
	LDA ATRAFL
	IFEQ
	RTS
	THEN
	LDA I,0FF
	STA CRPLNT
	STA CRPLNT+1
	BEGIN
	LDX TEMP1
	LDY I,7
	BEGIN
	LDA X,SCORE3
	CMP Y,SCLDRH
	IFEQ
	LDA X,SCORE2
	CMP Y,SCLDRM
	IFEQ
	LDA X,SCORE1
	CMP Y,SCLDRL
	IFEQ
	CLC
	THEN
	THEN
	THEN
	IFCS
	LDA I,CGETIN
	STA STATE
	STA STATE+1
	LDA I,0
	STA POINT1
	STA POINT2
	STA POINT3
	STY X,CRPLNT
	LDA X,SCORE1
	STA DIFF1
	LDA X,SCORE2
	STA DIFF2
	LDA X,SCORE3
	STA DIFF3
	BEGIN
	LDA POINT1
	LDX Y,INLDRL
	STA Y,INLDRL
	STX POINT1
	LDA POINT2
	LDX Y,INLDRM
	STA Y,INLDRM
	STX POINT2
	LDA POINT3
	LDX Y,INLDRH
	STA Y,INLDRH
	STX POINT3
	LDA DIFF1
	LDX Y,SCLDRL
	STA Y,SCLDRL
	STX DIFF1
	LDA DIFF2
	LDX Y,SCLDRM
	STA Y,SCLDRM
	STX DIFF2
	LDA DIFF3
	LDX Y,SCLDRH
	STA Y,SCLDRH
	STX DIFF3
	DEY
	MIEND
	THEN
	DEY
	MIEND
	DEC TEMP1
	MIEND
	LDA CRPLNT
	IFPL
	CMP CRPLNT+1
	IFCS
	DEC CRPLNT+1
	THEN
	THEN
	LDA PLAYUP
	EOR I,1
	ASL
	ASL
	ORA PLAYUP
	ADC I,5
	STA OGLORY
	JMP INTLDR
	.PAGE
	.SBTTL	HISCORE INITIALS PREP
INTLDR:	LDY I,CATRCT		;DEFAULT IS FAILURE
	LDA OGLORY
	IFNE				;--;MORE HI SCORES?
	AND I,3				;YES.
	STA PLAYUP			;SET UP PLAYER ID
	DEC PLAYUP			;
	LSR OGLORY
	LSR OGLORY
	LDX PLAYUP
	LDA X,CRPLNT
	IFPL
	STA THSTAT
	LDA I,60.
	STA EGEFLG
	LDA I,2
	STA ZOOMST			;SET UP INITIAL COUNTER
	LDY I,CGETIN
	STY STATE
	RTS
	ENDIF
	JMP INTLDR			;TRY AGAIN
	ENDIF
	LDX I,0
	STX PLAYUP
	DEX
	STX POINT8
	STY STATE
	RTS


TRBFSC: LDX I,8
	LDY I,7
	BEGIN
	LDA X,SCBUFR
	STA Y,SCLDRL
	LDA X,INBUFR
	STA Y,INLDRL
	DEX
	LDA X,SCBUFR
	STA Y,SCLDRM
	LDA X,INBUFR
	STA Y,INLDRM
	DEX
	LDA X,SCBUFR
	STA Y,SCLDRH
	LDA X,INBUFR
	STA Y,INLDRH
	DEY
	DEX
	MIEND
	RTS
TRSCBF: LDX I,8
	LDY I,7
	BEGIN
	LDA Y,SCLDRL
	STA X,SCBUFR
	LDA Y,INLDRL
	STA X,INBUFR
	DEX
	LDA Y,SCLDRM
	STA X,SCBUFR
	LDA Y,INLDRM
	STA X,INBUFR
	DEX
	LDA Y,SCLDRH
	STA X,SCBUFR
	LDA Y,INLDRH
	STA X,INBUFR
	DEY
	DEX
	MIEND
	RTS
	.PAGE
	.SBTTL	GET INITLS FROM PLAYER

GETINI:	LDA FRAME
	AND I,1F
	IFEQ
	DEC EGEFLG
	IFEQ
	LDX PLAYUP
	LDA X,CRPLNT
	CMP I,5
	IFCS
	JSR WRHIIN
	THEN
	JMP INTLDR
	THEN
	THEN
	LDA I,40
	STA LSCALE
	LDA I,1
	STA BSCALE
	LDX THSTAT
	JSR CHLETR
	LDX LASFIR
	LDA SWITCH
	AND I,MTRCTR
	STA LASFIR
	IFNE
	TXA 
	AND I,MTRCTR
	IFEQ
	LDA THSTAT
	CLC
	ADC I,8
	STA THSTAT
	DEC ZOOMST
	IFMI
	CMP I,29.
	IFCS
	JSR TRSCBF
	JSR WRHIIN
	THEN
	JSR INTLDR
	ELSE
	LDX THSTAT
	LDA I,1
	STA X,INLDRH
	THEN
	THEN
	THEN
	LDA I,CDGETI
	STA DSTATE
	STA DSTATE+1
	RTS
CHLETR:	LDA FRAME
	AND I,7
	IFEQ
	LDA SWITCH
	PHA
	AND I,MROTRT
	IFNE
	LDY X,INLDRH
	INY
	CPY I,27.
	IFCS
	LDY I,0
	THEN
	TYA
	STA X,INLDRH
	THEN
	PLA
	AND I,MROTLF
	IFNE
	LDY X,INLDRH
	DEY
	IFMI
	LDY I,26.
	THEN
	TYA
	STA X,INLDRH
	THEN
	THEN
	RTS


INHISC: LDX I,7
	BEGIN
	LDA X,DHISCM
	STA X,SCLDRM
	LDA X,DHIINM
	STA X,INLDRM
	LDA X,DHISCL
	STA X,SCLDRL
	LDA X,DHIINL
	STA X,INLDRL
	LDA X,DHIINH
	STA X,INLDRH
	DEX
	MIEND
	RTS

DHISCM: .BYTE 13,38,53,72,75,80,92,99
DHISCL: .BYTE 50,0,50,50,0,0,50,0
DHIINH: .BYTE 4,4,0C,7,0D,0A,0D,12
DHIINM: .BYTE 16,4,16,0A,5,0F,0C,4
DHIINL: .BYTE 5,13,12,0C,3,5,8,1
	.PAGE
	.SBTTL	IRQ

;INPUTS NONE
;OUTPUTS:SOFTWARE INTEGRITY CHECK, POWERON
;	 IF NECESSARY

IRQ:	PHA
	TXA
	PHA
	TYA
	PHA
	CLD
	TSX
	CPX I,0D0
	IFCS
	LDA SYNC
	CMP I,20
	LDA AX,106
	CMP I,PROG/100
	IFCS
	JMP SOFTOK
	THEN
	THEN
	STA VGSTOP
	BRK
	.PAGE

	.SBTTL	NORMAL IRQ PROCESSING

;INPUTS:NONE
;OUTPUTS:NONE

SOFTOK:	STA WTCHDG		;MUZZLE DOG
	STA INTACK
	JSR READSW
	JSR MODSND
	JSR MOOLAH
	JSR UPGTIM
	LDA $INTCT
	AND I,0F
	IFEQ
	JSR EAUPD
	THEN
	INC SYNC
	LDA HALT
	AND I,MSTEST
	IFNE
	LDA HALT
	AND I,40
	IFNE
	LDA BUFRDY		;TIME TO SWAP BUFFERS?
	IFEQ
	STA SYNC		;RESET SYNC
	LDA VECRAM+1
	EOR I,BUFR2/200
	STA VECRAM+1		;POINT TO NEW BUFFER
	LDY I,<VECRAM+BUFR2>/100
	AND I,BUFR2/200
	IFNE
	LDY I,VECRAM/100
	THEN			;POINT TO OPEN BUFFER
	STY BUFRDY
	THEN
	JSR CTLEDS
	STA VGSTOP
	STA VGSTART
	THEN
	THEN
	PLA
	TAY
	PLA
	TAX
	PLA
	RTI

	.PAGE
	.SBTTL	READ GAME SWITCHES

;INPUTS:GAMESW:ADDRESS OF ROTATE,
;	FIRE,TRACTOR,THRUST SWITCHES
;
;	POTGO:CONTROL PORT FOR READING
;	      SWITCHES
;
;
;OUTPUTS:SWITCH:STATE OF SWITCHES
;	 SAVED IN SWITCHS


READSW:	LDA IN2
	AND I,0E0
	STA IQTMP1
	LDA IN1
	AND I,1F
	ORA IQTMP1
	EOR I,0FF
	STA SWITCH
	RTS

	.PAGE
	.SBTTL	CONTROL LEDS & EMCOUNTERS

CTLEDS: LDA ATRAFL
	IFNE
	LSR
	IFNE
	LDA I,20
	ELSE
	LDA I,10
	THEN
	ELSE
	LDA $INTCT
	AND I,40
	IFNE
	LDA $$CRDT
	IFNE
	CMP I,2
	IFCS
	LDA I,0
	ELSE
	LDA I,10
	THEN
	ELSE
	LDA I,30
	THEN
	ELSE
	LDA I,30
	THEN
	THEN
	TAX
	LDA $CCTIM+2
	ORA $CCTIM+1
	IFMI
	TXA
	ORA I,3
	TAX
	THEN
	TXA
	ORA I,0C8
	STA OUT0
	RTS
	.PAGE
	.SBTTL OUTPUT EAROM DATA

PEAROM: BEGIN
	JSR DEAROM
	LDA SWITCH
	AND I,MROTRT!MROTLF!MSTRT1!MSTRT2
	EOR I,MROTRT!MROTLF!MSTRT1!MSTRT2
	IFEQ
	JSR EAZBOO
	THEN
	LDA IN0
	AND I,MSTEST
	NEEND
	JMP POWERON

CKSUM5:: .BYTE 0
DEAROM: BEGIN
	LDX BUFRDY
	NEEND
	STX VGLIST+1
	LDA I,VECRAM&0FF+2
	STA VGLIST
	LXL START 
	LAH START
	JSR VGJSRL
	LDA I,0
	LDX I,40
	TAY
	JSR VGVTR
	JSR SETRED
	LDA N1PGAM+1
	JSR DNUMBR
	LDA N1PGAM
	JSR DNUMBR
	LDA I,0E8
	TAX
	LDY I,0
	JSR VGVTR
	LDA N2PGAM+1
	JSR DNUMBR
	LDA N2PGAM
	JSR DNUMBR
	LDA I,0E8
	TAX
	LDY I,0
	JSR VGVTR
	LDA TOTSCR+2
	JSR DNUMBR
	LDA TOTSCR+1
	JSR DNUMBR
	LDA TOTSCR
	JSR DNUMBR
	LDA I,0DC
	TAX
	LDY I,0
	JSR VGVTR
	LDA TOTIME+2
	JSR DNUMBR
	LDA TOTIME+1
	JSR DNUMBR
	LDA TOTIME
	JSR DNUMBR
	JSR VGHALT
	LDA I,0
	STA BUFRDY
	RTS

CKSUM4:: .BYTE 0FD
UPGTIM: LDA ATRAFL
	IFNE
	LDA $INTCT
	IFEQ
	INC GTIMER
	IFEQ
	INC GTIMER+1
	THEN
	LDA PAUSTM
	IFPL
	DEC PAUSTM
	THEN
	LDX PLAYUP
	LDA X,STATE
	CMP I,CPLAY
	IFEQ
	LDA GTIMER
	AND I,1
	IFEQ
	LDA X,PLBONS
	IFNE
	SED
	SEC
	SBC I,1
	STA X,PLBONS
	CLD
	THEN
	THEN
	THEN
	THEN
	THEN
	RTS
	.PAGE
	.SBTTL	DISPLAY ROUTINE

;INPUTS:BUFRDY=POINTER TO OPEN BUFFER
;OUTPUTS:BUFRDY CLEARED TO INDICATE READY TO
;	 DISPLAY THIS BUFFER

DISPLA:	BEGIN
	LDX BUFRDY		;WAIT TIL BUFFER IS AVAILABLE
	NEEND
	STX VGLIST+1
	LDA I,VECRAM & 0FF+2
	STA VGLIST
	LXL START
	LAH START
	JSR VGJSRL
	LDA BSCALE
	LDY LSCALE
	JSR VGSCAL
	LDX PLAYUP
	LDA X,DSTATE
	TAX
	CPX I,EDLIST-DSTLST
	IFCS
	BRK
	THEN
	LDA X,DSTLST+1
	PHA
	LDA X,DSTLST
	PHA
	RTS
DSTLST:	.WORD DSETUP-1
	.WORD DPLAY-1
	.WORD DPAUSE-1
	.WORD DEXPND-1
	.WORD DATRCT-1
	.WORD DDPSPA-1
	.WORD DEXPN2-1
	.WORD DGETIN-1
	.WORD DATRM1-1
	.WORD DATRM2-1
EDLIST:
DPAUSE:
DSETUP:
DALWYS:	JSR DFCOUNT
	JSR DLIVES
	JSR DSCORE
	JSR DBONUS
	JSR DSUBON
	JSR DMESGE
	JSR DYNMSG
	JSR DPLAUP
	JSR DCRED
	JSR DTIMER
NAMONY:	JSR VGHALT
	LDA I,0
	STA BUFRDY
	RTS
DATRM2:
DPLAY:	JSR DLANDR
	JSR DSHELD
	JSR DSHOTS
	JSR DEXPLO
	JSR DLSCAP
	JSR DASHIP
	JMP DALWYS
DEXPND:	JSR DLANDR
	JSR DSHELD
	JSR DLSCAP
	JMP DALWYS
DATRM1:
DDPSPA:	JSR DLANDR
	JSR DSHELD
	JSR DMAP
	JSR DSHOTS
	JSR DEXPLO
	JSR DSTRFD
	JSR CRSTAR
	JSR DASHIP
	JMP DALWYS
DEXPN2:	JSR DLANDR
	JSR DSHELD
	JSR DMAP
	JSR DSTRFD
	JSR CRSTAR
	JSR DASHIP
	JMP DALWYS
DGETIN:	JSR DINSTR
	JMP DALWYS
DATRCT:	JSR DHSTBL
	JMP DALWYS
	.PAGE
	.SBTTL	DISPLAY HI SCORE TABLE
DHSTBL:
	LDA POINT8
	CMP I,0C0
	IFCS
DHISCR:	PLA
	PLA
	JSR HSTBOK
	JSR DRANK
	JMP DALWYS
	ELSE
	PLA
	PLA
	JSR DNAME
	JMP NAMONY
	THEN
	RTS

DINSTR: LDA I,MCONGR!MENTER!MPROTA!MTOCHG!MTRSEL!MPLAYR
	ORA MSGBT2
	STA MSGBT2
	JSR DPLAUP
	JMP DHISCR
HSTBOK:
	LDX I,7
	STX TEMP3
	LDX I,0F0
	STX POINT1
	BEGIN
	JSR VGCNTR
	LDX POINT1
	LDA I,0E0
	LDY I,0
	JSR VGVTR
	LDY PLAYUP
	LDA Y,CRPLNT
	BMI SRED
	CMP TEMP3
	IFNE
SRED:	JSR SETRED
	ELSE
	JSR SETWHT
	THEN
	LDA TEMP3
	EOR I,7
	CLC
	ADC I,1
	ASL
	JSR DSPCHR
	LDX I,0
	LDA I,6
	LDY I,0
	JSR VGVTR
	LDX TEMP3
	LDA X,INLDRH
	JSR DSPLTR
	LDX TEMP3
	LDA X,INLDRM
	JSR DSPLTR
	LDX TEMP3
	LDA X,INLDRL
	JSR DSPLTR
	LDX I,0
	LDA I,4
	LDY I,0
	JSR VGVTR
	LDX TEMP3
	CLC
	LDA X,SCLDRH
	JSR DIGIT2
	LDX TEMP3
	LDA X,SCLDRM
	JSR DIGIT2
	LDX TEMP3
	LDA X,SCLDRL
	JSR DIGIT2
	JSR VGCNTR
	LDA POINT1
	SEC
	SBC I,0D
	STA POINT1
	DEC TEMP3
	MIEND
	RTS
DSPLTR: ASL
	TAY
	IFEQ
	LDX VGMSGA
	LDA VGMSGA+1
	ELSE
	LDX Y,VGMSGA+14
	LDA Y,VGMSGA+15
	THEN
	JMP INJSRL


DNUMBR: PHA
	AND I,0F0
	LSR	
	LSR
	LSR
	JSR DSPCHR
	PLA
D1DIGT:	AND I,0F
	ASL
	JMP DSPCHR

DPLAUP:
	LDX PLAYUP
	LDA X,STATE
	CMP I,CEXPN2
	IFNE
	CMP I,CEXPND
	IFNE
	CMP I,CPAUSE
	IFNE
	CMP I,CGETIN
	IFNE
	LDA I,MPLAYR?0FF
	AND MSGBT2
	STA MSGBT2
	RTS
	THEN
	THEN
	THEN
	THEN
	JSR VGCNTR
	LDA I,MPLAYR
	ORA MSGBT2
	STA MSGBT2
	JSR DPLANO
	RTS

DPLANO: LDA I,10
	LDX I,5D
	LDY I,0
	JSR VGVTR
	LDX PLAYUP
	INX
	TXA
	JMP D1DIGT

	.PAGE
	.SBTTL	DISPLAY EXPLOSIONS
DEXPLO:	JSR SETPRP
	LDX I,NEXPLO*DOTS-1
	STX TEMP1
	BEGIN
	LDA X,EXTIME
	IFNE
	JSR VGCNTR
	LDX TEMP1
	LDA X,EXCPVH
	STA Z,YMSB
	LDA X,EXCPVL
	STA Z,YLSB
	LDA X,EXCPHH
	STA Z,XMSB
	LDA X,EXCPHL
	STA Z,XLSB
	LDA I,0
	STA VGBRIT
	LDX I,XCOMP
	JSR VGVCTR
	LDA I,0E0
	JSR VGDOT
	THEN
	DEC TEMP1
	LDX TEMP1
	EQEND
	RTS
	.PAGE
	.SBTTL	DISPLAY PLANETS
DMAP:
	LDX PLAYUP
	LDA X,STATE
	CMP I,CDSBTL
	IFNE
	LDA X,SSFLAG
	AND I,1
	IFEQ
	LDY I,8
	ELSE
	LDY I,12
	THEN
	TXA
	IFNE
	INY
	THEN
	STY TEMP1
	LDA I,4
	STA TEMP2
	BEGIN
	LDY TEMP1
	LDA Y,SIRNXR
	IFPL
	LDA Y,PLCOLR
	TAY
	LDA I,0
	JSR VGSTAT
	LDA TEMP1
	AND I,0FE
	TAY
	LDA Y,MAPADR+1
	LDX Y,MAPADR
	JSR VGJSRL
	THEN
	DEC TEMP1
	DEC TEMP1
	DEC TEMP2
	MIEND
	JSR SETYLW
	LDX PLAYUP
	LDA X,SSFLAG
	AND I,1
	ASL
	TAY
	LDA Y,STRJSR+1
	LDX Y,STRJSR
	JSR VGJSRL
	JSR SETWHT
	LXL HOPLAN
	LAH HOPLAN
	JSR VGJSRL
	JSR DBOMUL
	THEN
	RTS
MAPADR: .WORD NESCRE,SIRIN,DURIN,CENTRIC
	.WORD NOVA,ERCTUS,BIZARR,VECRAN
	.WORD VOLCAN,NOVA2
STRJSR: .WORD ASTRE,QUASAR
PLCOLR: .BYTE PURPLE,PURPLE,BLUE,BLUE,GREEN,GREEN,BLUGR,BLUGR
	.BYTE RED,RED,PURPLE,PURPLE,BLUE,BLUE,GREEN,GREEN
	.BYTE BLUGR,BLUGR,RED,RED
	.PAGE 
	.SBTTL STAR FIELD

DSTRFD:
 	LDX I,0F 
	STX TEMP1
	BEGIN
	JSR VGCNTR
	LDX TEMP1
	LDA X,STCPVL
	STA Z,YLSB
	LDA X,STCPVH
	STA Z,YMSB
	LDA X,STCPHL
	STA Z,XLSB
	LDA X,STCPHH
	STA Z,XMSB
	LDA I,0
	STA VGBRIT
	LDX I,XCOMP
	JSR VGVCTR
	JSR SETRND
	LDX TEMP1
	LDA X,STBRIT
	PHA
	SEC
	SBC I,4
	STA X,STBRIT
	PLA
	JSR VGDOT
	DEC TEMP1
	MIEND
	RTS


CRSTAR:
 	LDA WAIT
	IFEQ
	LDA FRAME
	AND I,0F
	TAX
	LDA RANDOM
	STA X,STCPVL
	LDA RANDOM
	AND I,3
	CLC
	ADC I,0FE
	STA X,STCPVH
	LDA RANDOM
	STA X,STCPHL
	LDA RANDOM
	AND I,3
	CLC
	ADC I,0FE
	STA X,STCPHH
	LDA I,0C0
	STA X,STBRIT
	LDA I,2
	STA WAIT
	ELSE
	DEC WAIT
	THEN
	RTS
	.PAGE
	.SBTTL	DISPLAY SHOTS

; INPUTS:	SHCPV,SHCPH=SHOT POSITIONS
;
; OUTPUTS:

DSHOTS:	LDX I,ASHOTS+PSHOTS+1
	STX TEMP1
	BEGIN
	LDA X,SHACTM
	IFNE
	JSR VGCNTR
	LDX TEMP1
	LDA X,SHCPHH		;SET UP COORDS TO DRAW BLANK
	STA Z,XMSB		;VECTOR TO SHOT POSITION
	LDA X,SHCPHL
	STA Z,XLSB
	LDA X,SHCPVL
	STA Z,YLSB
	LDA X,SHCPVH
	STA Z,YMSB
	LDA I,0
	STA VGBRIT
	LDX I,XCOMP
	JSR VGVCTR		;PUT VECTOR ON OPEN BUFFER
	JSR SETRED
	LDA I,0E0
	JSR VGDOT
	THEN
	DEC TEMP1
	LDX TEMP1
	MIEND
	RTS
	.PAGE
	.SBTTL	DISPLAY LANDER
;INPUTS:LACPVH,LACPVL,LACPHH,LACPHL=
;	LANDER COORDINATES
;	LANPIC=VECTOR ROM ADDRESS OF LANDER PICTURE

;OUTPUTS:LANDER PICTURE DRAWN
;	 VGLIST UPDATED
;	A,X,XCOMP,VGLIST UPDATED

DLANDR:	LDX PLAYUP
	LDA X,PLADED
	IFNE
	JSR DTOSHP
	JSR SETBLU
	JSR DRASHP
	JSR DFLAME
	THEN
	RTS
DTOSHP: JSR VGCNTR		;CENTER BEAM
	LDX I,3
	BEGIN
	LDA X,LACPHL		;SET UP COORDS FOR VGUT
	STA X,XCOMP
	DEX
	MIEND
	LDX I,XCOMP
	LDA I,0			;NULL VECTOR
	STA VGBRITE
	JSR VGVCTR
	RTS


	.SBTTL	DRAW SHIP

;INPUTS:LANGLE=ANGLE OF SHIP
;	LPICIX=TABLE OF INDEXES INTO LPICOS
;	LPICOS=TBLE OF VECTOR DELTAS USED TO
;	DRAW SHIP
;	LPICLN=# OF VECTORS USED TO DRAW SHIP
;
;OUTPUTS:COUNT,XCOMP,VGBRITE,Z,X,Y DESTROYED
;
;	 VGBUFFER UPDATED

DRASHP: LDA LANGLE
	AND I,0FE
	TAY
	LDA Y,SHJSRL+1
	LDX Y,SHJSRL
	JSR INJSRL
	RTS


DFLAME: LDA THSTAT
	IFNE
	JSR SETRED
	ELSE
	JSR SETBLK
	THEN
	LDA LANGLE
	AND I,0FE
	TAY
	LDX Y,FLADRS
	LDA Y,FLADRS+1
	JSR VGJSRL

DTRCTR: LDA TRSTAT
	IFNE
	JSR SETGRN
	JSR DTOSHP
	LDY PLAYUP
	LDX Y,CRPLNT
	LDY I,0
	CPX I,3
	IFEQ
	LDA LACPVH
	IFMI
	LDY I,2
	THEN
	THEN
	LDX Y,TRADRS
	LDA Y,TRADRS+1
	JSR VGJSRL
	THEN
	RTS
DSHELD: JSR SETRND
	LDA ATRAFL
	IFNE
	LDX PLAYUP
	LDA X,PLADED
	IFNE
	LDA SWITCH
	AND I,MTRCTR
	IFNE
	JSR DTOSHP
	LAH SHEPIC
	LXL SHEPIC
	JSR VGJSRL
	THEN
	THEN
	THEN
	RTS
FLADRS: .WORD FLAM01,FLAM02,FLAM03,FLAM04
	.WORD FLAM05,FLAM06,FLAM07,FLAM08
	.WORD FLAM09,FLAM10,FLAM11,FLAM12
	.WORD FLAM13,FLAM14,FLAM15,FLAM16
	.WORD FLAM17,FLAM18,FLAM19,FLAM20
	.WORD FLAM21,FLAM22,FLAM23,FLAM24
	.WORD FLAM25,FLAM26,FLAM27,FLAM28
	.WORD FLAM29,FLAM30,FLAM31,FLAM32

TRADRS: .WORD TRAC03,TRAC08

	
	.PAGE
	.SBTTL	DISPLAY LANDSCAPE
;INPUTS:	START ADDRESS OF VG INITIALIZATION
;		ROUTINE,HEDGEL,HEDGEH = LENGHT FROM CENTER TO
;		LEFT EDGE OF SCREEN, YINITL,YINITH = VERTICAL
;		STARTING POINT OF LANDSCAPE
;OUTPUTS:	XCOMP,BUFFER,A,X,Y,BUFRDY UPDATED

DLSCAP:  JSR VGCNTR		;CENTER BEAM
	LDA I,HEDGEL
	SEC
	SBC OVRSCN		;POINT TO LEFT HORIZ EDGE OF SCAPE
	STA XACCL
	IFEQ
	SEC
	THEN
	LDA I,HEDGEH
	SBC I,0
	STA XACCH
	LDA LFTSCT		;USE LEFTMOST SECTOR #*2 AS
	ASL			;INDEX INTO VERTICAL OFFSET TABLE
	TAY
	STY TEMP1
	TYA
	STA TEMP2
	LDA VLEGEL
	CLC
	ADC NY,SCTVOS		;ADJUST V STARTING POINT FOR THIS SECTOR
	STA YACCL
	LDA VLEGEH
	INY
	ADC NY,SCTVOS
	DEY
	STA YACCH
	BEGIN
	LDX PLAYUP
	LDA X,PLADED
	BEQ LITEON
	LDA X,SSFLAG
	AND I,4
	IFNE
	JSR SETBLK
	ELSE
LITEON:	JSR SETGRN
	THEN
	LDA LSCALE
	BNE DSECTR
	LDA TEMP1
	SEC
	SBC TEMP2
	AND I,1F
	CMP I,6
	IFCS
	CMP I,1C
	IFCC
DSECTR:	LDA I,0			;DRAW BLANK VECTOR TO STARTING POINT OF
	STA VGBRIT		;CURRENT SECTOR TO DRAW
	LDX I,3
	BEGIN
	LDA X,XACCL		;PUT COORDS INTO XCOMP - FOR VGVCTR
	STA ZX,XLSB
	DEX
	MIEND
	LDX I,XCOMP
	JSR VGVCTR		;DRAW VECTOR
	LDY TEMP1
	LDA NY,SECADR
	TAX
	INY
	LDA NY,SECADR
	IFNE
	JSR VGJSRL		;PUT SECTOR JSRL IN OPEN BUFFER
	JSR DBASE
	JSR DFUEL
	THEN
	JSR VGCNTR		;RECENTER
	THEN
	THEN
	LDY TEMP1
	INC XACCH
	LDA YACCL
	CLC
	ADC NY,SCTVDL
	STA YACCL
	LDA YACCH
	INY
	ADC NY,SCTVDL
	DEY
	STA YACCH
	INY			;POINT TO NEXT JSRL ADDRESS & OFFSETS
	INY
	TYA
	AND I,1F
	STA TEMP1
	CMP TEMP2		;DONE WITH ALL?
	EQEND
	JMP DREACT
OVSIRN:	.WORD 0,-100,-100,0E0
	.WORD -60,-140,-140,80
	.WORD -40,-40,80,0
	.WORD -120,-120,-120,100
OVDURN:	.WORD 0,-180,-260,-260,-200,-120,-240,-200
	.WORD 0,-180,-1C0,-240,-1C0,0,-1A0,-160
OVCNTC:	.WORD 0,80,40,60,60,0,0,80
	.WORD 0C0,0C0,40,80,20,40,40,80
OVNIMI: .WORD 0,0,0,0
	.WORD 0,-120,-180,-280
	.WORD -140,-1E0,-220,0E0
	.WORD 0,0,0,0
OVNOVA:	.WORD 0,0,0,0
	.WORD 0,0,0,0
	.WORD 0,0,0,0
	.WORD 0,0,0,0
OVEASY:	.WORD 0,0,-100,-100
	.WORD 0,0,0,-100
	.WORD -200,-200,-100,-100
	.WORD 0,-100,-100,0
OVBIZR:	.WORD 0,-100,-100,-100
	.WORD -100,-100,100,-200
	.WORD -200,-100,0,0
	.WORD 100,0,0,0
OVVOLC	=OVNIMI
OVERCT:	.WORD 0,0,2C0,380
	.WORD 580,140,140,-80
	.WORD -80,80,80,80
	.WORD 80,80,80,80
	.PAGE
SIBADR:	.WORD BASE01,0,BASE02,BASE13
	.WORD BASE04,0,BASE05,0
	.WORD 0,BASE06,0,0
	.WORD BASE07,0,BASE08,0
DUBADR: .WORD BASE11,0,0,BASE12
	.WORD BASE09,0,BASE09,0
	.WORD 0,0,BASE13,BASE09
	.WORD BASE14,0,BASE09,0

CTBADR: .WORD 0,BASE09,0,BASE10
	.WORD 0,BASE09,0,BASE10
	.WORD 0,BASE09,0,BASE10
	.WORD 0,BASE09,0,BASE10
NIBADR: .WORD 0,0,0,0
 	.WORD	BASE12,BASE14,BASE11,BASE14
	.WORD	BASE14,BASE11,BASE10,BASE11
	.WORD 0,0,0,0
NOBADR:	.WORD 0,0,0,BASE13
	.WORD BASE11,BASE13,BASE10,BASE13
	.WORD BASE09,0,BASE14,0
	.WORD BASE14,0,0,0
EABADR:	.WORD 0,BASE13,0,0
	.WORD BASE10,0,BASE13,BASE13
	.WORD 0,BASE12,0,BASE12
	.WORD BASE13,0,0,BASE10
BZBADR:	.WORD 0,BASE11,BASE13,0
	.WORD BASE12,BASE14,BASE13,BASE10
	.WORD 0,0,0,BASE14
	.WORD 0,BASE11,0,0
ERBADR:	.WORD 0,BASE12,BASE12,0
	.WORD BASE01,BASE11,0,0
	.WORD BASE14,0,BASE11,BASE01
	.WORD BASE14,0,0,0
VLBADR:	.WORD 0,0,0,BASE11
	.WORD BASE11,BASE14,BASE13,0
	.WORD BASE12,BASE12,BASE13,BASE12
	.WORD 0,0,0,0
SIBIX:	.BYTE	0,0,1,2,3,0,4,0
	.BYTE	0,5,0,0,6,0,7,0
DUBIX:	.BYTE 0,0,0,1,2,0,3,0
	.BYTE 0,0,4,5,6,0,7,0

CTBIX:	.BYTE 0,0,0,1,0,2,0,3
	.BYTE 0,4,0,5,0,6,0,7
NIBIX:	.BYTE 0,0,0,0,0,1,2,3
	.BYTE 4,5,6,7,0,0,0,0
EABIX:	.BYTE 0,0,0,0,1,0,2,3
	.BYTE 0,4,0,5,6,0,0,7
BZBIX:	.BYTE 0,0,1,0,2,3,4,5
	.BYTE 0,0,0,6,0,7,0,0
ERBIX:	.BYTE 0,0,1,0,2,3,0,0
	.BYTE 4,0,5,6,7,0,0,0
VLBIX:	.BYTE 0,0,0,0,1,2,3,0
	.BYTE 4,5,6,7,0,0,0,0
NOBIX:	.BYTE 0,0,0,0,1,2,3,4
	.BYTE 5,0,6,0,7,0,0,0
	.PAGE
	.SBTTL	DISPLAY LIVE BASES
; INPUTS:	TEMP1: SECTOR POINTER
;		*2
;		BASTAT: CURRENT STATUS OF BASES
;		XACCL,XACCH=POINTER TO LEFT MOST POINT OF
;		SECTOR HORIZONTAL
;		YACCL,YACCH=SAME POINTER FOR VERTICAL
;
; OUTPUTS:	BASES DRAWN
DBASE:	JSR VGCNTR		;CENTER BEAM
	LDY TEMP1		;LOAD INDEX INTO OFFSETS
	INY
	LDA NY,BASADR		;BASE IN THIS SECTOR?
	IFNE
	LDA TEMP1
	LSR
	TAY
	LDA NY,BSINDX
	TAY
	LDA Y,BSTAT		;CURRENTLY ALIVE?
	IFNE
	LDA Y,BACPVL
	STA Z,YLSB
	LDA Y,BACPVH
	STA Z,YMSB
	LDA Y,BACPHL
	STA Z,XLSB
	LDA Y,BACPHH
	STA Z,XMSB
	LDA I,0			;BLANK VECTOR
	STA VGBRITE
	LDX I,XCOMP
	JSR VGVCTR		;DRAW VECTOR
	JSR SETRED
	LDY TEMP1
	LDA NY,BASADR
	TAX
	INY
	LDA NY,BASADR
	JSR VGJSRL		;ADD BASE JSRL TO BUFFER
	THEN
	THEN
	LDY	TEMP1
	RTS
	.PAGE
DFUEL:	JSR VGCNTR		;CENTER BEAM
	JSR SETBLU
	LDA TEMP1
	LSR
	TAY
	LDA I,0
	STA POINT2
				;RESIDENT SECTOR OF FUEL?
	LDA Y,FSTAT
	IFNE
	LDA XACCL
	CLC			;POINT TO LOWER LEFT CORNER
	ADC NY,FHOS		;OF FUEL DEPOSIT
	STA Z,XLSB
	LDA XACCH
	STA Z,XMSB
	IFCS
	INC Z,XMSB
	THEN
	LDA NY,FVOS
	STA POINT1
	LDX I,3
	BEGIN
	ASL POINT1
	ROL POINT2
	DEX
	MIEND
	LDA POINT2
	CMP I,8
	IFCS
	ORA I,0F0
	THEN
	STA POINT2
	LDA YACCL
	CLC
	ADC POINT1
	STA Z,YLSB
	LDA YACCH
	ADC POINT2
	STA Z,YMSB
	LDA I,0
	STA VGBRIT		;DRAW BLANK VECTOR THERE
	LDX I,XCOMP
	JSR VGVCTR
	LDX PLAYUP
	LDA X,CRPLNT
	CMP I,3
	BNE REGFUL
	LDA Z,YMSB
	IFMI
	LDA FUEADR+3
	LDX FUEADR+2
	ELSE
REGFUL: LDX FUEADR
	LDA FUEADR+1
	THEN
	JSR VGJSRL		;PUT JSRL INTO OPEN BUFFER
	THEN
	RTS
FUEADR: .WORD FUEL07,FUEL01
SIFHOS:	.BYTE 32.,128.,128.,160.
	.BYTE 32.,96.,0,0
	.BYTE 96.,0,0,0
	.BYTE 96.,96.,0,192.
SIFVOS:	.BYTE 0,0,24,0EC
	.BYTE 1A,0,0,0
	.BYTE 0,0,0,0
	.BYTE 0,24,0,0F0

DUFVOS: .BYTE 0,0,0,0
	.BYTE 0A,0EE,4,0
	.BYTE 0,0,0F8,8
	.BYTE 8,0,0,0

DUFHOS: .BYTE 0,0,0C0,40
	.BYTE 60,0C0,80,0
	.BYTE 0,0,0C0,0A0
	.BYTE 80,0,40,40



CTFVOS: .BYTE 0,0,2,0
	.BYTE 0,8,8,0
	.BYTE 0,0,0,0
	.BYTE 0,4,4,0

CTFHOS: .BYTE 0,20,60,0
	.BYTE 0,20,40,0
	.BYTE 20,0A0,20,0
	.BYTE 0,40,80,0


NIFVOS: .BYTE 0,0,0,0
	 .BYTE 0,22,0F0,0
	.BYTE 30,30,4,0
	.BYTE 0,0,0,0

NIFHOS: .BYTE 0,0,0,0
	 .BYTE 40,0C0,40,60
	.BYTE 20,0C0,60,40
	.BYTE 0,0,0,0

EAFHOS:	.BYTE 0A0,0,80,0,80,60,0,0
	.BYTE 80,0,80,0,0,80,0,60
NOFID:
EAFVOS:	.BYTE 0,0,0,0
	.BYTE 0,0,0,0
	.BYTE 0,0,0,0
	.BYTE 0,0,0,0

BZFVOS: .BYTE 0,0,0,0
	.BYTE 0,0,0,0
	.BYTE 20,0,0,0
	.BYTE 0,0,0,0

BZFHOS: .BYTE 0,0A0,60,60
	.BYTE 80,40,0,80
	.BYTE 60,0,60,0
	.BYTE 60,0,60,0
ERFHOS:	.BYTE 60,0,20,0
	.BYTE 0,60,0,60
	.BYTE 40,40,40,60
	.BYTE 60,60,0,0
ERFVOS:	.BYTE 0,0,0,0
	.BYTE 0,0,0,0
	.BYTE 2C,2C,12,12
	.BYTE 24,0,0,0
VLFHOS:	.BYTE 0,0,0,0A0
	.BYTE 0A0,60,60,0
	.BYTE 0,60,40,80
	.BYTE 0,0,0,0
VLFVOS:	.BYTE 0,0,0,10
	.BYTE 40,0,20,0
	.BYTE 0,0,0,20
	.BYTE 0,0,0,0
DREACT:	LDX PLAYUP
	LDA X,CRPLNT
	TAX
	LDA X,PLANID
	AND I,20
	IFNE
	LDA RACTVE
	IFPL
	JSR SETRND
	ELSE
	JSR SETPRP
	THEN
	LXL REACTR
	LAH REACTR
	JSR VGJSRL
	THEN
	RTS
	.PAGE


DASHIP:	LDA ATRAFL 
	IFNE
	JSR SETRED
	LDX PLAYUP
	LDA X,STATE
	CMP I,CPLAY
	IFEQ
	JMP DPATRO
	THEN
	LDY X,APDEAD
	IFEQ
	CMP I,CDPSPA
	IFCS
	CMP I,CDSBTL
	IFEQ
	LDX ASHPID
	STX TEMP1
	JMP DSHIP1
	THEN
	LDX I,1
	STX TEMP1
	BEGIN
	JSR DSHIP1
	MIEND
	THEN
	THEN
	THEN
	RTS
DPATRO:	LDX PLAYUP
	LDA X,CRPLNT
	TAX
	LDA X,PLANID
	IFPL
	LDX I,1
	STX TEMP2
	LDX I,3
	STX TEMP1
	BEGIN
	JSR DSHIP1
	DEC TEMP2
	MIEND
	THEN
	RTS
DSHIP1: JSR VGCNTR
	LDX TEMP1
	LDA X,ASDETM
	IFEQ
	LDA X,ASCPVH
	STA Z,YMSB
	LDA X,ASCPVL
	STA Z,YLSB
	LDA X,ASCPHH
	STA Z,XMSB
	LDA X,ASCPHL
	STA Z,XLSB
	LDX I,XCOMP
	LDA I,0
	STA VGBRIT
	JSR VGVCTR
	LDA TEMP1
	ASL
	TAY
	LDA Y,ASADR+1
	LDX Y,ASADR
	JSR VGJSRL
	THEN
	DEC TEMP1
	LDX TEMP1
	RTS
ASADR: .WORD ASHIP1,ASHIP2,PTRL01,PTRL01
	.PAGE
	.SBTTL 	DISPLAY SCORE







DSCORE: LDX I,1
	STX TEMP4
	BEGIN
	JSR VGCNTR
	LDA I,0A0
	STA Z,XLSB
	LDA I,0D0
	STA Z,YLSB
	LDA I,1
	STA Z,YMSB
	LDX TEMP4
	LDA X,SCPHH
	STA Z,XMSB
	LDA I,0
	STA VGBRIT
	LDX I,XCOMP
	JSR VGVCTR
	LDA I,4
	CLC
	ADC TEMP4
	STA TEMP1
	SEC
	BEGIN
	LDX TEMP1
	LDA X,SCORE1
	JSR DIGIT2
	DEC TEMP1
	DEC TEMP1
	MIEND
	DEC TEMP4
	MIEND
	RTS

SCPHH:  .BYTE -2,0

INJSRL: LDY I,1
	STA NY,VGLIST
	DEY
	TXA
	STA NY,VGLIST
	LDA VGLIST
	CLC
	ADC I,2
	STA VGLIST
	IFCS
	INC VGLIST+1
	THEN
	RTS

SETBLK:	LDY I,0
	BEQ INSTAT
SETBLU: LDY I,BLUE
	BNE INSTAT
SETGRN: LDY I,GREEN
	BNE INSTAT
SETRED: LDY I,RED
	BNE INSTAT
SETBLG: LDY I,BLUGR
	BNE INSTAT
SETPRP: LDY I,PURPLE
	BNE INSTAT
SETYLW: LDY I,YELLOW
	BNE INSTAT
SETWHT: LDY I,WHITE
INSTAT: LDA I,0
	JMP VGSTAT

SETRND: LDA RANDOM
	AND I,0C
	IFEQ
	ORA I,1
	THEN
	TAY
	BNE INSTAT
	.PAGE


DFCOUNT: 
	LDA I,1
	LDY I,40
	JSR VGSCAL
	LDA I,1
	STA TEMP4
	BEGIN
	JSR VGCNTR
	JSR SETGRN
	LDA I,0A0
	STA Z,XLSB
	STA Z,YLSB
	LDA I,1
	STA Z,YMSB
	LDX TEMP4
	LDA X,FCPHH
	STA Z,XMSB
	LDA I,0
	STA VGBRIT
	LDX I,XCOMP
	JSR VGVCTR
	LDA I,4
	CLC
	ADC TEMP4
	STA TEMP1
	SEC
	BEGIN
	LDX TEMP1
	LDA X,FUEL1
	JSR DIGIT2
	DEC TEMP1
	DEC TEMP1
	MIEND
	DEC TEMP4
	MIEND
	RTS

FCPHH:  .BYTE -2,0
DSPCHR:	TAY
	LDX Y,VGMSGA+2
	LDA Y,VGMSGA+3
	JSR INJSRL
	RTS
	.PAGE



DLIVES: JSR SETBLU
	LDX I,1
	STX TEMP1
	BEGIN
	LDA X,LIVES
	IFNE
	IFPL
	CMP I,5
	IFCS
	LDA I,5
	THEN
	STA TEMP2
	LDA X,LIVCOH
	PHA
	LDA X,LIVCOV
	TAX
	PLA
	JSR MESGPOS
	BEGIN
	LDA SHJSRL+1
	LDX SHJSRL
	JSR INJSRL
	LDY TEMP1
	LDA Y,LIVOSV
	LDX Y,LIVOSH
	LDY I,0
	JSR VGVTR
	DEC TEMP2
	EQEND
	THEN
	THEN
	DEC TEMP1
	LDX TEMP1
	MIEND
	RTS


LIVCOV: .BYTE 74,74
LIVCOH: .BYTE 0A0,58
LIVOSV: .BYTE 0F8,8
LIVOSH: .BYTE 0FB,0FB
DTIMER:
	LDX PLAYUP
	LDA X,STATE
	CMP I,CPLAY
	IFEQ
	LDA X,CRPLNT
	TAX
	LDA X,PLANID
	AND I,20
	IFNE
	LDA APTIME
	IFPL
	JSR SETWHT
	JSR VGCNTR
	LDA I,0E0
	TAX
	LDY I,0
	JSR VGVTR
	LDA APTIME
	SEC
	JSR DIGIT2
	THEN
	THEN
	THEN
	RTS


DSUBON: LDX PLAYUP
	LDA X,STATE
	CMP I,CEXPN2
	IFEQ
	LDA X,FIRPLA
	CMP I,1
	IFEQ
	LSR
	STA POINT2
	LDY X,CRPLNT
	LDA Y,SUBONS
	STA POINT1
	LDY I,3
	BEGIN
	ASL POINT1
	ROL POINT2
	DEY
	MIEND
	LDA X,PLBONS
	CLC
	ADC POINT1
	STA POINT1
	IFCS
	INC POINT2
	THEN
	JSR VGCNTR
	LDA I,-8
	LDX I,4
	LDY I,0
	JSR VGVTR
	LDA POINT2
	SEC
	JSR DIGIT2
	LDA POINT1
	JSR DIGIT2
	LDA I,0
	CLC
	JSR DIGIT2
	THEN
	THEN
	RTS

SUBONS: .BYTE 0,2,6,12,20
DBONUS:	LDX PLAYUP
	LDA X,STATE
	CMP I,CPLAY
	IFEQ
	JSR VGCNTR
	LDA I,7F
	LDX I,70
	LDY I,0
	JSR VGVTR
	LDX PLAYUP
	LDA X,PLBONS
	SEC
	JSR DIGIT2
	LDA I,0
	JSR DIGIT2
	LDA I,MBONUS
	ORA MSGBIT
	STA MSGBIT
	ELSE
	LDA I,MBONUS?0FF
	AND MSGBIT
	STA MSGBIT
	THEN
	RTS
DBOMUL: JSR SETBLG
	LDA I,MESTIM?0FF
	AND MSGBT3
	STA MSGBT3
	LDY PLAYUP
	LDA Y,SSFLAG
	AND I,1
	IFNE
	LDA I,12
	ELSE
	LDA I,8
	THEN
	CLC
	ADC PLAYUP
	STA TEMP4
	LDA I,4
	STA TEMP3
	BEGIN
	JSR VGCNTR
	LDX TEMP4
	LDA X,SIRNXR
	IFPL
	LDA X,MPOSVH
	STA Z,YMSB
	LDA X,MPOSVL
	STA Z,YLSB
	LDA X,MPOSHH
	STA Z,XMSB
	LDA X,MPOSHL
	STA Z,XLSB
	STX TEMP2
	LDA I,0
	STA VGBRIT
	LDX I,XCOMP
	JSR VGVCTR
	LDX TEMP2
	LDA X,SAVBON
	SEC
	JSR DIGIT2
	LDA I,0
	JSR DIGIT2
	THEN
	DEC TEMP4
	DEC TEMP4
	DEC TEMP3
	MIEND
	RTS

MPOSVH: .BYTE 2,2,0FE,0FE,0FD,0FD,2,2,0,0
	.BYTE 2,2,0FF,0FF,0FD,0FD,0FE,0FE,2,2

MPOSVL: .BYTE 20,20,0,0,60,60,0,0,0,0
	.BYTE 20,20,0C0,0C0,60,60,0,0,0,0

MPOSHH: .BYTE 2,2,2,2,0FD,0FD,0FD,0FD,0FD,0FD
	.BYTE 0FF,0FF,2,2,0,0,0FC,0FC,2,2

MPOSHL: .BYTE 60,60,80,80,60,60,0E0,0E0,0,0
	.BYTE 0A0,0A0,10,10,0E0,0E0,0A0,0A0,0,0

DYNMSG: LDY I,1F
	STY TEMP3
	LDA MSGBIT
	STA POINT1
	LDA MSGBT2
	STA POINT2
	LDA MSGBT3
	STA POINT3
	LDA MSGBT4
	STA POINT4
	BEGIN
	ASL POINT1
	ROL POINT2
	ROL POINT3
	ROL POINT4
	IFCS
	LDY TEMP3
	LDA Y,MSGCOH
	LDX Y,MSGCOV
	JSR MESGPOS
	LDY TEMP3
	LDA Y,MSGNUM
	TAY
	JSR VGMSG
	THEN
	DEC TEMP3
	MIEND
	RTS

MSGCOV: .BYTE 0C0,0C0,0C0,0C0,0C0,68,5C,82
	.BYTE 50,44,38,2C,20,14,00,5C
	.BYTE 2C,24,0C8,10,28,0F0,0F0,0F0
	.BYTE 10

MSGCOH: .BYTE 0E0,0E0,0E0,0E0,0E0,7C,0D0,0D0
	.BYTE 0D6,0CA,0C7,0D0,0D0,0D9,18,0E8
	.BYTE 0E0,0DA,0D0,0E8,8,30,0C0,0B8
	.BYTE 0F0

MSGNUM:	.BYTE 0D,0C,0B,9,9,0A,8,7
	.BYTE 0E,0F,10,11,12,13,1C,1D
	.BYTE 1E,1F,20,21,22,23,24,6
	.BYTE 25

	.PAGE
DRANK:	LDA SCLDRH+7
	LDY I,7
	BEGIN
	ASL
	BCS OUT2
	DEY
	EQEND
OUT2:	STY TEMP3
	LDA Y,RANKH
	LDX I,0
	JSR MESGPOS
	LDY TEMP3
	LDA Y,RANKNO
	TAY
	JSR VGMSG
	LDY I,1C
	JSR VGMSG
	RTS

RANKH:	.BYTE 0E6,0E6,0E0,0E8,0DC,0D4,0CC,0D0

RANKNO: .BYTE 14,15,16,17,18,19,1A,1B
	.PAGE

	.SBTTL DISPLAY MESSAGES



DGAMOV:	LDA I,10
	LDX I,5D
	JSR MESGPOS
	LDX LASPLA
	INX
	TXA
	JSR D1DIGT
	JMP STUPID
MPRSTR=0
MGAMOV=1
MOUTFU=2
MSCORE=3
MFUEL =4
MLIVES=5
DMESGE: LDX PLAYUP
	LDA X,STATE
	CMP I,CPAUSE
	BEQ DGAMOV
	CMP I,CATRCT
	IFEQ
	LDA $$CRDT
	IFNE
	LDA I,0E0 
	LDX I,28
	JSR MESGPOS
	LDY I,MPRSTR
	JSR VGMSG
	LDA MINCOI!M1C1PL?0FF
	AND MSGBT3
	ORA I,MCREDS
	ELSE
	LDA I,MINCOI!M1C1PL
	ORA MSGBT3
	AND I,MCREDS?0FF
	THEN
	AND I,MTRFUL!MSHBNK?0FF
	STA MSGBT3
STUPID:	LDA I,0E6
	LDX I,34
	JSR MESGPOS
	LDY I,MGAMOV
	JSR VGMSG
	LDX PLAYUP
	LDA X,FUEL1
	ORA X,FUEL2
	ORA X,FUEL3
	IFEQ
	LDA I,0E0
	LDX I,40
	JSR MESGPOS
	LDY I,MOUTFU
	JSR VGMSG
	JMP SKIP4
	THEN
	
	THEN
	LDX PLAYUP
	LDA X,PLADED
	IFNE
	LDA ATRAFL
	IFNE
	LDA X,FUEL3
	BNE OUT3
	LDA X,FUEL2
	CMP I,20
	IFCC
	LDA I,LOONFU
	ORA MSGBIT
	ELSE
OUT3:	LDA I,LOONFU?0FF
	AND MSGBIT
	THEN
	STA MSGBIT
	THEN
	THEN
SKIP4:	LDX I,74
	LDA I,0F4
	JSR MESGPOS
	LDY I,MSCORE
	JSR VGMSG
	LDX I,68
	LDA I,0F8
	JSR MESGPOS
	LDY I,MFUEL
	JSR VGMSG
	RTS


DCRED:  LDA ATRAFL
	IFEQ
	LDX PLAYUP
	LDA X,STATE
	CMP I,CGETIN
	IFNE
	LDA $$CRDT
	IFNE
	LDX I,11
	LDA I,18
	LDY I,0
	JSR MESGPOS
	LDA $$CRDT
	JSR D1DIGT
	THEN
	LDX PLAYUP
	LDA X,STATE
	CMP I,CATRM1
	IFEQ
	LDA I,0
	LDX I,0DC
	JSR MESGPOS
	LDA I,3
	JSR DSPLTR
	THEN
	THEN
	THEN
	RTS
	.PAGE
	.SBTTL DISPLAY NAME

DNAME:	JSR SETBLU
	LDY LULSCA
	LDA LUBSCA
	JSR VGSCAL
	LXL LUNAR
	LAH LUNAR
	JSR VGJSRL
	JSR SETRED
	LDY BALSCA
	LDA BABSCA
	JSR VGSCAL
	LXL BATTLE
	LAH BATTLE
	JSR VGJSRL
	RTS

DIGIT2: PHA
	PHP
	LSR
	LSR
	LSR
	LSR
	PLP
	JSR DIGITZ
	PLA
	AND I,0F
DIGITZ: BCC 10$
	AND I,0F
	BEQ DBLANK
	CLC
10$:	ASL
	PHP
	JSR DSPCHR
	PLP
	RTS
	.PAGE

DBLANK: LDX VGMSGA
	LDA VGMSGA+1
	PHP
	JSR INJSRL
	PLP
	RTS
	.SBTTL	MULTIPLY

;INPUTS MUPYER:MULTIPLYER
;	MUPCAN:MULTIPLICAND
;
;
;
;
;OUTPUTS:RESL=LO BYTE OF PRODUCT
;	 RESH=HI BYTE OF PRODUCT
;	 A,Y-DESTROYED

MULTIPLY:	LDA I,0
	STA RESL		;0 RESULT
	STA RESH
	LDY I,7			;BIT COUNT
	BEGIN
	ASL RESL		;SHIFT RESULT
	ROL RESH
	ASL MUPYER
	IFCS
	LDA MUPCAN
	CLC
	ADC RESL		;ADD IN MULTIPLICAND
	STA RESL
	IFCS
	INC RESH		;CY INTO HI RESULT
	THEN
	THEN
	DEY
	MIEND
	RTS
	.VCTRS	0EFFA,POWERON,POWERON,IRQ
	HLL65
	.END
                                                                             